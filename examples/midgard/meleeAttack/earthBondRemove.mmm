!rem // earthBondRemove
!rem // GM script: Remove a player-generated earthbond as a special ability of the magic rune blade "Stoneheart"
!rem //
!mmm script
!mmm   set scriptVersion = "earthBondRemove v1.1.0 (2022-02-02)"
!mmm
!rem
!rem // runeEffectEarthBond(weaponWielderID, weaponLabel, targetID, command)
!rem // 
!rem //   CURRENTLY ONLY ALLOWS FOR RELEASE COMMANDS
!rem //   Executes command on weaponWielderID's earth bond capability (from the rune blade weaponLabel) against targetID
!rem // 
!mmm function runeEffectEarthBond(weaponWielderID, weaponLabel, targetID, command)
!mmm   
!mmm   set earthBondToggleCost = 1
!mmm   set deactivateGIF = ""
!mmm
!mmm   if command eq "releaseByCreator"
!mmm   
!mmm     do m3mgdModifyEndurance(-1 * earthBondToggleCost, weaponWielderID.token_id, "AP")
!mmm     combine chat
!mmm       chat: Die Erdfessel gibt ${foeID.name} frei, und plötzlich sieht der Untergrund wieder aus wie vorher, als wäre nichts gewesen.
!mmm       if deactivateGIF ne ""
!mmm         chat: [x](${deactivateGIF})
!mmm       end if
!mmm     end combine
!mmm
!mmm   else if command eq "escapeByPatience"
!mmm
!mmm     set dxRoll = roll("1d100")
!mmm     if isunknown(targetID.Gw)
!mmm       chat: /w GM ${targetID.name} erlaubt keinen Zugriff auf das Attribut **Gw**. **Bitte manuell würfeln (+/-0) und bei Erfolg die beiden Statusmarker entfernen.**
!mmm       return false
!mmm     else if dxRoll > targetID.Gw
!mmm       chat: /w GM **Lösen aus der Erdfessel:** PW:Gw(${targetID.Gw}) ist mit ${dxRoll} **fehlgeschlagen.**
!mmm       chat: ${targetID.name} versucht sich allzu ruckartig von ${weaponWielderID.name}s Erdfessel zu befreien und verstrickt sich damit nur weiter. So ein Mist, verdammter! Und dafür hab ich 5 Minuten verschwendet.
!mmm       return false
!mmm     else
!mmm       combine chat
!mmm         chat: /w GM **Lösen aus der Erdfessel:** PW:Gw(${targetID.Gw}) ist mit ${dxRoll} **gelungen.**
!mmm         chat: ${targetID.name} befreit sich laaaaangsam, aber immerhin elegant von ${weaponWielderID.name}s Erdfessel. Es kann so einfach sein -- das waren 5 gut investierte Minuten!
!mmm         if deactivateGIF ne ""
!mmm           chat: [x](${deactivateGIF})
!mmm         end if
!mmm       end combine
!mmm     end if
!mmm
!mmm   else if command eq "escapeByForce"
!mmm
!mmm     set stRoll = roll("1d100")
!mmm     set foeActOfStrength = roll(targetID.token_id, "@"&"{KraftaktW}")
!mmm     if isunknown(foeActOfStrength)
!mmm       chat: /w GM ${targetID.name} erlaubt keinen Zugriff auf das Attribut **KraftaktW**. **Bitte manuell würfeln (+/-0) und bei Erfolg die beiden Statusmarker entfernen.**
!mmm       return false
!mmm     else if stRoll > foeActOfStrength
!mmm       chat: /w GM **Ausbruch aus Erdfessel:** PW:KraftaktW(${foeActOfStrength}) ist mit ${stRoll} **fehlgeschlagen.**
!mmm       chat: ${targetID.name} versucht sich mit einem schier übermensch(ork?)lichen Kraftakt von ${weaponWielderID.name}s Erdfessel zu befreien und verstrickt sich damit nur weiter. So ein Mist, verdammter!
!mmm       return false
!mmm     else
!mmm       combine chat
!mmm         chat: /w GM **Ausbruch aus Erdfessel:** PW:KraftaktW(${foeActOfStrength}) ist mit ${stRoll} **gelungen.**
!mmm         chat: ${targetID.name} bricht mit einem übermensch(ork?)lichen Kraftakt aus ${weaponWielderID.name}s Erdfessel aus. Muckis, Baby!
!mmm         if deactivateGIF ne "" 
!mmm           chat: [x](${deactivateGIF})
!mmm         end if
!mmm       end combine
!mmm     end if
!mmm
!mmm   end if
!mmm
!mmm   do setattr(targetID, m3mgdEarthBondStatusMarker, false)
!mmm   do setattr(weaponWielderID, m3mgdEarthBondStatusMarker, false)
!mmm   do setattr(weaponWielderID, m3mgdEarthBondPCAttrTargetID, "")
!mmm
!mmm   return true
!mmm
!mmm end function
!mmm
!rem   // Config (if script is used by more than one player, use config scripts to overwrite, otherwise just customize here)
!rem   //  cWeaponLabel         The label of the weapon, must be identical to your combat sheet
!rem   //  deactivateGIF       GIF to play as the earth releases the foe
!mmm   set customizable cCheckVersion = false
!mmm   set customizable cWeaponWielder = "Yerrick MacRothgar"
!mmm   set customizable cWeaponLabel = "Steinherz (Streitaxt)"
!mmm
!mmm   if cCheckVersion
!mmm     do whisperback(scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!mmm   if version < 1.26 or not m3mgdExchange
!mmm     do whisperback("MMM version too low or game-global m3mgdExchange variables missing.")
!mmm     exit script
!mmm   end if
!mmm
!mmm   if not cWeaponWielder.name eq cWeaponWielder
!mmm     do whisperback("Character " & cWeaponWielder & " not accessible or something.")
!mmm     exit script
!mmm   end if
!mmm
!rem   // Retrieve release mode from GM
!mmm   set releaseCommand = "?{Wie wird die Erdfessel entfernt? | Auf Wunsch des Schöpfers,releaseByCreator| Langsam und vorsichtig (5min/PW:Gw),escapeByPatience| Mit Gewalt (einmalig),escapeByForce}"
!mmm
!rem   // Retrieve target ID
!mmm   set foeID = cWeaponWielder.(m3mgdEarthBondPCAttrTargetID)
!mmm   if not foeID or isdenied(foeID.token_id) or isunknown(foeID.token_id) or isdenied(foeID.(m3mgdEarthBondStatusMarker))
!mmm     do whisperback("Target given as token ID '" & foeID & "' may not be a correct token ID, or does not allow access to attributes:" & getreason(foeID))
!mmm     exit script
!mmm   end if
!mmm 
!mmm   if foeID.(m3mgdEarthBondStatusMarker) ne "shown"
!mmm     do whisperback("Token " & foeID.name & " trägt aber keine Erdfessel oder " & m3mgdEarthBondStatusMarker & " wurde schon entfernt.")
!mmm     exit script
!mmm   end if
!mmm
!mmm   do runeEffectEarthBond(cWeaponWielder.token_id, cWeaponLabel, foeID, releaseCommand)
!mmm
!mmm end script