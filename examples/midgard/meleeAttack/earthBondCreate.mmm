!rem // earthBondCreate
!rem // Player script: Create an earthen bond as a special ability of the magic rune blade "Stoneheart"
!mmm script
!mmm   set scriptVersion = "earthBondCreate v1.0.4 (2022-01-14)"
!mmm
!rem   // Config (if script is used by more than one player, use config scripts to overwrite, otherwise just customize here)
!rem   //  cWeaponLabel         The label of the weapon, must be identical to your combat sheet
!rem   //  cWeaponToggleCost    sender.AP cost (AP) to activating the weapon's magic
!mmm   set customizable cCheckVersion = false
!mmm   set customizable cWeaponLabel = "Steinherz (Streitaxt)"
!mmm   set customizable cWeaponName = "Steinherz"
!mmm   set customizable cWeaponToggleCost = 1
!mmm   set customizable cOwnID = sender.token_id
!mmm   set customizable cActivateGIF = ""
!mmm
!mmm   if cCheckVersion
!mmm     do whisperback(scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
%{MacroSheetLibrary|libMidgardGlobal}
%{MacroSheetLibrary|libMidgardFunctions}
!mmm
!rem   // Check if cOwnID points to a character I control
!mmm   if isdenied(cOwnID.character_id) or cOwnID.permission ne "control"
!mmm     do whisperback("Abbruch: " & getreason(cOwnID)) 
!mmm     exit script
!mmm   end if
!mmm
!rem   // Identify target (user points to token)
!mmm   set foeID = "@{target|Angriffsziel|token_id}"
!mmm   if isunknown(foeID.token_id) or not (foeID.permission eq "view" or foeID.permission eq "control")
!mmm     do whisperback("Abbruch: " & foeID.name & " " & foeID.permission & " " & getreason(foeID.token_id)) 
!mmm     exit script
!mmm   end if
!mmm 
!rem   // Process exclusionary conditions and outcomes
!mmm   
!rem   // - Check for melee range (<=1 grid cell) and orientation
!mmm   set myViewAngle = m3mgdGetViewAngle(cOwnID, foeID)
!mmm   if (myViewAngle < -90) or (myViewAngle > 90)
!mmm     do whisperback(foeID.name & " ist außerhalb meines Blickfeldes (" & myViewAngle & "°). Schade.")
!mmm     exit script
!mmm   end if
!mmm   set foeIDViewAngle = m3mgdGetViewAngle(foeID, cOwnID)
!mmm   set distance = m3mgdGetDistance(cOwnID, foeID)
!mmm   set distanceUnits = distunits() 
!mmm   if (distance/(distsnap()*distscale())) > 0
!mmm     do whisperback(foeID.name & " ist zu weit entfernt, um eine Erdfessel auszulösen (max. 1m). Schade.")
!mmm     exit script
!mmm   end if
!mmm   
!rem   // - Character exhausted => earthbond cannot work
!mmm   if cWeaponToggleCost > 0 and cOwnID.AP <= 0
!mmm     do whisperback("Nichts passiert, außer dass ein einsamer Schweißtropfen an meinem Haupt hinunterrollt.")
!mmm   
!rem   // - Character does not wield the magic weapon Stoneheart
!mmm   else if not findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeEW")
!mmm     do whisperback("WO IST DIE ERDKLINGE?! Ich scheine sie jedenfalls nicht zu führen.")
!mmm   
!rem   // - Character is maintaining another earthbond => no second earthbond possible
!mmm   else if cOwnID.(m3mgdEarthBondStatusMarker)
!mmm     do whisperback("Nichts passiert. Ich hab ja noch eine andere aktive Erdfessel, ich Dussel!")
!mmm   
!rem   // - No exclusionary conditions: begin creating a new earthbond (to be completed by GM)
!mmm   else
!mmm   
!mmm     chat: Ich richte *${cWeaponName}* auf den Boden vor ${foeID.name}.
!mmm   
!mmm     if cWeaponToggleCost > 0
!mmm       do m3mgdModifyEndurance(-1 * cWeaponToggleCost, cOwnID, "AP")
!mmm       chat: /w "${cOwnID.name}" Das hat ${cWeaponToggleCost} AP gekostet.
!mmm     end if
!mmm   
!rem     // Store target for use by earthBondCreate and earthBondRemove scripts
!mmm     do m3mgdExchangeStoreAttack("magic", "runeBladeEarthBond")
!mmm   
!rem     // Confirm creation to GM and player
!mmm     combine chat
!mmm       chat: /w gm ${cOwnID.name} hat eine magische Erdfessel gegen **${foeID.name}** erzeugt. (Kosten: ${cWeaponToggleCost} AP, schon abgezogen.)
!mmm       chat: Die Erdfessel wirkt erst, wenn du [**ANWENDEN**](~earthBondApply) klickst.
!mmm     end combine
!mmm     chat: /w "${cOwnID.name}" Du hast die Erdfessel gegen **${foeID.name}** erzeugt (${cWeaponToggleCost} AP abgezogen), jetzt muss sie der Spielleiter noch bestätigen...
!mmm   
!mmm   end if
!mmm   
!rem   // If this act exhausted the character, let them and the GM know
!mmm   if cOwnID.AP <= 0
!mmm     chat: /w "${cOwnID.name}" Ich bin erschöpft (**AP=0**).
!mmm     chat: /w gm ${cOwnID.name} ist nun erschöpft (**AP=0**).
!mmm   end if
!mmm   
!mmm end script