!rem // Midgard Melee Attack Script
!rem //
!mmm script
!mmm   set scriptVersion = "v1.8.1 (2021-05-19)"
!rem
!rem   // MMM compatibility check: die if MMM version too low
!mmm   if version < 1.18
!mmm     do whisperback("Abbruch: MMM-Version 1.18.0 oder h√∂her erforderlich.")
!mmm     exit script
!mmm   end if
!rem
!rem   // Config: set defaults for customizable variables
!rem   //
!rem   //  cCheckVersion        Toggle version check
!rem   //  cVerbose             Toggle narrative output: true for on, false for off (made for oeGStefan)
!rem   //  cWeaponLabel         The label of the weapon, must be identical to your combat sheet
!rem   //  cWeaponName          Optional: The name of a named weapon, e.g. "Sting"
!rem   //  cWeaponType          The type of weapon for narration, the "sword" in "I'm swinging my sword Glamdring"
!rem   //  cMagicExtraDamageMarker   Attribute in which activity status (true/false) of the weapon's magic is stored
!rem   //  cMagicExtraDamage    Damage roll expression ("1d6+x") for additional magic damage
!rem   //  cFoeNameDefault      Output label for targets without names
!rem   //  cOwnID               Override if the script should work from a sender not identical with the character
!rem   //
!mmm   set customizable cCheckVersion = false
!mmm   set customizable cVerbose = true
!mmm   set customizable cWeaponLabel = "Kurzschwert"
!mmm   set customizable cWeaponName = ""
!mmm   set customizable cWeaponType = "Kurzschwert"
!mmm   set customizable cMagicExtraDamageMarker = ""
!mmm   set customizable cMagicExtraDamage = ""
!mmm   set customizable cMagicExtraDamageLabel = "" 
!mmm   set customizable cFoeNameDefault = "Fiesling ohne Namen"
!mmm   set customizable cOwnID = getattr(sender, "character_id")
!mmm
!mmm   if cCheckVersion
!mmm     do whisperback("Midgard Melee Attack Script " & scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!rem   // Refetch character_id to ensure access in case of erroneous override in customize block
!mmm   set cOwnID = getattr(cOwnID, "character_id")
!mmm   if isdenied(cOwnID)
!mmm     do whisperback("Abbruch: Keine Zuordnung zu einem zugreifbaren Charakter m√∂glich - " & cOwnID) 
!mmm     exit script
!mmm   end if
!rem
!rem   // Init
!rem   // - Constants
!mmm   set emojiExhausted = "ü•¥"
!mmm   set emojiLock = "üîí"
!mmm   set emojiMagic = "‚ú®"
!mmm   set emojiKnife = "üî™"
!rem   // - Character & NPC data
!mmm   set ownName = getattr(cOwnID, "name")
!mmm   set effAttack = getattr(cOwnID, findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeEW"))
!mmm   set weaponDamageExpr = getattr(cOwnID, findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeSchaden"))
!mmm   set weaponDamageBonus = getattr(cOwnID, findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeEfSchB"))
!mmm   if weaponDamageBonus >= 0
!mmm     set weaponDamageBonus = "+" & weaponDamageBonus
!mmm   end if
!mmm   set magicWeapon = (getattr(cOwnID, findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeSchB")) > 0) or (getattr(cOwnID, findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeAngB")) > 0)
!mmm   if (not effAttack) or (not weaponDamageExpr)
!rem     // Most probably, a weapon called cWeaponLabel does not exist, so the script cannot work
!mmm     do whisperback("Abbruch: " & ownName & " hat keine Waffe namens " & cWeaponLabel & ".")
!mmm     exit script
!mmm   end if
!mmm   set magicExtraDamageActive = (magicWeapon and getattr(cOwnID, cMagicExtraDamageMarker) eq "shown")
!mmm   set foeId = "@{target|Angriffsziel|token_id}"
!mmm   set foeName = getattr(foeId, "name")
!mmm   if isdenied(foeName) or isunknown(foeName)
!mmm     set foeName = cFoeNameDefault
!mmm   end if
!mmm   set ownRotation = round(getattr(cOwnID, "rotation")) % 360
!mmm   set foeRotation = round(getattr(foeId, "rotation")) % 360
!mmm   set foeEndurance = getattr(foeId, "bar2")
!mmm
!rem   // Check for melee range (<=1 grid cell) and orientation, prepare calculations for attack from behind
!mmm   set offsetYfromFoe = round((getattr(cOwnID, "top")  - getattr(foeId, "top"))  * distscale())
!mmm   set offsetXfromFoe = round((getattr(cOwnID, "left") - getattr(foeId, "left")) * distscale())
!mmm   set ownDirectionFromFoe = round(atan(offsetYfromFoe, offsetXfromFoe)) - 90
!mmm   set foeDirectionFromMe = round(atan(-offsetYfromFoe, -offsetXfromFoe)) - 90
!mmm   set myViewAngle = (((foeDirectionFromMe - ownRotation) + 180) % 360 + 360) % 360 - 180
!mmm   set foeViewAngle = (((ownDirectionFromFoe - foeRotation) + 180) % 360 + 360) % 360 - 180
!mmm   if (myViewAngle < -90) or (myViewAngle > 90)
!mmm     chat [OutsideFieldOfVision]: $[targetName]{foeName} ist au√üerhalb meines Blickfeldes ($[viewAngle]{myViewAngle}¬∞). Schade.
!mmm     exit script
!mmm   end if
!mmm   set absTokenDistY = round(((abs(getattr(cOwnID, "top")  - getattr(foeId, "top")))  - .5 * getattr(cOwnID, "height") - .5 * getattr(foeId, "height")) * distscale())
!mmm   set absTokenDistX = round(((abs(getattr(cOwnID, "left") - getattr(foeId, "left"))) - .5 * getattr(cOwnID, "width")  - .5 * getattr(foeId, "width"))  * distscale())
!mmm   set distance = max(absTokenDistY, absTokenDistX)
!mmm   set distanceUnits = distunits() 
!mmm   if (distance/(distsnap()*distscale())) > 0
!mmm     chat [OutsideStrikeDistance]: $[targetName]{foeName} ist zu weit entfernt f√ºr mein $[weaponType]{cWeaponType} * $[weaponName]{cWeaponName} * ($[weaponLabel]{cWeaponLabel}). Schade.
!mmm     exit script
!mmm   end if
!mmm      
!rem   // Collect attack modifiers
!mmm   set modifiers = 0
!mmm   set autoModifiers = 0
!mmm   set modifierLog = ""
!mmm   set modifierTooltip = ""
!rem   // 1. Attacker exhausted
!mmm   if getattr(cOwnID, "AP") <= 0
!mmm     set autoModifiers = autoModifiers - 4
!mmm     set modifierLog = modifierLog & " {{Angreifer ersch√∂pft ([" & ownName & "].AP:0)=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Angreifer&nbsp;ersch√∂pft&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!rem   // 2. Target exhausted
!mmm   if isdenied(foeEndurance) 
!mmm     set modifierLog = modifierLog & " {{[" & foeName & "].AP&nbsp;" & emojiLock & "=**ggf. EW+2**}}"
!mmm     set modifierTooltip = modifierTooltip & " Ziel.AP&nbsp;" & emojiLock & "=ggf. EW+2 "
!mmm   else if foeEndurance <= 0
!mmm     set autoModifiers = autoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{Ziel ersch√∂pft=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Ziel&nbsp;ersch√∂pft&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!rem   // 3. Attack from behind
!mmm   if (foeViewAngle < -90) or (foeViewAngle > 90)
!mmm     set autoModifiers = autoModifiers + 2
!mmm     set modifierLog = modifierLog & " {{Angriff von hinten=+2}} "
!mmm     set modifierTooltip = modifierTooltip & " Angriff&nbsp;von&nbsp;hinten&nbsp;&nbsp;&nbsp;+2 "
!mmm   end if
!rem   // 4. Collect additional standard modifiers from user via dropdown menu
!mmm   set semiAutoModCode = "?{Standard-Angriffsmodifikatoren|   Normaler Angriff +/-0, ?{Normaler Angriff +/- ... &#124;      NORMAL HALT +/-0              &#44; 1 &#124;      Angriff von oben (70cm+) +2   &#44; 2 &#124;      Liegendes Ziel (ich stehe) +4 &#44; 3 &#124;      Wehrloses Ziel +4             &#44; 5 &#124;      Panisch fliehendes Ziel +4    &#44; 7   &#125; |   Spontaner Angriff -4, ?{Spontaner Angriff -4 +/- ... &#124;      Nix weiter +/-0               &#44; 11 &#124;      Angriff von oben (70cm+) +2   &#44; 22 &#124;      Liegendes Ziel (ich stehe) +4 &#44; 33 &#124;      Wehrloses Ziel +4             &#44; 55 &#124;      Panisch fliehendes Ziel +4    &#44; 77   &#125; |   √úberst√ºrzter Angriff -6, ?{√úberst√ºrzter Angriff -6 +/- ... &#124;      Nix weiter +/-0               &#44; 13 &#124;      Angriff von oben (70cm+) +2   &#44; 26 &#124;      Liegendes Ziel (ich stehe) +4 &#44; 39 &#124;      Wehrloses Ziel +4             &#44; 65 &#124;      Panisch fliehendes Ziel +4    &#44; 91   &#125; |   V√∂llige Dunkelheit/Blendung -6, ?{V√∂llige Dunkelheit/Blendung -6 +/- ... &#124;      Nix weiter +/-0               &#44; 17 &#124;      Angriff von oben (70cm+) +2   &#44; 34 &#124;      Liegendes Ziel (ich stehe) +4 &#44; 51 &#124;      Wehrloses Ziel +4             &#44; 85 &#124;      Panisch fliehendes Ziel +4    &#44; 119 &#124;       Spontaner Angriff -4 &#44; ?{V√∂llige Dunkelheit/Blendung -6 Spontaner Angriff -4 +/- ... &amp;#124;         Das war alles               &amp;#44; 187 &amp;#124;         Angriff von oben (70cm+) +2 &amp;#44; 374 &amp;#124;         Liegendes Ziel (ich stehe) +4 &amp;#44; 561 &amp;#124;         Wehrloses Ziel +4           &amp;#44; 935 &amp;#124;         Panisch fliehendes Ziel +4  &amp;#44; 1309       &amp;#125; &#124;       √úberst√ºrzter Angriff -6 &#44; ?{V√∂llige Dunkelheit/Blendung -6 √úberst√ºrzter Angriff -6 +/- ... &amp;#124;        Das war alles               &amp;#44; 221 &amp;#124;        Angriff von oben (70cm+) +2 &amp;#44; 442 &amp;#124;        Liegendes Ziel (ich stehe) +4 &amp;#44; 663 &amp;#124;        Wehrloses Ziel +4           &amp;#44; 1105 &amp;#124;        Panisch fliehendes Ziel +4  &amp;#44; 1547       &amp;#125;    &#125; }"
!mmm   if semiAutoModCode % 17 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers - 6
!mmm     set modifierLog = modifierLog & " {{V√∂llige Dunkelheit / geblendet=-6}} "
!mmm     set modifierTooltip = modifierTooltip & " V√∂llige&nbsp;Dunkelheit/geblendet&nbsp;&nbsp;&nbsp;-6 "
!mmm   end if
!mmm   if semiAutoModCode % 13 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers - 6
!mmm     set modifierLog = modifierLog & " {{√úberst√ºrzter Angriff=-6}} "
!mmm     set modifierTooltip = modifierTooltip & " √úberst√ºrzter&nbsp;Angriff&nbsp;&nbsp;&nbsp;-6 "
!mmm   end if
!mmm   if semiAutoModCode % 11 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers - 4
!mmm     set modifierLog = modifierLog & " {{Spontaner Angriff=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Spontaner&nbsp;Angriff&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!mmm   if semiAutoModCode % 7 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{Ziel flieht panisch=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Ziel&nbsp;flieht&nbsp;panisch&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!mmm   if semiAutoModCode % 5 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{Ziel wehrlos=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Ziel&nbsp;wehrlos&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!mmm   if semiAutoModCode % 3 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{Ziel liegt=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Ziel&nbsp;liegt&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!mmm   if semiAutoModCode % 2 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 2
!mmm     set modifierLog = modifierLog & " {{Angriff von oben=+2}} "
!mmm     set modifierTooltip = modifierTooltip & " Angriff&nbsp;von&nbsp;oben&nbsp;&nbsp;&nbsp;+2 "
!mmm   end if
!rem   // Ask for individual modifiers and sum up the various modifiers
!mmm   set manualModifiers = "?{Weitere spezielle Angriffsmodifikatoren|0}"
!mmm   if manualModifiers != 0
!mmm     if manualModifiers > 0
!mmm       set manualModifiers = "+" & manualModifiers
!mmm     end if
!mmm     set modifierLog = modifierLog & " {{Benutzereingabe=" & manualModifiers & "}} "
!mmm     set modifierTooltip = modifierTooltip & " Benutzereingabe&nbsp;&nbsp;&nbsp;" & manualModifiers & " "
!mmm   end if
!mmm   set modifiers = modifiers + autoModifiers + semiAutoModifiers + manualModifiers
!rem
!rem   // Run & format rolls
&{template:none} [[1d20]] {{}}
!mmm   set attackRoll = $[[0]]
!mmm   set attackResult = attackRoll + effAttack + modifiers
!mmm   if modifiers == 0
!mmm     set modifiers = "+/-0"
!mmm   else if modifiers > 0
!mmm     set modifiers = "+" & modifiers
!mmm   end if
!mmm   if modifierLog eq ""
!mmm     set modifierLog = "{{Boni/Mali=keine}}"
!mmm     set modifierTooltip = " Boni/Mali&nbsp;&nbsp;&nbsp;keine "
!mmm   end if
!mmm   set modifiers = highlight(modifiers, "info", modifierTooltip)
!mmm   set attackResultLog = attackResult & " = (1d20 = " & attackRoll & ") + (EW = " & effAttack & ") + (" & modifiers & ")"
!mmm   if iscritical(attackRoll)
!mmm     set attackResult = highlight(attackResult, "good", attackResultLog)
!mmm   else if isfumble(attackRoll)
!mmm     set attackResult = highlight(attackResult, "bad", attackResultLog)
!mmm   else 
!mmm     set attackResult = highlight(attackResult, "normal", attackResultLog)
!mmm   end if
!rem
!rem   // Damage roll (cannot be lower than 0)
!mmm   set damageResult = roll(weaponDamageExpr & weaponDamageBonus)
!mmm   set damageResultLog = "(" & weaponDamageExpr & " = " & (damageResult - weaponDamageBonus) & ") " & weaponDamageBonus
!mmm   if damageResult < 0
!mmm     set damageResult = highlight(0, "bad")
!mmm   end if
!mmm   if magicExtraDamageActive and cMagicExtraDamage ne ""
!mmm     set magicExtraDamageRoll = roll(cMagicExtraDamage)
!mmm     if magicExtraDamageRoll < 0
!mmm       set magicExtraDamageRoll = highlight(0, "bad")
!mmm     end if
!mmm     set damageResult = highlight(damageResult + magicExtraDamageRoll, "normal")
!mmm     set damageResultLog = damageResultLog & " + (" & cMagicExtraDamage & " = " & magicExtraDamageRoll & ")"
!mmm   end if 
!mmm   set damageResultLog = damageResult & " = " & damageResultLog
!mmm   set damageResult = highlight(damageResult, default, damageResultLog)
!mmm
!rem   // Break down magic effects for  player and GM
!mmm   if magicExtraDamageActive and attackResult >= 20 and damageResult > 0
!mmm     set modifierLog = modifierLog & " {{Zauberwaffe=**" & (damageResult - magicExtraDamageRoll) & "**&nbsp;" & emojiKnife & emojiMagic & " +&nbsp;**" & magicExtraDamageRoll & "**&nbsp;" & cMagicExtraDamageLabel & "}} "
!mmm   else if magicWeapon and attackResult >= 20 and damageResult > 0
!mmm     set modifierLog = modifierLog & " {{Zauberwaffe=**" & damageResult & "**&nbsp;" & emojiKnife & emojiMagic & " }} "
!mmm   end if
!mmm
!rem   // Pull together a sensible guess at a default way of addressing the weapon for output
!mmm   if cWeaponName eq ""
!mmm     set outputWeaponLabel = cWeaponLabel
!mmm   else
!mmm     set outputWeaponLabel = cWeaponType & " *" & cWeaponName & "*"
!mmm   end if
!mmm
!rem   // Prepare visual indicator of exhaustion or lack to access to endurance attribute for output
!mmm   if isdenied(foeEndurance)
!mmm     set foeEnduranceIndicator = highlight(emojiLock, "bad", foeName & ".AP: Zugriff verweigert")
!mmm   else if foeEndurance <= 0
!mmm     set foeEnduranceIndicator = highlight(emojiExhausted, "info", foeName & ".AP:0")
!mmm   else
!mmm     set foeEnduranceIndicator = ""
!mmm   end if 
!mmm
!mmm   if cVerbose
!mmm     combine chat
!mmm       if not magicExtraDamageActive
!mmm         chat [AttackOpening]:                          Mein Angriff mit $[weaponLabel]{outputWeaponLabel} ($[weaponType]{cWeaponType} "$[weaponName]{cWeaponName}") auf $[targetName]{foeName} 
!mmm       else
!mmm         chat [AttackOpeningExtraMagicDamage]:          Mein Angriff mit $[weaponLabel]{outputWeaponLabel} ($[weaponType]{cWeaponType} "$[weaponName]{cWeaponName}", magischer Zusatzschaden aktiviert) auf $[targetName]{foeName} 
!mmm       end if
!mmm       if iscritical(attackRoll)
!mmm         chat [AttackIsCriticalSuccess]:                ist ein kritischer Erfolg ($[attackResult]{attackResult+0}). **Kritischer Erfolg: PP pr√ºfen!**
!mmm       else if isfumble(attackRoll)
!mmm         chat [AttackIsCriticalFailure]:                ist ein **peinlicher Patzer**. 
!mmm       else if attackResult >= 22 and damageResult > 3
!mmm         chat [AttackSucceedsWell]:                     scheint ein ordentlicher Treffer zu werden ($[attackResult]{attackResult+0}).
!mmm       else if attackResult >= 20 
!mmm         chat [AttackSucceedsBarely]:                   trifft gerade so ($[attackResult]{attackResult+0}).
!mmm       else if attackResult >= 18
!mmm         chat [AttackFailsBarely]:                      verfehlt knapp ($[attackResult]{attackResult+0}). 
!mmm       else
!mmm         chat [AttackFailsClearly]:                     verfehlt erheblich ($[attackResult]{attackResult+0}). 
!mmm       end if
!mmm       if attackResult >= 20
!mmm         if damageResult > 10
!mmm           set damageDescription = "ausgesprochen schwerer, unter Umst√§nden lebensgef√§hrlicher"
!mmm         else if damageResult > 3
!mmm           set damageDescription = "schmerzhafter"
!mmm         else if damageResult > 0
!mmm           set damageDescription = "leichter"
!mmm         else 
!mmm           set damageDescription = "bestenfalls kosmetischer"
!mmm         end if
!mmm         if not magicExtraDamageActive
!mmm           chat [AttackSuccessClosing]:                 $[targetName]{foeName} erwischt ein $[damageDescription]{damageDescription} Treffer, wenn die Abwehr nicht gelingt.
!mmm         else
!mmm           chat [AttackSuccessClosingExtraMagicDamage]: $[targetName]{foeName} erwischt ein $[damageDescription]{damageDescription} Treffer, wenn die Abwehr nicht gelingt.
!mmm         end if
!mmm       else
!mmm         chat [AttackFailureClosing]:                   $[targetName]{foeName} war zu keinem Zeitpunkt in Gefahr.
!mmm       end if
!mmm     end combine
!mmm   end if
!rem
!rem   // Add data table for easy reference
!mmm   combine chat
!mmm     chat:      ${"&"}{template:default} {{name=Nahkampfangriff mit $[weaponLabel]{outputWeaponLabel}}} 
!mmm     chat:      {{Ziel=**$[targetName]{foeName}** ${foeEnduranceIndicator}}}
!mmm     if attackResult >= 20
!mmm       chat:    {{Erfolg=$[attackResult]{attackResult} (**Wurf**&nbsp;$[attackRoll]{attackRoll})}}
!mmm     else 
!mmm       chat:    {{Fehlschlag=$[attackResult]{attackResult} (**Wurf**&nbsp;$[attackRoll]{attackRoll})}}
!mmm     end if
!mmm     chat:      {{Boni/Mali=**$[attackModifiers]{modifiers}**}}
!mmm     if attackResult >= 20
!mmm       set damageType = emojiKnife
!mmm       if magicWeapon and damageResult > 0
!mmm         set damageType = damageType & emojiMagic
!mmm       end if 
!mmm       chat:    {{Schaden=$[damageResult]{damageResult} **LP/AP** ${damageType}}}
!mmm     end if
!mmm   end combine
!mmm   chat: /w GM ${"&"}{template:default} ${modifierLog}
!mmm   chat: /w "${ownName}" ${"&"}{template:default} ${modifierLog}
!mmm   if iscritical(attackRoll) and foeEndurance > 0
!mmm     chat: /w "${ownName}"  ***Nicht vergessen: PP einstecken!*** [x](https://media.giphy.com/media/hKafco7mFwBioBxqFT/giphy.gif)
!mmm     chat: /w GM            ***Nicht vergessen: PP einstecken!*** [x](https://media.giphy.com/media/hKafco7mFwBioBxqFT/giphy.gif)
!mmm   else if iscritical(attackRoll)
!mmm     chat: /w "${ownName}" Leider kein PP, da der Gegner ersch√∂pft ist (AP <= 0).
!mmm   end if
!mmm end script