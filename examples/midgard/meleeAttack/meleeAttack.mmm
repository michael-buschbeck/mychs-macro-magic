!rem // Midgard Melee Attack Script
!rem //
!mmm script
!mmm   set scriptVersion = "v1.13.1 (2022-01-24)"
!rem
!mmm   set customizable cCheckVersion = false
!mmm   if cCheckVersion
!mmm     do whisperback("Midgard Melee Attack Script " & scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!rem   // MMM compatibility check: die if MMM version too low
!mmm   if version < 1.26
!mmm     do whisperback("Abbruch: MMM-Version 1.26.0 oder h√∂her erforderlich.")
!mmm     exit script
!mmm   end if
!mmm
!mmm   if not m3mgdExchange
!mmm     do whisperback("Game-global m3mgdExchange variables missing.")
!mmm     exit script
!mmm   end if
!rem
!rem   // Config: set defaults for customizable variables
!rem   //
!rem   //  cVerbose             Toggle narrative output: true for on, false for off (made for oeGStefan)
!rem   //  cWeaponLabel         The label of the weapon, must be identical to your combat sheet
!rem   //  cWeaponName          Optional: The name of a named weapon, e.g. "Sting"
!rem   //  cWeaponType          The type of weapon for narration, the "sword" in "I'm swinging my sword Glamdring"
!rem   //  cMagicExtraDamageMarker   Attribute in which activity status (true/false) of the weapon's magic is stored
!rem   //  cMagicExtraDamage    Damage roll expression ("1d6+x") for additional magic damage
!rem   //  cFoeNameDefault      Output label for targets without names
!rem   //
!mmm   set customizable cVerbose = true
!mmm   set customizable cWeaponLabel = ""
!mmm   set customizable cWeaponName = ""
!mmm   set customizable cWeaponType = ""
!mmm   set customizable cMagicExtraDamageMarker = ""
!mmm   set customizable cMagicExtraDamage = ""
!mmm   set customizable cMagicExtraDamageLabel = "" 
!mmm   set customizable cFoeNameDefault = "Fiesling ohne Namen"
!rem
!rem   //  cOwnID               Override if the script should work from a sender not identical with the character
!rem   //  cHealthAttr          Override if the script operates on a token for a generic NPC
!rem   //  cEnduranceAttr       Override if the script operates on a token for a generic NPC
!rem   //  cGMSilentMode        true/false: override if GM operates this script and wants to keep it silent
!rem   //  cAttackRoll          Override by player to see beautifully animated 3D dice
!mmm   set customizable cOwnID = sender.token_id
!mmm   set customizable cHealthAttr = "LP"
!mmm   set customizable cEnduranceAttr = "AP"
!mmm   set customizable cGMSilentMode = false
!mmm   set customizable cAttackRoll = roll("1d20")
!mmm
!rem   // Refetch token_id to ensure access in case of erroneous override in customize block
!mmm   set cOwnID = cOwnID.token_id
!mmm   if isdenied(cOwnID)
!mmm     do whisperback("Abbruch: Keine Zuordnung zu einem zugreifbaren Charakter m√∂glich - " & cOwnID) 
!mmm     exit script
!mmm   end if
!rem
!rem   // Override chat sender for characters unconnected to the player (usually NPCs)
!mmm   if cOwnID ne sender
!mmm     set sender = cOwnID
!mmm   end if
!rem
!rem   // Constants
!rem
!mmm   set emojiLock = "üîí"
!mmm   set emojiMagic = "‚ú®"
!mmm   set emojiKnife = "üî™"
!mmm
!mmm   set whirlWindMagicMarker = "status_half_haze"
!mmm
!rem   // Validate attack weapon and request manual entry if selection is missing
!mmm
!mmm   if cWeaponLabel eq "" or not cOwnID.(findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeEW"))
!mmm     set weaponsList = m3mgdListMeleeAttackWeapons(cOwnID)
!mmm     if not weaponsList
!mmm       do whisperback(cOwnID.name & " hat keine Nahkampfwaffen bzw. -f√§higkeiten.")
!mmm       exit script
!mmm     else if count(weaponsList) == 1
!mmm       set cWeaponLabel = cOwnID.(weaponsList)
!mmm     else if cWeaponLabel ne ""
!mmm       do whisperback("Fehler: " & cOwnID.name & " hat keine Nahkampfwaffe " & cWeaponLabel & ".")
!mmm       exit script
!mmm     else 
!mmm       do whisperback("Fehler: " & cOwnID.name & " hat mehrere Nahkampfwaffen. Bitte per Konfigskript ausw√§hlen.")
!mmm       exit script
!mmm     end if
!mmm   end if
!mmm
!rem   // Initialize attack weapon properties
!mmm
!mmm   set effAttack = cOwnID.(findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeEW"))
!mmm   set weaponDamageExpr = cOwnID.(findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeSchaden"))
!mmm   set weaponDamageBonus = cOwnID.(findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeEfSchB"))
!mmm   if weaponDamageBonus >= 0
!mmm     set weaponDamageBonus = "+" & weaponDamageBonus
!mmm   end if
!mmm   set magicWeapon = (cOwnID.(findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeSchB")) > 0) or (cOwnID.(findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeAngB")) > 0)
!mmm   set magicExtraDamageActive = (magicWeapon and getattr(cOwnID, cMagicExtraDamageMarker) eq "shown")
!mmm
!rem   // Initialize target token
!mmm
!mmm   set foeID = "@{target|Angriffsziel|token_id}"
!mmm   set foeName = foeID.name
!mmm   if isdenied(foeName) or isunknown(foeName)
!mmm     set foeName = cFoeNameDefault
!mmm   end if
!mmm   set foeEndurance = foeID.bar2
!mmm
!rem   // Check for melee range (<=1 grid cell) and orientation, prepare calculations for attack from behind
!rem
!mmm   set myViewAngle = m3mgdGetViewAngle(cOwnID, foeID)
!mmm   if (myViewAngle < -90) or (myViewAngle > 90)
!mmm     chat [OutsideFieldOfVision]: $[targetName]{foeName} ist au√üerhalb meines Blickfeldes ($[viewAngle]{myViewAngle}¬∞). Schade.
!mmm     exit script
!mmm   end if
!mmm   set foeViewAngle = m3mgdGetViewAngle(foeID, cOwnID)
!mmm   set distance = m3mgdGetDistance(cOwnID, foeID)
!mmm   set distanceUnits = distunits() 
!mmm   if (distance/(distsnap()*distscale())) > 0
!mmm     chat [OutsideStrikeDistance]: $[targetName]{foeName} ist zu weit entfernt f√ºr mein $[weaponType]{cWeaponType} * $[weaponName]{cWeaponName} * ($[weaponLabel]{cWeaponLabel}). Schade.
!mmm     exit script
!mmm   end if
!mmm      
!rem   // Collect attack modifiers
!rem
!mmm   set modifiers = 0
!mmm   set autoModifiers = 0
!mmm   set modifierLog = ""
!mmm   set modifierTooltip = ""
!rem   // 1. Attacker exhausted
!mmm   if cOwnID.(cEnduranceAttr).max > 0 and cOwnID.(cEnduranceAttr) <= 0
!mmm     set autoModifiers = autoModifiers - 4
!mmm     set modifierLog = modifierLog & " {{Angreifer ersch√∂pft ([" & cOwnID.name & "].AP:0)=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Angreifer&nbsp;ersch√∂pft&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!rem   // 2. Target exhausted
!mmm   if isdenied(foeEndurance) 
!mmm     set modifierLog = modifierLog & " {{[" & foeName & "].AP&nbsp;" & emojiLock & "=**ggf. EW+2**}}"
!mmm     set modifierTooltip = modifierTooltip & " Ziel.AP&nbsp;" & emojiLock & "=ggf. EW+2 "
!mmm   else if foeID.bar2.max > 0 and foeEndurance <= 0
!mmm     set autoModifiers = autoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{Ziel ersch√∂pft=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Ziel&nbsp;ersch√∂pft&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!rem   // 3. Attack from behind
!mmm   if (foeViewAngle < -90) or (foeViewAngle > 90)
!mmm     set autoModifiers = autoModifiers + 2
!mmm     set modifierLog = modifierLog & " {{Angriff von hinten=+2}} "
!mmm     set modifierTooltip = modifierTooltip & " Angriff&nbsp;von&nbsp;hinten&nbsp;&nbsp;&nbsp;+2 "
!mmm   end if
!rem   // 4. Finn's magic sword Whirlwind
!mmm   if (foeName eq "Finn" or foeName eq "Finn MacRathgar") and foeID.(whirlWindMagicMarker) eq "shown"
!mmm     set autoModifiers = autoModifiers - 4
!mmm     set modifierLog = modifierLog & " {{Wirbelwinds Wirbel-Winde=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Wirbelwinde&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!rem   // 5. Collect additional standard modifiers from user via dropdown menu
!mmm   set semiAutoModCode = "?{Standard-Angriffsmodifikatoren|   Normaler Angriff +/-0, ?{Normaler Angriff +/- ... &#124;      NORMAL HALT +/-0              &#44; 1 &#124;      Angriff von oben (70cm+) +2   &#44; 2 &#124;      Liegendes Ziel (ich stehe) +4 &#44; 3 &#124;      Wehrloses Ziel +4             &#44; 5 &#124;      Panisch fliehendes Ziel +4    &#44; 7   &#125; |   Spontaner Angriff -4, ?{Spontaner Angriff -4 +/- ... &#124;      Nix weiter +/-0               &#44; 11 &#124;      Angriff von oben (70cm+) +2   &#44; 22 &#124;      Liegendes Ziel (ich stehe) +4 &#44; 33 &#124;      Wehrloses Ziel +4             &#44; 55 &#124;      Panisch fliehendes Ziel +4    &#44; 77   &#125; |   √úberst√ºrzter Angriff -6, ?{√úberst√ºrzter Angriff -6 +/- ... &#124;      Nix weiter +/-0               &#44; 13 &#124;      Angriff von oben (70cm+) +2   &#44; 26 &#124;      Liegendes Ziel (ich stehe) +4 &#44; 39 &#124;      Wehrloses Ziel +4             &#44; 65 &#124;      Panisch fliehendes Ziel +4    &#44; 91   &#125; |   V√∂llige Dunkelheit/Blendung -6, ?{V√∂llige Dunkelheit/Blendung -6 +/- ... &#124;      Nix weiter +/-0               &#44; 17 &#124;      Angriff von oben (70cm+) +2   &#44; 34 &#124;      Liegendes Ziel (ich stehe) +4 &#44; 51 &#124;      Wehrloses Ziel +4             &#44; 85 &#124;      Panisch fliehendes Ziel +4    &#44; 119 &#124;       Spontaner Angriff -4 &#44; ?{V√∂llige Dunkelheit/Blendung -6 Spontaner Angriff -4 +/- ... &amp;#124;         Das war alles               &amp;#44; 187 &amp;#124;         Angriff von oben (70cm+) +2 &amp;#44; 374 &amp;#124;         Liegendes Ziel (ich stehe) +4 &amp;#44; 561 &amp;#124;         Wehrloses Ziel +4           &amp;#44; 935 &amp;#124;         Panisch fliehendes Ziel +4  &amp;#44; 1309       &amp;#125; &#124;       √úberst√ºrzter Angriff -6 &#44; ?{V√∂llige Dunkelheit/Blendung -6 √úberst√ºrzter Angriff -6 +/- ... &amp;#124;        Das war alles               &amp;#44; 221 &amp;#124;        Angriff von oben (70cm+) +2 &amp;#44; 442 &amp;#124;        Liegendes Ziel (ich stehe) +4 &amp;#44; 663 &amp;#124;        Wehrloses Ziel +4           &amp;#44; 1105 &amp;#124;        Panisch fliehendes Ziel +4  &amp;#44; 1547       &amp;#125;    &#125; }"
!mmm   if semiAutoModCode % 17 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers - 6
!mmm     set modifierLog = modifierLog & " {{V√∂llige Dunkelheit / geblendet=-6}} "
!mmm     set modifierTooltip = modifierTooltip & " V√∂llige&nbsp;Dunkelheit/geblendet&nbsp;&nbsp;&nbsp;-6 "
!mmm   end if
!mmm   if semiAutoModCode % 13 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers - 6
!mmm     set modifierLog = modifierLog & " {{√úberst√ºrzter Angriff=-6}} "
!mmm     set modifierTooltip = modifierTooltip & " √úberst√ºrzter&nbsp;Angriff&nbsp;&nbsp;&nbsp;-6 "
!mmm   end if
!mmm   if semiAutoModCode % 11 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers - 4
!mmm     set modifierLog = modifierLog & " {{Spontaner Angriff=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Spontaner&nbsp;Angriff&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!mmm   if semiAutoModCode % 7 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{Ziel flieht panisch=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Ziel&nbsp;flieht&nbsp;panisch&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!mmm   if semiAutoModCode % 5 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{Ziel wehrlos=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Ziel&nbsp;wehrlos&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!mmm   if semiAutoModCode % 3 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{Ziel liegt=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Ziel&nbsp;liegt&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!mmm   if semiAutoModCode % 2 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 2
!mmm     set modifierLog = modifierLog & " {{Angriff von oben=+2}} "
!mmm     set modifierTooltip = modifierTooltip & " Angriff&nbsp;von&nbsp;oben&nbsp;&nbsp;&nbsp;+2 "
!mmm   end if
!rem   // Ask for individual modifiers and sum up the various modifiers
!mmm   set manualModifiers = "?{Weitere spezielle Angriffsmodifikatoren|0}"
!mmm   if manualModifiers != 0
!mmm     if manualModifiers > 0
!mmm       set manualModifiers = "+" & (manualModifiers * 1)
!mmm     end if
!mmm     set modifierLog = modifierLog & " {{Benutzereingabe=" & manualModifiers & "}} "
!mmm     set modifierTooltip = modifierTooltip & " Benutzereingabe&nbsp;&nbsp;&nbsp;" & manualModifiers & " "
!mmm   end if
!mmm   set modifiers = modifiers + autoModifiers + semiAutoModifiers + manualModifiers
!rem
!rem   // Run & format rolls
!rem
!mmm   if cGMSilentMode
!mmm     set cAttackRoll = roll("1d20")
!mmm   end if
!mmm   set attackResult = cAttackRoll + effAttack + modifiers
!mmm   if modifiers == 0
!mmm     set modifiers = "+/-0"
!mmm   else if modifiers > 0
!mmm     set modifiers = "+" & (modifiers * 1)
!mmm   end if
!mmm   if modifierLog eq ""
!mmm     set modifierLog = "{{Boni/Mali=keine}}"
!mmm     set modifierTooltip = " Boni/Mali&nbsp;&nbsp;&nbsp;keine "
!mmm   end if
!mmm   set modifiers = highlight(modifiers, "info", modifierTooltip)
!mmm   set attackResultLog = attackResult & " = (1d20 = " & cAttackRoll & ") + (EW = " & effAttack & ") + (" & modifiers & ")"
!mmm   if iscritical(cAttackRoll)
!mmm     set attackResult = highlight(attackResult, "good", attackResultLog)
!mmm     set criticalAttack = true
!mmm   else if isfumble(cAttackRoll)
!mmm     set attackResult = highlight(attackResult, "bad", attackResultLog)
!mmm     set criticalAttack = false
!mmm   else 
!mmm     set attackResult = highlight(attackResult, "normal", attackResultLog)
!mmm     set criticalAttack = false
!mmm   end if
!rem
!rem   // Damage roll (cannot be lower than 0)
!mmm   set damageRoll = roll(weaponDamageExpr)
!mmm   set damageResult = damageRoll + weaponDamageBonus
!mmm   if damageResult < 0
!mmm     set damageResult = highlight(0, "bad")
!mmm   end if
!mmm   set damageResultLog = "(" & weaponDamageExpr & " = " & damageRoll & ") " & weaponDamageBonus
!mmm   if magicExtraDamageActive and cMagicExtraDamage ne ""
!mmm     set magicExtraDamageRoll = roll(cMagicExtraDamage)
!mmm     if magicExtraDamageRoll < 0
!mmm       set magicExtraDamageRoll = highlight(0, "bad")
!mmm     end if
!mmm     set damageResult = highlight(damageResult + magicExtraDamageRoll, "normal")
!mmm     set damageResultLog = damageResultLog & " + (" & cMagicExtraDamage & " = " & magicExtraDamageRoll & ")"
!mmm   end if 
!mmm   set damageResultLog = damageResult & " = " & damageResultLog
!mmm   set damageResult = highlight(damageResult, default, damageResultLog)
!mmm
!rem   // Save key attack data for defense script
!rem
!mmm   if attackResult >= 20 and damageResult > 0
!mmm     if not m3mgdExchangeStoreAttack("melee") 
!mmm       do whisperback("Problem bei der Datenablage f√ºr das Abwehrskript; Angriffsdaten bitte manuell erg√§nzen.")
!mmm     end if
!mmm   end if
!rem
!rem   // Break down magic effects for player and GM
!rem
!mmm   if magicExtraDamageActive and attackResult >= 20 and damageResult > 0
!mmm     set modifierLog = modifierLog & " {{Zauberwaffe=**" & (damageResult - magicExtraDamageRoll) & "**&nbsp;" & emojiKnife & emojiMagic & " +&nbsp;**" & magicExtraDamageRoll & "**&nbsp;" & cMagicExtraDamageLabel & "}} "
!mmm   else if magicWeapon and attackResult >= 20 and damageResult > 0
!mmm     set modifierLog = modifierLog & " {{Zauberwaffe=**" & damageResult & "**&nbsp;" & emojiKnife & emojiMagic & " }} "
!mmm   end if
!mmm
!rem   // Pull together a sensible guess at a default way of addressing the weapon for output
!mmm   if cWeaponName eq ""
!mmm     set outputWeaponLabel = cWeaponLabel
!mmm   else
!mmm     set outputWeaponLabel = cWeaponType & " *" & cWeaponName & "*"
!mmm   end if
!mmm
!mmm
!mmm   if cVerbose
!mmm     combine chat
!rem       // Check if script is run for an NPC (whisper data to GM only)
!mmm       if cGMSilentMode
!mmm         chat: /w GM 
!mmm       end if
!mmm       if not magicExtraDamageActive
!mmm         chat [AttackOpening]:                          Mein Angriff mit $[weaponLabel]{outputWeaponLabel} ($[weaponType]{cWeaponType} "$[weaponName]{cWeaponName}") auf $[targetName]{foeName} 
!mmm       else
!mmm         chat [AttackOpeningExtraMagicDamage]:          Mein Angriff mit $[weaponLabel]{outputWeaponLabel} ($[weaponType]{cWeaponType} "$[weaponName]{cWeaponName}", magischer Zusatzschaden aktiviert) auf $[targetName]{foeName} 
!mmm       end if
!mmm       if criticalAttack
!mmm         chat [AttackIsCriticalSuccess]:                ist ein **kritischer Erfolg** ($[attackResult]{attackResult+0}).
!mmm       else if isfumble(cAttackRoll)
!mmm         chat [AttackIsCriticalFailure]:                ist ein **peinlicher Patzer**. 
!mmm       else if attackResult >= 22 and damageResult > 3
!mmm         chat [AttackSucceedsWell]:                     scheint ein ordentlicher Treffer zu werden ($[attackResult]{attackResult+0}).
!mmm       else if attackResult >= 20 
!mmm         chat [AttackSucceedsBarely]:                   trifft gerade so ($[attackResult]{attackResult+0}).
!mmm       else if attackResult >= 18
!mmm         chat [AttackFailsBarely]:                      verfehlt knapp ($[attackResult]{attackResult+0}). 
!mmm       else
!mmm         chat [AttackFailsClearly]:                     verfehlt ($[attackResult]{attackResult+0}). 
!mmm       end if
!mmm       if attackResult >= 20
!mmm         if damageResult > 10
!mmm           set damageDescription = "ausgesprochen schwerer, unter Umst√§nden lebensgef√§hrlicher"
!mmm         else if damageResult > 3
!mmm           set damageDescription = "schmerzhafter"
!mmm         else if damageResult > 0
!mmm           set damageDescription = "leichter"
!mmm         else 
!mmm           set damageDescription = "h√∂chstens kosmetischer"
!mmm         end if
!mmm         if damageRoll >= 8 and not isdenied(foeEndurance) and foeEndurance >= 8
!mmm           set damageDescription = damageDescription & ", verheerender"
!mmm         end if
!mmm         if not magicExtraDamageActive
!mmm           chat [AttackSuccessClosing]:                 $[targetName]{foeName} erwischt ein $[damageDescription]{damageDescription} Treffer, wenn die Abwehr nicht gelingt.
!mmm         else
!mmm           chat [AttackSuccessClosingExtraMagicDamage]: $[targetName]{foeName} erwischt ein $[damageDescription]{damageDescription} Treffer, wenn die Abwehr nicht gelingt.
!mmm         end if
!mmm       else
!mmm         chat [AttackFailureClosing]:                   $[targetName]{foeName} war zu keinem Zeitpunkt in Gefahr.
!mmm       end if
!mmm     end combine
!mmm   end if
!rem
!rem   // Add data table for easy reference
!mmm   combine chat
!rem     // Check if script is run for an NPC (whisper data to GM only)
!mmm     if cGMSilentMode
!mmm       chat: /w GM 
!mmm     end if
!mmm     chat:      ${"&"}{template:default} {{name=${cOwnID.name}: Nahkampfangriff mit $[weaponLabel]{outputWeaponLabel}}} 
!mmm     chat:      {{Ziel=**$[targetName]{foeName}** ${m3mgdShapeMoji(foeID, cHealthAttr, cEnduranceAttr)}}}
!mmm     if attackResult >= 20
!mmm       chat:    {{Erfolg=$[attackResult]{attackResult} (**Wurf**&nbsp;$[attackRoll]{cAttackRoll})}}
!mmm     else 
!mmm       chat:    {{Fehlschlag=$[attackResult]{attackResult} (**Wurf**&nbsp;$[attackRoll]{cAttackRoll})}}
!mmm     end if
!mmm     chat:      {{Boni/Mali=**$[attackModifiers]{modifiers}**}}
!mmm     if attackResult >= 20
!mmm       set damageType = emojiKnife
!mmm       if magicWeapon and damageResult > 0
!mmm         set damageType = damageType & emojiMagic
!mmm       end if 
!mmm       chat:    {{Schaden=$[damageResult]{damageResult} **LP/AP** ${damageType}}}
!mmm     end if
!mmm   end combine
!mmm   chat: /w GM ${"&"}{template:default} ${modifierLog}
!mmm   if not cGMSilentMode
!mmm     chat: /w "${cOwnID.name}" ${"&"}{template:default} ${modifierLog}
!mmm   end if
!mmm   if iscritical(cAttackRoll) and foeEndurance > 0
!mmm     chat: /w "${cOwnID.name}"  ***Nicht vergessen: PP einstecken!*** [x](https://media.giphy.com/media/hKafco7mFwBioBxqFT/giphy.gif)
!mmm     chat: /w GM            ***Nicht vergessen: PP einstecken!*** [x](https://media.giphy.com/media/hKafco7mFwBioBxqFT/giphy.gif)
!mmm   else if iscritical(cAttackRoll)
!mmm     chat: /w "${cOwnID.name}" Leider kein PP, da der Gegner ersch√∂pft ist (AP <= 0).
!mmm   end if
!mmm   if attackResult >= 20
!mmm     do m3mgdSendDefenseChatButtons(foeID)
!mmm   end if
!mmm end script