!rem // Defends against Midgard's Frostball spell, as defined in Arkanum:86
!rem //
!mmm script
!mmm   set scriptVersion = "defendFrostball 1.3.2 (2022-01-14)"
!mmm
!mmm   set customizable cCheckVersion = false
!mmm   if cCheckVersion
!mmm     do whisperback("Midgard script " & scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!mmm   set CURRENT = scriptVersion
%{MacroSheetLibrary|libMidgardGlobal}
%{MacroSheetLibrary|libMidgardFunctions}
%{MacroSheetLibrary|libDefenseDataTable}
!mmm
!mmm   set customizable cVerbose = true
!mmm   set customizable cGMSilentMode = false
!mmm   if selected and sender.token_id ne selected
!mmm     set customizable cOwnID = selected.token_id
!mmm   else 
!mmm     set customizable cOwnID = sender.token_id
!mmm   end if
!mmm   set customizable cHealthAttr = "bar3"
!mmm   set customizable cEnduranceAttr = "bar2"
!mmm
!mmm   set endurance = cOwnID.(cEnduranceAttr)
!mmm   set health = cOwnID.(cHealthAttr)
!mmm   set maxEndurance = cOwnID.(cEnduranceAttr).max
!mmm   set maxHealth = cOwnID.(cHealthAttr).max
!mmm
!mmm   set attackType = m3mgdExchange.(m3mgdAttrAttackType)
!mmm   if not (attackType eq "magic" and m3mgdExchange.(m3mgdAttrAttackType).max eq "Frostball")
!mmm     do whisperback("Wrong attack/spell data found: '" & m3mgdExchange.(m3mgdAttrAttackType) & "' / '" & m3mgdExchange.(m3mgdAttrAttackType).max & "'")
!mmm     exit script
!mmm   end if
!mmm   
!mmm   set attackResult = m3mgdExchange.(m3mgdAttrAttackResult)
!mmm   if isdenied(attackResult) or isunknown(attackResult)
!mmm     do whisperback("Access to attack result value failed: " & getreason(attackResult))
!mmm     exit script
!mmm   end if
!mmm   set criticalAttack = (m3mgdExchange.(m3mgdAttrAttackResult).max eq "true")
!mmm   if criticalAttack == true
!mmm     set attackResult = highlight(attackResult, "good")
!mmm   else 
!mmm     set attackResult = highlight(attackResult, "normal")
!mmm   end if 
!mmm
!mmm   set defenseModifier = 0
!mmm   if endurance <= 0
!mmm     set defenseModifier = -4
!mmm   end if
!mmm
!mmm   set defenseResult = roll("1d20+" & (cOwnID.Abwehr + defenseModifier))
!mmm   
!mmm   set effEnduranceLoss = 0
!mmm   set effHealthLoss = 0
!mmm
!mmm   if iscritical(defenseResult) or (criticalAttack == false and defenseResult >= attackResult)
!mmm     
!rem     // Defense successful
!mmm     set defenseSuccess = true
!mmm
!rem     // Calculate endurance damage (only for max endurance > 0; armor, parry weapons or shields do not matter)
!mmm     if not cOwnID.(cEnduranceAttr).max == 0
!mmm       set effEnduranceLoss = 1
!mmm     end if
!mmm     
!mmm   else 
!mmm     
!rem     // Defense failed
!mmm     set defenseSuccess = false
!mmm
!rem     // AP loss: 1d6, if not an undead creature with unlimited endurance
!mmm     if not cOwnID.(cEnduranceAttr).max == 0
!mmm       set effEnduranceLoss = roll("1d6")
!mmm     end if
!mmm
!rem     // Health loss: 2, regardless of armor but only for non-ghosts (max health > 0)
!mmm     if not cOwnID.(cHealthAttr).max == 0
!mmm       set effHealthLoss = 2
!mmm     end if
!mmm
!mmm   end if
!mmm
!rem   // Process loss of endurance, if any, and applicable consequences
!mmm   set newEndurance = m3mgdModifyEndurance(-effEnduranceLoss, cOwnID, cEnduranceAttr)
!mmm   set effEnduranceLoss = highlight(effEnduranceLoss, "info")
!mmm
!rem   // Process loss of health and applicable consequences 
!mmm   set newHealth = m3mgdModifyHealth(-effHealthLoss, cOwnID, cHealthAttr)
!mmm   if effHealthLoss > 0 and newHealth < 0
!mmm     set timeToDie = roll("1d6-" & abs(newHealth)) 
!mmm   end if
!mmm   set effHealthLoss = highlight(effHealthLoss, "info")
!mmm
!mmm   set sender = cOwnID
!mmm   
!rem   // Verbose output
!mmm   if cVerbose
!mmm     combine chat
!mmm       if defenseSuccess
!mmm         chat:      Schlangengleich winde ich mich und weiche dem fiesen Frostball aus.
!mmm         if newHealth < 0
!mmm           chat:    ***Ich bin aber immer noch lebensgefährlich verletzt, mein Countdown läuft weiter.*** [x](https://media.giphy.com/media/OY9XK7PbFqkNO/giphy.gif) ***Ich. brauchte. jetzt!!! Hilfe!!!!!***
!mmm         end if
!mmm       else
!mmm         if criticalAttack == true
!mmm           chat:    Ich winde mich, strecke mich, versuche mich zu wehren, aber es reicht einfach nicht: mich erwischt ein schwerer **kritischer Treffer**.
!mmm         else 
!mmm           chat:    Ich winde mich, strecke mich, versuche mich zu wehren, aber es reicht einfach nicht: mich erwischt ein schwerer Treffer.
!mmm         end if
!mmm         if newHealth < 0 and timeToDie < 0
!mmm           chat:    War schön mit euch allen. Bis dann, und danke für den Fisch! $[ownName]{cOwnID.name}, out. [x](https://media.giphy.com/media/96quMvXlWpwRO/giphy.gif)
!mmm         else if newHealth < 0 and timeToDie >= 0
!mmm           chat:    ***Ich bin lebensgefährlich verletzt.*** Ich habe noch **$[timeToDie]{timeToDie+0} Minuten** zu leben. [x](https://media.giphy.com/media/OY9XK7PbFqkNO/giphy.gif)
!mmm           chat:    ***Ungefähr genau jetzt könnte ich eine heilende Hand gebrauchen...***
!mmm         end if
!mmm       end if 
!mmm     end combine
!mmm   end if
!rem
!rem   // Output data table using data table execution code from library
!mmm   do m3mgdDefenseDataTable()
!mmm
!mmm end script