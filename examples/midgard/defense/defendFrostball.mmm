!rem // Defends against Midgard's Frostball spell, as defined in Arkanum:86
!rem //
!mmm script
!mmm   set scriptVersion = "defendFrostball 1.6.0-WIP2 (2025-04-16)"
!mmm
!mmm   set customizable cCheckVersion = false
!mmm   if cCheckVersion
!mmm     do whisperback(scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!mmm   if version < 1.26 or not m3mgdExchange
!mmm     do whisperback("MMM version too low or game-global m3mgdExchange variables missing.")
!mmm     exit script
!mmm   end if
!mmm
!mmm   set customizable cVerbose = true
!mmm   set customizable cNoDefense = false
!mmm   set customizable cGMSilentMode = false
!mmm   if selected and sender.token_id ne selected
!mmm     set customizable cOwnID = selected.token_id
!mmm   else 
!mmm     set customizable cOwnID = sender.token_id
!mmm   end if
!mmm   set customizable cHealthAttr = "bar3"
!mmm   set customizable cEnduranceAttr = "bar2"
!mmm   set customizable searchByOrigin = default
!mmm   set customizable searchByAttackResult = default
!mmm   set customizable searchByDamageResult = default
!mmm
!mmm   set endurance = cOwnID.(cEnduranceAttr)
!mmm   set health = cOwnID.(cHealthAttr)
!mmm   set maxEndurance = cOwnID.(cEnduranceAttr).max
!mmm   set maxHealth = cOwnID.(cHealthAttr).max
!mmm
!mmm   set attackData = m3mgdRetrieveAttack(cOwnID)
!mmm   if not (attackData and attackData.attackType eq "magic" and attackData.magicSpell eq "Frostball")
!mmm     set filter = { action: "combatAttack", attackType: "magic", magicSpell: "Frostball" }
!mmm     if not isdefault(searchByOrigin)
!mmm       set filter = { filter, origin: searchByOrigin }
!mmm     end if
!mmm     if not isdefault(searchByAttackResult)
!mmm       set filter = { filter, attackResult: searchByAttackResult }
!mmm     end if
!mmm     if not isdefault(searchByDamageResult)
!mmm       set filter = { filter, damageResult: searchByDamageResult }
!mmm     end if
!mmm     set attackData = m3mgdExchangeGetData(m3mgdExchangeRegistries.combatData, filter)
!mmm     if not attackData or count(attackData) > 1
!mmm       do whisperback("Wrong attack/spell data found.")
!mmm       do whisperback(m3mgdExchangeAssignAttack(m3mgdExchangeRegistries.combatData, { action: "combatAttack", attackType: "magic", magicSpell: "Frostball" }, cOwnID))
!mmm       exit script
!mmm     end if
!mmm     debug chat: Identified a single dataset that fits: ${attackData}
!mmm   end if
!mmm   
!mmm   set attackResult = attackData.attackResult
!mmm   if attackData.criticalAttack == true
!mmm     set attackResult = highlight(attackResult, "good")
!mmm   else 
!mmm     set attackResult = highlight(attackResult, "normal")
!mmm   end if 
!mmm   set enduranceDamage = attackData.damageResult
!mmm
!mmm   set defenseModifier = 0
!mmm   if endurance <= 0
!mmm     set defenseModifier = -4
!mmm   end if
!mmm
!rem   // Dazzle players with special effect
!mmm   do spawnfx("beam-frost", attackData.origin.left, attackData.origin.top, cOwnID.left, cOwnID.top)
!mmm
!mmm   set defenseResult = roll("1d20+" & (cOwnID.Abwehr + defenseModifier))
!mmm   
!mmm   set effEnduranceLoss = 0
!mmm   set effHealthLoss = 0
!mmm
!mmm   if iscritical(defenseResult) or (attackData.criticalAttack == false and defenseResult >= attackResult)
!mmm     
!rem     // Defense successful
!mmm     set defenseSuccess = true
!mmm
!rem     // Calculate endurance damage (only for max endurance > 0; armor, parry weapons or shields do not matter)
!mmm     if not cOwnID.(cEnduranceAttr).max == 0
!mmm       set effEnduranceLoss = 1
!mmm     end if
!mmm     
!mmm   else 
!mmm     
!rem     // Defense failed
!mmm     set defenseSuccess = false
!mmm
!rem     // AP loss: 1d6, if not an undead creature with unlimited endurance
!mmm     if not cOwnID.(cEnduranceAttr).max == 0
!mmm       set effEnduranceLoss = enduranceDamage
!mmm     end if
!mmm
!rem     // Health loss: 2, regardless of armor but only for non-ghosts (max health > 0)
!mmm     if not cOwnID.(cHealthAttr).max == 0
!mmm       set effHealthLoss = 2
!mmm     end if
!mmm
!mmm   end if
!mmm
!rem   // Process loss of endurance, if any, and applicable consequences
!mmm   set newEndurance = m3mgdModifyEndurance(-effEnduranceLoss, cOwnID, cEnduranceAttr)
!mmm   set effEnduranceLoss = highlight(effEnduranceLoss, "info")
!mmm
!rem   // Process loss of health and applicable consequences 
!mmm   set newHealth = m3mgdModifyHealth(-effHealthLoss, cOwnID, cHealthAttr)
!mmm   if effHealthLoss > 0 and newHealth < 0
!mmm     set timeToDie = roll("1d6-" & abs(newHealth)) 
!mmm   end if
!mmm   set effHealthLoss = highlight(effHealthLoss, "info")
!mmm
!rem   // Process experience gain for attacker if script is run on an NPC, in which case player is GM with control over attacker sheet
!mmm   if isChar(attackData.origin) and not cNoDefense and ((cOwnID.(cEnduranceAttr).max == 0 and not defenseSuccess) or (cOwnID.(cEnduranceAttr).max != 0 and endurance > 0 and effEnduranceLoss > 0))
!mmm     set xpGain = m3mgdProcessAttackXP(attackData.attackType, effEnduranceLoss, attackData.origin, cOwnID)
!mmm     set modifierLog = modifierLog & " {{ Erfahrung=" & attackData.origin.name & ": +" & xpGain & " EP}} "
!mmm   else if not cNoDefense and cOwnID.(cEnduranceAttr).max == 0 and defenseSuccess
!mmm     set modifierLog = modifierLog & " {{ Erfahrung=" & attackData.origin.name & ": keine (∞ AP, abgewehrt) }} "
!mmm   else if cNoDefense or (cOwnID.(cEnduranceAttr).max != 0 and endurance <= 0)
!mmm       set modifierLog = modifierLog & " {{ Erfahrung=" & attackData.origin.name & ": keine (Ziel erschöpft/wehrlos) }} "
!mmm   else if cOwnID.(cEnduranceAttr).max != 0 and effEnduranceLoss <= 0
!mmm       set modifierLog = modifierLog & " {{ Erfahrung=" & attackData.origin.name & ": keine (kein AP-Verlust) }} "
!mmm   end if
!mmm
!mmm   if cOwnID.token_name ne sender
!mmm     set sender = cOwnID.token_name
!mmm   end if
!mmm   
!rem   // Verbose output
!mmm   if cVerbose
!mmm     combine chat
!mmm       if defenseSuccess
!mmm         chat:      Schlangengleich winde ich mich und weiche dem fiesen Frostball aus.
!mmm         if newHealth < 0
!mmm           chat:    ***Ich bin aber immer noch lebensgefährlich verletzt, mein Countdown läuft weiter.*** [x](https://media.giphy.com/media/OY9XK7PbFqkNO/giphy.gif) ***Ich. brauchte. jetzt!!! Hilfe!!!!!***
!mmm         end if
!mmm       else
!mmm         if criticalAttack == true
!mmm           chat:    Ich winde mich, strecke mich, versuche mich zu wehren, aber es reicht einfach nicht: mich erwischt ein schwerer **kritischer Treffer**.
!mmm         else 
!mmm           chat:    Ich winde mich, strecke mich, versuche mich zu wehren, aber es reicht einfach nicht: mich erwischt ein schwerer Treffer.
!mmm         end if
!mmm         if newHealth < 0 and timeToDie < 0
!mmm           chat:    War schön mit euch allen. Bis dann, und danke für den Fisch! $[ownName]{cOwnID.name}, out. [x](https://media.giphy.com/media/96quMvXlWpwRO/giphy.gif)
!mmm         else if newHealth < 0 and timeToDie >= 0
!mmm           chat:    ***Ich bin lebensgefährlich verletzt.*** Ich habe noch **$[timeToDie]{timeToDie+0} Minuten** zu leben. [x](https://media.giphy.com/media/OY9XK7PbFqkNO/giphy.gif)
!mmm           chat:    ***Ungefähr genau jetzt könnte ich eine heilende Hand gebrauchen...***
!mmm         end if
!mmm       end if 
!mmm     end combine
!mmm   end if
!rem
!rem   // Output data table using data table execution code from library
!mmm   do m3mgdDefenseDataTable(attackData)
!mmm   
!rem   // Output modifier log and PP reminder
!mmm   chat: /w GM ${"&"}{template:default} ${modifierLog}
!mmm   if not cGMSilentMode
!mmm     chat: /w "${cOwnID.character_name}" ${"&"}{template:default} ${modifierLog}
!mmm   end if
!mmm   
!rem   // Special effects
!mmm   if effHealthLoss > 0 or effEnduranceLoss > 0
!mmm     do m3mgdInjuryFX(cOwnID, effHealthLoss / maxHealth, effEnduranceLoss / cOwnID.(cEnduranceAttr).max)
!mmm   end if
!mmm   
!mmm end script