!rem // Midgard Combat Defense Script
!rem // 
!mmm script
!mmm   set scriptVersion = "defense 1.12.0 (2022-01-16)"
!mmm
!mmm   set customizable cCheckVersion = false
!mmm   if cCheckVersion
!mmm     do whisperback("Midgard Combat Defense Script " & scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!rem   // MMM compatibility check: die if MMM version too low
!mmm   if version < 1.24
!mmm     do whisperback("Abbruch: MMM-Version 1.24.0 oder höher erforderlich.")
!mmm     exit script
!mmm   end if
!mmm
!rem   // Initialize globals (mainly data exchange)
%{MacroSheetLibrary|libMidgardGlobal}
!rem
!rem   // Config: set defaults for customizable variables
!rem   //
!rem   //  cVerbose             Toggle narrative output: true for on, false for off 
!rem   //  cWeaponLabel         The label of the weapon, must be identical to your combat sheet
!rem   //
!mmm   set customizable cVerbose = true
!mmm   set customizable cWeaponLabel = ""
!rem
!rem   //  cOwnID               Override if the script should work from a sender not identical with the character
!rem   //  cHealthAttr          Override if character's/token's health is stored differently than usual
!rem   //  cEnduranceAttr       Override if character's/token's endurance is stored differently than usual
!rem   //  cGMSilentMode        true/false: override if GM operates this script and wants to keep it silent
!rem   //  cDefenseRoll         Override by player to see beautifully animated 3D dice
!rem   //
!mmm   if selected and sender.token_id ne selected
!mmm     set customizable cOwnID = selected.token_id
!mmm     set customizable cHealthAttr = "bar3"
!mmm     set customizable cEnduranceAttr = "bar2"
!mmm   else if m3mgdExchange.(m3mgdAttrAttackTargetID) ne ""
!mmm     set customizable cOwnID = m3mgdExchange.(m3mgdAttrAttackTargetID)
!mmm     set customizable cHealthAttr = "bar3"
!mmm     set customizable cEnduranceAttr = "bar2"
!mmm   else 
!mmm     set customizable cOwnID = sender.token_id
!mmm     set customizable cHealthAttr = "LP"
!mmm     set customizable cEnduranceAttr = "AP"
!mmm   end if
!mmm   set customizable cGMSilentMode = false
!mmm   set customizable cDefenseRoll = roll("1d20")
!mmm
!rem
!rem   // Initialize & collect data
!rem
!rem   // Refetch character_id to ensure access in case of erroneous override in customize block
!mmm   set cOwnID = cOwnID.token_id
!mmm   if isdenied(cOwnID) or isunknown(cOwnID)
!mmm     do whisperback("Abbruch: Keine Zuordnung zu einem zugreifbaren Token möglich - " & cOwnID) 
!mmm     exit script
!mmm   end if
!rem
!rem   // Override chat sender for characters unconnected to the player (usually NPCs)
!mmm   if cOwnID ne sender
!mmm     set sender = cOwnID
!mmm   end if
!rem
!rem   // Validate attack data and request manual entry if anything is amiss
!mmm   if not m3mgdValidateAttackData()
!mmm     do whisperback("**Abwehrskript: Daten fehlen oder widersprüchlich** [**Angriffsdaten eingeben**](~MacroSheet|defenseDataEntry)")
!mmm     exit script
!mmm   end if
!mmm   if not m3mgdExchange.(m3mgdAttrAttackTargetID) eq cOwnID
!mmm     do whisperback("**Abwehrskript:** " & m3mgdExchange.(m3mgdAttrAttackTargetID).name & " wurde zuletzt angegriffen, nicht ich. [**Angriffsdaten eingeben**](~MacroSheet|defenseDataEntry)")
!mmm     exit script
!mmm   end if
!mmm   set attackType = m3mgdExchange.(m3mgdAttrAttackType)
!mmm   set attackResult = m3mgdExchange.(m3mgdAttrAttackResult)
!mmm   set criticalAttack = (m3mgdExchange.(m3mgdAttrAttackResult).max eq "true")
!mmm   set attackDamage = m3mgdExchange.(m3mgdAttrAttackDamage)
!mmm   set attackDamageRoll = m3mgdExchange.(m3mgdAttrAttackDamageRoll)
!mmm   set attackWeaponType = m3mgdExchange.(m3mgdAttrAttackWeaponType)
!rem   // Special case: magic weapons or weapon spells with additional effects scripts whose name is stored in .max
!mmm   if m3mgdExchange.(m3mgdAttrAttackWeaponType).max ne ""
!mmm     set attackWeaponSpecialEffectScript = m3mgdExchange.(m3mgdAttrAttackWeaponType).max
!mmm   end if
!mmm
!rem   // Initialize defender 
!mmm   set effDefense = cOwnID.Abwehr + cOwnID.BonusAbwehr
!mmm   set armorLabel = cOwnID.(findattr(cOwnID, "Ruestung", "RsGetragen", true, "Rüstung"))
!mmm   set armorProtection = cOwnID.(findattr(cOwnID, "Ruestung", "RsGetragen", true, "RüsLP"))
!mmm   if isunknown(armorLabel) or isunknown(armorProtection)
!rem     // For NPC tokens where the search for "RsGetragen==true" doesn't work, just take first armor in sheet
!mmm     set armorLabel = cOwnID.(findattr(cOwnID, "Ruestung", "Rüstung"))
!mmm     set armorProtection = cOwnID.(findattr(cOwnID, "Ruestung", "RüsLP"))
!mmm   end if
!mmm   set endurance = cOwnID.(cEnduranceAttr)
!mmm   set health = cOwnID.(cHealthAttr)
!mmm   set maxHealth = cOwnID.(cHealthAttr).max
!rem
!rem   // Initialize modifier log to collect rule effects as applied by the script for user output
!mmm   set modifierLog = ""
!mmm   set modifierTooltip = ""
!rem
!rem   // Validate defense weapon and request manual entry if selection is missing
!mmm   if cWeaponLabel eq ""
!mmm     set defenseWeaponList = m3mgdListDefenseWeapons(cOwnID)
!mmm     if not defenseWeaponList
!mmm       do whisperback("Fehler im Charakterbogen: " & cOwnID.name & " hat keine Abwehrwaffen bzw. -fähigkeiten.")
!mmm       exit script
!mmm     else if count(defenseWeaponList) == 1
!mmm       set cWeaponLabel = cOwnID.(defenseWeaponList)
!mmm     else if count(defenseWeaponList) > 1 and (defenseWeaponList where cOwnID.(...) eq "Abwehr ohne Schild")
!mmm       set modifierLog = modifierLog & " {{Default=Abwehr ohne Schild}}"
!mmm       set cWeaponLabel = "Abwehr ohne Schild"
!mmm     else 
!mmm       do whisperback("Fehler: " & cOwnID.name & " hat mehrere Abwehrfertigkeiten, aber nicht 'Abwehr ohne Schild'.")
!mmm       exit script
!mmm     end if
!mmm   end if
!rem
!rem   // If user wants to use a parry weapon/shield, validate
!mmm   set parryWeaponUsable = false
!mmm   set parryWeapon = (cOwnID.(findattr(cOwnID, "Abwehr", "AbwWaffe", cWeaponLabel, "FWAbwWaffe")) > 0)
!mmm   if m3mgdParryLargeShieldTypes where ... eq cWeaponLabel 
!mmm     set parryWeaponUsable = true
!mmm   else if (m3mgdParrySmallShieldTypes where ... eq cWeaponLabel) and (m3mgdParrySmallShieldEffectiveAgainst where ... eq attackWeaponType)
!mmm     set parryWeaponUsable = true
!mmm   else if (m3mgdParryStandardTypes where ... eq cWeaponLabel) and (m3mgdParryStandardEffectiveAgainst where ... eq attackWeaponType)
!mmm     set parryWeaponUsable = true
!mmm   end if
!rem   // If shield/parry weapon accessible and endurance > 0, check applicability and calculate defense benefits
!mmm   if (cOwnID.(cEnduranceAttr).max > 0 and endurance > 0 and parryWeapon and parryWeaponUsable) or (cOwnID.(cEnduranceAttr).max == 0 and parryWeapon and parryWeaponUsable)
!mmm     set effParryWeaponSkill = cOwnID.(findattr(cOwnID, "Abwehr", "AbwWaffe", cWeaponLabel, "FWAbwWaffe")) + cOwnID.(findattr(cOwnID, "Abwehr", "AbwWaffe", cWeaponLabel, "WaffeAbwB"))
!rem     // Endurance protection: reverse sign for more logical handling (modifier is stored as negative in character sheet)
!mmm     set parryWeaponEnduranceProtection = -1 * cOwnID.(findattr(cOwnID, "Abwehr", "AbwWaffe", cWeaponLabel, "AbwWAP"))
!mmm     set effDefense = effDefense + effParryWeaponSkill
!mmm     set modifierLog = modifierLog & " {{" & cWeaponLabel & "= WW inkl. +" & effParryWeaponSkill & ", AP-Verlust (leichte Treffer) -" & parryWeaponEnduranceProtection & "}}"
!mmm   else if parryWeapon and not parryWeaponUsable
!mmm     set modifierLog = modifierLog & " {{" & cWeaponLabel & "= nutzlos gegen " & attackWeaponType & "}}"
!mmm   else if parryWeapon
!mmm     set modifierLog = modifierLog & " {{" & cWeaponLabel & "= nutzlos: bin erschöpft (AP=0)}}"
!mmm   end if
!rem
!rem   // Check for armor piercing projectiles
!mmm   set armorPiercing = ((m3mgdArmorPiercingWeaponTypes where ... eq attackWeaponType) eq attackWeaponType)
!mmm
!rem   // Collect defense modifiers
!mmm   set modifiers = 0
!mmm   set autoModifiers = 0
!rem   // 1. Defender exhausted or magically encumbered (runeBladeEarthBond)
!mmm   if cOwnID.(cEnduranceAttr).max > 0 and endurance <= 0
!mmm     set autoModifiers = autoModifiers - 4
!mmm     set modifierLog = modifierLog & " {{Erschöpft=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Erschöpft&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!mmm   if cOwnID.(m3mgdEarthBondStatusMarker) eq "shown"
!mmm     set autoModifiers = autoModifiers - 2
!mmm     set modifierLog = modifierLog & " {{Erdfessel=-2}} "
!mmm     set modifierTooltip = modifierTooltip & " Erdfessel&nbsp;&nbsp;&nbsp;-2 "
!mmm   end if
!rem   // 2. Dropdown prompt: Focused defense, surprise
!mmm   set semiManualModifiers = "?{Standard-Abwehrmodifikatoren| Normale Abwehr +/-0,0 | Konzentrierte Abwehr +4,+4 | Ich greife überstürzt an -2,-2 | Ich bin überrascht -4,-4 }"
!mmm   if semiManualModifiers == 4
!mmm     set modifierLog = modifierLog & " {{Konzentrierte Abwehr=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Konzentrierte&nbsp;Abwehr&nbsp;&nbsp;&nbsp;+4 "
!mmm   else if semiManualModifiers == -2
!mmm     set modifierLog = modifierLog & " {{Überstürzter eigener Angriff=-2}} "
!mmm     set modifierTooltip = modifierTooltip & " Überstürzter&nbsp;eigener&nbsp;Angriff&nbsp;&nbsp;&nbsp;-2 "
!mmm   else if semiManualModifiers == -4
!mmm     set modifierLog = modifierLog & " {{Überraschender Angriff=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Überraschender&nbsp;Angriff&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!rem   // Add manual modifiers from user input
!mmm   set manualModifiers = "?{Weitere spezielle Abwehrmodifikatoren|0}"
!mmm   set modifiers = modifiers + autoModifiers + semiManualModifiers + manualModifiers
!mmm   if manualModifiers != 0
!mmm     if manualModifiers > 0
!mmm       set manualModifiers = "+" & (manualModifiers * 1)
!mmm     end if
!mmm     set modifierLog = modifierLog & " {{Manuelle Modifikatoren=" & manualModifiers & "}} "
!mmm     set modifierTooltip = modifierTooltip & " Manuelle&nbsp;Modifikatoren&nbsp;&nbsp;&nbsp;" & manualModifiers & " "
!mmm   end if
!mmm   if modifierLog eq ""
!mmm     set modifierLog = " {{Boni/Mali=keine}} "
!mmm     set modifierTooltip = " Boni/Mali&nbsp;&nbsp;&nbsp;keine "
!mmm   end if
!rem
!rem   // Run & format rolls
!rem
!mmm   set defenseResult = cDefenseRoll + effDefense + modifiers
!mmm   if modifiers == 0
!mmm     set modifiers = "+/-0"
!mmm   else if modifiers > 0
!mmm     set modifiers = "+" & modifiers
!mmm   end if
!mmm   set defenseResultLog = defenseResult & " = (1d20:" & cDefenseRoll & ")+(WW:" & effDefense & ")+(" & modifiers & ")"
!mmm   set modifiers = highlight(modifiers, "info")
!mmm   if iscritical(cDefenseRoll)
!mmm     set defenseResult = highlight(defenseResult, "good", defenseResultLog)
!mmm   else if isfumble(cDefenseRoll)
!mmm     set defenseResult = highlight(defenseResult, "bad", defenseResultLog)
!mmm   else
!mmm     set defenseResult = highlight(defenseResult, "normal", defenseResultLog)
!mmm   end if
!mmm
!mmm   set effEnduranceLoss = 0
!mmm   set effHealthLoss = 0
!mmm
!mmm   if (not criticalAttack and (defenseResult >= attackResult or iscritical(cDefenseRoll))) or (criticalAttack and iscritical(cDefenseRoll))
!rem     // Defense successful
!mmm     set defenseSuccess = true
!mmm
!rem     // Calculate endurance damage (only for max endurance > 0; may be reduced due to parry weapon or shield properties)
!mmm     if not cOwnID.(cEnduranceAttr).max == 0 and parryWeaponEnduranceProtection
!mmm       set effEnduranceLoss = attackDamage - parryWeaponEnduranceProtection
!mmm       if effEnduranceLoss < 0
!mmm         set effEnduranceLoss = 0
!mmm       end if
!mmm     else if not cOwnID.(cEnduranceAttr).max == 0
!mmm       set effEnduranceLoss = attackDamage
!mmm     end if
!mmm
!mmm   else 
!rem     // Defense failed
!mmm     set defenseSuccess = false
!mmm
!rem     // Defender loses full hit points worth of endurance regardless of armor, if not an undead creature with unlimited endurance
!mmm     if not cOwnID.(cEnduranceAttr).max == 0
!mmm       set effEnduranceLoss = attackDamage
!mmm     else 
!mmm       set effEnduranceLoss = 0
!mmm     end if
!mmm
!rem     // Calculate damage to health depending on armor (only for max health > 0, ghosts do not lose health)
!mmm     if not cOwnID.(cHealthAttr).max == 0 and armorProtection > 0
!mmm       if armorProtection > 3 and armorPiercing == true
!mmm         set armorProtection = 3
!mmm         set modifierLog = modifierLog & " {{" & armorLabel & "=LP-Verlust -" & armorProtection & ", reduziert wg. Geschosstyp (" & m3mgdExchange.(m3mgdAttrAttackWeaponType) & ")}} "
!mmm       else
!mmm         set modifierLog = modifierLog & " {{" & armorLabel & "=LP-Verlust -" & armorProtection & "}} "
!mmm       end if
!mmm       set effHealthLoss = attackDamage - armorProtection
!mmm       if effHealthLoss < 0
!mmm         set effHealthLoss = 0
!mmm       end if
!mmm     else if not cOwnID.(cHealthAttr).max == 0
!mmm       set effHealthLoss = attackDamage
!mmm     end if
!mmm   end if
!mmm
!rem   // Process loss of endurance, if any, and applicable consequences
!mmm   set newEndurance = m3mgdModifyEndurance(-effEnduranceLoss)
!mmm   set effEnduranceLoss = highlight(effEnduranceLoss, "info")
!mmm
!rem   // Process loss of health and applicable consequences 
!mmm   set newHealth = m3mgdModifyHealth(-effHealthLoss)
!mmm   if effHealthLoss > 0 and newHealth < 0
!mmm     set timeToDie = roll("1d6-" & abs(newHealth)) 
!mmm   end if
!mmm   set effHealthLoss = highlight(effHealthLoss, "info")
!mmm
!rem   // Process or indicate if an endurance ceiling is in place (for serious injuries, health <= 50%)
!mmm   if m3mgdAttrCeiling(cOwnID, cEnduranceAttr)
!mmm     set newEndurance = highlight(cOwnID.(cEnduranceAttr), "important", "Schwächelnd bis der Heiler kommt (max. " & round(cOwnID.(cEnduranceAttr).max/2) & "=50%)")
!mmm   end if
!rem
!rem   // Process experience gain for attacker if script is run on an NPC, in which case player is GM with control over attacker sheet
!mmm   if (cOwnID.(cEnduranceAttr).max == 0 and not defenseSuccess) or (cOwnID.(cEnduranceAttr).max != 0 and endurance > 0 and effEnduranceLoss > 0)
!mmm     set xpGain = m3mgdProcessAttackXP(m3mgdExchange.(m3mgdAttrAttackType), attackDamageRoll, m3mgdExchange.(m3mgdAttrAttackerID), cOwnID)
!mmm     set modifierLog = modifierLog & " {{ Erfahrung=" & m3mgdExchange.(m3mgdAttrAttackerID).name & ": +" & xpGain & " EP}} "
!mmm   else if cOwnID.(cEnduranceAttr).max == 0 and defenseSuccess
!mmm     set modifierLog = modifierLog & " {{ Erfahrung=" & m3mgdExchange.(m3mgdAttrAttackerID).name & ": keine (∞ AP, abgewehrt) }} "
!mmm   else if cOwnID.(cEnduranceAttr).max != 0 and endurance <= 0
!mmm       set modifierLog = modifierLog & " {{ Erfahrung=" & m3mgdExchange.(m3mgdAttrAttackerID).name & ": keine (Ziel erschöpft) }} "
!mmm   else if cOwnID.(cEnduranceAttr).max != 0 and effEnduranceLoss <= 0
!mmm       set modifierLog = modifierLog & " {{ Erfahrung=" & m3mgdExchange.(m3mgdAttrAttackerID).name & ": keine (kein AP-Verlust) }} "
!mmm   end if
!mmm
!rem   // Add button for player to call special damage effects script
!mmm   if not defenseSuccess and attackWeaponSpecialEffectScript
!mmm     set modifierLog = modifierLog & " {{ " & attackWeaponType & "=[**Sondereffekt**](~" & attackWeaponSpecialEffectScript & ") }}"
!mmm   end if
!mmm
!rem   // Verbose output
!mmm   if cVerbose
!mmm     combine chat
!rem       // Check if script is run for an NPC (whisper data to GM only)
!mmm       if cGMSilentMode
!mmm         chat: /w GM 
!mmm       end if
!mmm       if defenseSuccess
!mmm         chat       [DefenseSuccess]:           Ich winde mich, strecke mich, wehre mich, und erleide nur einen **leichten Treffer**.
!mmm         if newHealth < 0
!mmm           chat     [OngoingCriticalInjury]:    ***Ich bin immer noch lebensgefährlich verletzt, mein Countdown läuft weiter.*** [x](https://media.giphy.com/media/OY9XK7PbFqkNO/giphy.gif) ***Jetzt könnte ich eine heilende Hand gebrauchen...***
!mmm         end if
!mmm       else
!mmm         if criticalAttack
!mmm           chat     [DefenseFailureCritDamage]: Ich winde mich, strecke mich, versuche mich zu wehren, aber es reicht einfach nicht: mich erwischt ein schwerer **kritischer Treffer**.
!mmm         else 
!mmm           chat     [DefenseFailureRegDamage]:  Ich winde mich, strecke mich, versuche mich zu wehren, aber es reicht einfach nicht: mich erwischt ein schwerer Treffer.
!mmm         end if
!mmm         if effHealthLoss > 0.33 * maxHealth
!mmm           chat     [DefenseFailureHugeHit]:    AAAAAAARRG! Blut spritzt, Knochen brechen, solche Treffer halte ich nicht viele aus!
!mmm         else if effHealthLoss > .2 * maxHealth
!mmm           chat     [DefenseFailureSeriousHit]: AAAH! Das darf mir nicht oft passieren.
!mmm         else if effHealthLoss > .1 * maxHealth
!mmm           chat     [DefenseFailureModestHit]:  Aua!
!mmm         else if effHealthLoss > 0
!mmm           chat     [DefenseFailureMinorHit]:   Au. Pieks. Na warte!
!mmm         else 
!mmm           chat     [DefenseFailureNoDamage]:   Doch kein Problem für meine $[armorLabel]{armorLabel}, außer Schweiß verliere ich nichts.
!mmm         end if
!mmm         if newHealth < 0 and timeToDie < 0
!mmm           chat     [EffectsImmediateDeath]:    War schön mit euch allen. Bis dann, und danke für den Fisch! $[ownName]{cOwnID.name}, out.${"<"}br>[x](https://media.giphy.com/media/96quMvXlWpwRO/giphy.gif)
!mmm         else if newHealth < 0 and timeToDie >= 0
!mmm           chat     [EffectsCriticalInjury]:    ***Ich bin lebensgefährlich verletzt.*** Ich habe noch **$[timeToDie]{timeToDie+0} Minuten** zu leben.${"<"}br>[x](https://media.giphy.com/media/OY9XK7PbFqkNO/giphy.gif)${"<"}br>***Jetzt könnte ich eine heilende Hand gebrauchen...***
!mmm         end if
!mmm       end if 
!mmm     end combine
!mmm   end if
!rem
!rem   // Output data table using data table execution code from library
!mmm   do m3mgdDefenseDataTable()
!mmm
!rem   // Output modifier log and PP reminder
!mmm   chat: /w GM ${"&"}{template:default} ${modifierLog}
!mmm   if not cGMSilentMode
!mmm     chat: /w "${cOwnID.name}" ${"&"}{template:default} ${modifierLog}
!mmm   end if
!mmm   if parryWeapon and parryWeaponUsable and iscritical(cDefenseRoll)
!mmm     chat: /w "${cOwnID.name}" ***Nicht vergessen: PP für ${cWeaponLabel} einstecken!*** [x](https://media.giphy.com/media/hKafco7mFwBioBxqFT/giphy.gif)
!mmm     chat: /w GM               ***Nicht vergessen: PP für ${cWeaponLabel} einstecken!*** [x](https://media.giphy.com/media/hKafco7mFwBioBxqFT/giphy.gif)
!mmm   end if
!mmm end script