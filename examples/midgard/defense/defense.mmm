!rem // Midgard Combat Defense Script
!rem // 
!mmm script
!mmm   set scriptVersion = "defense 1.9.0 (2021-12-28)"
!mmm
!rem   // MMM compatibility check: die if MMM version too low
!mmm   if version < 1.24
!mmm     do whisperback("Abbruch: MMM-Version 1.24.0 oder h√∂her erforderlich.")
!mmm     exit script
!mmm   end if
!mmm
!rem // including function libraries here to avoid breaking the link between customize and script blocks 
%{MacroSheetLibrary|libMidgardFunctions}
%{MacroSheetLibrary|libDefenseDataTable}
!rem
!rem   // Config: set defaults for customizable variables
!rem   //
!rem   //  cCheckVersion        Toggle version check
!rem   //  cVerbose             Toggle narrative output: true for on, false for off 
!rem   //  cWeaponLabel         The label of the weapon, must be identical to your combat sheet
!rem   //
!mmm   set customizable cCheckVersion = false
!mmm   set customizable cVerbose = true
!mmm   set customizable cWeaponLabel = "Abwehr ohne Schild"
!rem
!rem   //  cOwnID               Override if the script should work from a sender not identical with the character
!rem   //  cHealthAttr          Override if character's/token's health is stored differently than usual
!rem   //  cEnduranceAttr       Override if character's/token's endurance is stored differently than usual
!rem   //  cGMSilentMode        0/1: override if GM operates this script and wants to keep it silent
!rem   //  cDefenseRoll         Override by player to see beautifully animated 3D dice
!rem   //
!mmm   if selected
!mmm     set customizable cOwnID = selected.token_id
!mmm     set customizable cHealthAttr = "bar3"
!mmm     set customizable cEnduranceAttr = "bar2"
!mmm   else 
!mmm     set customizable cOwnID = sender.token_id
!mmm     set customizable cHealthAttr = "LP"
!mmm     set customizable cEnduranceAttr = "AP"
!mmm   end if
!mmm   set customizable cGMSilentMode = 0
!mmm   set customizable cDefenseRoll = undef
!mmm
!mmm   if cCheckVersion
!mmm     do whisperback("Midgard Combat Defense Script " & scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!rem   // Initialize globals (mainly data exchange)
!mmm   set CURRENT = scriptVersion
%{MacroSheetLibrary|libMidgardGlobal}
!mmm
!rem   // Constants
!mmm
!mmm   set m3mgdAttrXP = "Erfahrungsschatz"
!mmm
!mmm   set parryWeaponLargeShieldTypes = "Gro√üer Schild"
!mmm   set parryWeaponSmallShieldTypes = "Kleiner Schild"
!mmm   set parryWeaponSmallShieldEffectiveAgainst = "Einhandschlagwaffe", "Einhandschwert", "Fechtwaffe", "Kettenwaffe", "Spie√üwaffe", "Stichwaffe", "Stockwaffe", "Zauberstab", "waffenlos"
!mmm   set parryWeaponStandardTypes = "Buckler", "Parierdolch", "Kampfgabeln"
!mmm   set parryWeaponStandardEffectiveAgainst = "Einhandschwert", "Fechtwaffe", "Spie√üwaffe", "Stichwaffe", "Stockwaffe", "Zauberstab", "Buckler", "Parierdolch", "Kampfgabeln"
!rem
!rem   // Initialize & collect data
!rem
!rem   // Refetch character_id to ensure access in case of erroneous override in customize block
!mmm   set cOwnID = cOwnID.token_id
!mmm   if isdenied(cOwnID) or isunknown(cOwnID)
!mmm     do whisperback("Abbruch: Keine Zuordnung zu einem zugreifbaren Token m√∂glich - " & cOwnID) 
!mmm     exit script
!mmm   end if
!rem
!rem   // GM silent mode vs. player mode
!mmm   if cGMSilentMode == 0 and not cDefenseRoll
!mmm     do whisperback("Abbruch: Spielermodus erfordert √úbergabe des W√ºrfelwurfs - " & cDefenseRoll) 
!mmm     exit script
!mmm   end if
!rem
!rem   // Override chat sender for characters unconnected to the player (usually NPCs)
!mmm   if cOwnID ne sender
!mmm     set sender = cOwnID
!mmm   end if
!rem
!rem   // Validate attack data and request manual entry if anything is amiss
!mmm   set validInputs = 0
!mmm   if m3mgdExchange.(m3mgdAttrAttackTargetID) eq cOwnID
!mmm     set validInputs = validInputs + 1
!mmm   end if
!mmm   if m3mgdExchange.(m3mgdAttrAttackType) eq "melee" or m3mgdExchange.(m3mgdAttrAttackType) eq "ranged"
!mmm     set attackType = m3mgdExchange.(m3mgdAttrAttackType)
!mmm     set validInputs = validInputs + 1
!mmm   end if
!mmm   if m3mgdExchange.(m3mgdAttrAttackResult) >= 20
!mmm     set attackResult = m3mgdExchange.(m3mgdAttrAttackResult)
!mmm     set validInputs = validInputs + 1
!mmm   end if
!mmm   if m3mgdExchange.(m3mgdAttrAttackResult).max eq "true" or m3mgdExchange.(m3mgdAttrAttackResult).max eq "false"
!mmm     set criticalAttack = (m3mgdExchange.(m3mgdAttrAttackResult).max eq "true")
!mmm     set validInputs = validInputs + 1
!mmm   end if
!mmm   if not isunknown(m3mgdExchange.(m3mgdAttrAttackDamage)) and m3mgdExchange.(m3mgdAttrAttackDamage) >= 0
!mmm     set attackDamage = m3mgdExchange.(m3mgdAttrAttackDamage)
!mmm     set validInputs = validInputs + 1
!mmm   end if
!mmm   if not isunknown(m3mgdExchange.(m3mgdAttrAttackDamageRoll)) and m3mgdExchange.(m3mgdAttrAttackDamageRoll) >= 0
!mmm     set attackDamageRoll = m3mgdExchange.(m3mgdAttrAttackDamageRoll)
!mmm     set validInputs = validInputs + 1
!mmm   end if
!mmm   if not isunknown(m3mgdExchange.(m3mgdAttrAttackWeaponType)) and not m3mgdExchange.(m3mgdAttrAttackWeaponType) eq ""
!mmm     set attackWeaponType = m3mgdExchange.(m3mgdAttrAttackWeaponType)
!mmm     set validInputs = validInputs + 1
!mmm   end if 
!mmm   
!mmm   if validInputs < 7
!mmm     do whisperback("**Abwehrskript: Daten fehlen** [**Angriffsdaten eingeben**](~defenseDataEntry)")
!mmm     exit script
!mmm   end if
!mmm
!rem   // Initialize defender 
!mmm   set effDefense = cOwnID.Abwehr + cOwnID.BonusAbwehr
!mmm   set armorLabel = cOwnID.(findattr(cOwnID, "Ruestung", "RsGetragen", true, "R√ºstung"))
!mmm   set armorProtection = cOwnID.(findattr(cOwnID, "Ruestung", "RsGetragen", true, "R√ºsLP"))
!mmm   if isunknown(armorLabel) or isunknown(armorProtection)
!rem     // For NPC tokens where the search for "RsGetragen==true" doesn't work, just take first armor in sheet
!mmm     set armorLabel = cOwnID.(findattr(cOwnID, "Ruestung", "R√ºstung"))
!mmm     set armorProtection = cOwnID.(findattr(cOwnID, "Ruestung", "R√ºsLP"))
!mmm   end if
!mmm   set endurance = cOwnID.(cEnduranceAttr)
!mmm   set health = cOwnID.(cHealthAttr)
!mmm   set maxHealth = cOwnID.(cHealthAttr).max
!rem
!rem   // Initialize modifier log to collect rule effects as applied by the script for user output
!mmm   set modifierLog = ""
!mmm   set modifierTooltip = ""
!rem
!rem   // Check for applicability of parry weapon or shield
!rem   // TODO: parry weapons are usable only if the attack is from the front or the respective side, plus further restrictions based on the kind of parry weapon (Kodex, 70).
!mmm   set parryWeaponUsable = false
!mmm   set parryWeapon = (cOwnID.(findattr(cOwnID, "Abwehr", "AbwWaffe", cWeaponLabel, "FWAbwWaffe")) > 0)
!mmm   if parryWeaponLargeShieldTypes where ... eq cWeaponLabel 
!mmm     set parryWeaponUsable = true
!mmm   else if (parryWeaponSmallShieldTypes where ... eq cWeaponLabel) and (parryWeaponSmallShieldEffectiveAgainst where ... eq attackWeaponType)
!mmm     set parryWeaponUsable = true
!mmm   else if (parryWeaponStandardTypes where ... eq cWeaponLabel) and (parryWeaponStandardEffectiveAgainst where ... eq attackWeaponType)
!mmm     set parryWeaponUsable = true
!mmm   end if
!rem   // If shield/parry weapon accessible and endurance > 0, check applicability and calculate defense benefits
!mmm   if (cOwnID.(cEnduranceAttr).max > 0 and endurance > 0 and parryWeapon and parryWeaponUsable) or (cOwnID.(cEnduranceAttr).max == 0 and parryWeapon and parryWeaponUsable)
!mmm     set effParryWeaponSkill = cOwnID.(findattr(cOwnID, "Abwehr", "AbwWaffe", cWeaponLabel, "FWAbwWaffe")) + cOwnID.(findattr(cOwnID, "Abwehr", "AbwWaffe", cWeaponLabel, "WaffeAbwB"))
!rem     // Endurance protection: reverse sign for more logical handling (modifier is stored as negative in character sheet)
!mmm     set parryWeaponEnduranceProtection = -1 * cOwnID.(findattr(cOwnID, "Abwehr", "AbwWaffe", cWeaponLabel, "AbwWAP"))
!mmm     set effDefense = effDefense + effParryWeaponSkill
!mmm     set modifierLog = modifierLog & " {{" & cWeaponLabel & "= WW +" & effParryWeaponSkill & ", AP-Verlust -" & parryWeaponEnduranceProtection & "}}"
!mmm   else if parryWeapon and not parryWeaponUsable
!mmm     set modifierLog = modifierLog & " {{" & cWeaponLabel & "= nutzlos gegen " & attackWeaponType & "}}"
!mmm   else if parryWeapon
!mmm     set modifierLog = modifierLog & " {{" & cWeaponLabel & "= nutzlos: bin ersch√∂pft (AP=0)}}"
!mmm   end if
!rem
!rem   // Check for armor piercing projectiles
!mmm   if attackWeaponType eq "Langbogen" or attackWeaponType eq "Kompositbogen" or attackWeaponType eq "schwere Armbrust" or attackWeaponType eq "Schwere Armbrust"
!mmm     set armorPiercing = 1
!mmm   else
!mmm     set armorPiercing = 0
!mmm   end if
!mmm
!rem   // Collect defense modifiers
!mmm   set modifiers = 0
!mmm   set autoModifiers = 0
!rem   // 1. Defender exhausted
!mmm   if cOwnID.(cEnduranceAttr).max > 0 and endurance <= 0
!mmm     set autoModifiers = autoModifiers - 4
!mmm     set modifierLog = modifierLog & " {{Ersch√∂pft=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Ersch√∂pft&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!rem   // 2. Dropdown prompt: Focused defense, surprise
!mmm   set semiManualModifiers = "?{Standard-Abwehrmodifikatoren| Normale Abwehr +/-0,0 | Konzentrierte Abwehr +4,+4 | Ich greife √ºberst√ºrzt an -2,-2 | Ich bin √ºberrascht -4,-4 }"
!mmm   if semiManualModifiers == 4
!mmm     set modifierLog = modifierLog & " {{Konzentrierte Abwehr=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Konzentrierte&nbsp;Abwehr&nbsp;&nbsp;&nbsp;+4 "
!mmm   else if semiManualModifiers == -2
!mmm     set modifierLog = modifierLog & " {{√úberst√ºrzter eigener Angriff=-2}} "
!mmm     set modifierTooltip = modifierTooltip & " √úberst√ºrzter&nbsp;eigener&nbsp;Angriff&nbsp;&nbsp;&nbsp;-2 "
!mmm   else if semiManualModifiers == -4
!mmm     set modifierLog = modifierLog & " {{√úberraschender Angriff=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " √úberraschender&nbsp;Angriff&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!rem   // Add manual modifiers from user input
!mmm   set manualModifiers = "?{Weitere spezielle Abwehrmodifikatoren|0}"
!mmm   set modifiers = modifiers + autoModifiers + semiManualModifiers + manualModifiers
!mmm   if manualModifiers != 0
!mmm     if manualModifiers > 0
!mmm       set manualModifiers = "+" & (manualModifiers * 1)
!mmm     end if
!mmm     set modifierLog = modifierLog & " {{Manuelle Modifikatoren=" & manualModifiers & "}} "
!mmm     set modifierTooltip = modifierTooltip & " Manuelle&nbsp;Modifikatoren&nbsp;&nbsp;&nbsp;" & manualModifiers & " "
!mmm   end if
!mmm   if modifierLog eq ""
!mmm     set modifierLog = " {{Boni/Mali=keine}} "
!mmm     set modifierTooltip = " Boni/Mali&nbsp;&nbsp;&nbsp;keine "
!mmm   end if
!rem
!rem   // Run & format rolls
!rem
!mmm   if cGMSilentMode
!mmm     set cDefenseRoll = roll("1d20")
!mmm   end if
!mmm   set defenseResult = cDefenseRoll + effDefense + modifiers
!mmm   if modifiers == 0
!mmm     set modifiers = "+/-0"
!mmm   else if modifiers > 0
!mmm     set modifiers = "+" & modifiers
!mmm   end if
!mmm   set defenseResultLog = defenseResult & " = (1d20:" & cDefenseRoll & ")+(WW:" & effDefense & ")+(" & modifiers & ")"
!mmm   set modifiers = highlight(modifiers, "info")
!mmm   set defenseResult = highlight(defenseResult, default, defenseResultLog)
!mmm
!mmm   set effEnduranceLoss = 0
!mmm   set effHealthLoss = 0
!mmm
!mmm   if (not criticalAttack and (defenseResult >= attackResult or iscritical(cDefenseRoll))) or (criticalAttack and iscritical(cDefenseRoll))
!rem     // Defense successful
!mmm     set defenseSuccess = true
!mmm
!rem     // Calculate endurance damage (only for max endurance > 0; may be reduced due to parry weapon or shield properties)
!mmm     if not cOwnID.(cEnduranceAttr).max == 0 and parryWeaponEnduranceProtection
!mmm       set effEnduranceLoss = attackDamage - parryWeaponEnduranceProtection
!mmm       if effEnduranceLoss < 0
!mmm         set effEnduranceLoss = 0
!mmm       end if
!mmm     else if not cOwnID.(cEnduranceAttr).max == 0
!mmm       set effEnduranceLoss = attackDamage
!mmm     end if
!mmm
!mmm   else 
!rem     // Defense failed
!mmm     set defenseSuccess = false
!mmm
!rem     // Defender loses full hit points worth of endurance regardless of armor, if not an undead creature with unlimited endurance
!mmm     if not cOwnID.(cEnduranceAttr).max == 0
!mmm       set effEnduranceLoss = attackDamage
!mmm     else 
!mmm       set effEnduranceLoss = 0
!mmm     end if
!mmm
!rem     // Calculate damage to health depending on armor (only for max health > 0, ghosts do not lose health)
!mmm     if not cOwnID.(cHealthAttr).max == 0 and armorProtection > 0
!mmm       if armorProtection > 3 and armorPiercing == 1
!mmm         set armorProtection = 3
!mmm         set modifierLog = modifierLog & " {{" & armorLabel & "=LP-Verlust -" & armorProtection & ", reduziert wg. Geschosstyp (" & m3mgdExchange.(m3mgdAttrAttackWeaponType) & ")}} "
!mmm       else
!mmm         set modifierLog = modifierLog & " {{" & armorLabel & "=LP-Verlust -" & armorProtection & "}} "
!mmm       end if
!mmm       set effHealthLoss = attackDamage - armorProtection
!mmm       if effHealthLoss < 0
!mmm         set effHealthLoss = 0
!mmm       end if
!mmm     else if not cOwnID.(cHealthAttr).max == 0
!mmm       set effHealthLoss = attackDamage
!mmm     end if
!mmm   end if
!mmm
!rem   // Process loss of endurance, if any, and applicable consequences
!mmm   set newEndurance = highlight(endurance - effEnduranceLoss, "normal", "bin fit!")
!mmm   if not cOwnID.(cEnduranceAttr).max == 0 and newEndurance <= 0 
!rem     // AP=0: Green token marker
!mmm     set newEndurance = highlight(0, "bad", "üü¢ ersch√∂pft")
!mmm     do setattr(cOwnID, "status_green", true)
!mmm   end if
!mmm   do setattr(cOwnID, cEnduranceAttr, newEndurance)
!mmm   set effEnduranceLoss = highlight(effEnduranceLoss, "info")
!mmm
!rem   // Process loss of health and applicable consequences 
!mmm   set newHealth = highlight(health - effHealthLoss, "normal", "nur ein paar Kratzer")
!mmm   if not cOwnID.(cHealthAttr).max == 0 and newHealth <= 0
!rem     // LP <=0 : Death zone marker
!mmm     set newHealth = highlight(newHealth, "bad", "üíÄ (fast) tot")
!mmm     do setattr(cOwnID, "status_death_zone", true)
!mmm   else if not cOwnID.(cHealthAttr).max == 0 and newHealth <= 3 
!rem     // Health <= 3: Red token marker => no actions, movement reduced to 4, 10-minute countdown to shock
!mmm     set newHealth = highlight(newHealth, "bad", "üî¥ sehr schwer verletzt")
!mmm     do setattr(cOwnID, "status_red", true)
!mmm   else if not cOwnID.(cHealthAttr).max == 0 and newHealth <= .5 * maxHealth
!rem     // Health <= 50%: Yellow token marker => endurance and movement to be limited to 50%
!mmm     set newHealth = highlight(newHealth, "important", "üü° schwer verletzt")
!mmm     do setattr(cOwnID, "status_yellow", true)
!mmm   end if
!mmm   do setattr(cOwnID, cHealthAttr, newHealth)
!mmm   if effHealthLoss > 0 and newHealth < 0
!mmm     set timeToDie = roll("1d6-" & abs(newHealth)) 
!mmm   end if
!mmm   set effHealthLoss = highlight(effHealthLoss, "info")
!rem
!rem   // Process experience gain for attacker if script is run on an NPC, in which case player is GM with control over attacker sheet
!mmm   set attacker = m3mgdExchange.(m3mgdAttrAttackerID)
!mmm   if attacker.permission eq "control"
!mmm     if (cOwnID.(cEnduranceAttr).max == 0 and not defenseSuccess) or (cOwnID.(cEnduranceAttr).max != 0 and endurance > 0 and effEnduranceLoss > 0)
!mmm       set attackerXPGain = cOwnID.Grad + 3
!mmm       set XPGainLog = "3+" & cOwnID.Grad
!mmm       if m3mgdExchange.(m3mgdAttrAttackType) eq "ranged"
!mmm         set attackerXPGain = attackerXPGain / 2
!mmm         set XPGainLog = "(" & XPGainLog & ") / 2[FK]"
!mmm       end if
!rem       // Devastating hits
!mmm       if attackDamageRoll >= 8 and endurance >= 8
!mmm         set attackerXPGain = attackerXPGain * 2
!mmm         set XPGainLog = "(" & XPGainLog & ") x 2[verheerend]"
!mmm       end if
!mmm       do setattr(attacker, m3mgdAttrXP, attacker.(m3mgdAttrXP) + attackerXPGain)
!mmm       set modifierLog = modifierLog & " {{ Erfahrung (" & attacker.name & ")=+" & attackerXPGain & " EP (= " & XPGainLog & ")}} "
!mmm     else if cOwnID.(cEnduranceAttr).max == 0 and defenseSuccess
!mmm       set modifierLog = modifierLog & " {{ Erfahrung (" & attacker.name & ")=(keine: ‚àû AP, abgewehrt) }} "
!mmm     else if cOwnID.(cEnduranceAttr).max != 0 and endurance <= 0
!mmm       set modifierLog = modifierLog & " {{ Erfahrung (" & attacker.name & ")=(keine: Ziel ersch√∂pft) }} "
!mmm     else if cOwnID.(cEnduranceAttr).max != 0 and effEnduranceLoss <= 0
!mmm       set modifierLog = modifierLog & " {{ Erfahrung (" & attacker.name & ")=(keine: kein AP-Verlust) }} "
!mmm     end if
!mmm   else
!mmm     set modifierLog = modifierLog & " {{ Erfahrung (" & attacker.name & ")=(**kein Zugriff**: " & getreason(attacker) & ") }} "
!mmm   end if
!rem
!rem   // Verbose output
!mmm   if cVerbose
!mmm     combine chat
!rem       // Check if script is run for an NPC (whisper data to GM only)
!mmm       if cGMSilentMode == 1
!mmm         chat: /w GM 
!mmm       end if
!mmm       if defenseSuccess
!mmm         chat       [DefenseSuccess]:           Ich winde mich, strecke mich, wehre mich, und erleide nur einen **leichten Treffer**.
!mmm         if newHealth < 0
!mmm           chat     [OngoingCriticalInjury]:    ***Ich bin immer noch lebensgef√§hrlich verletzt, mein Countdown l√§uft weiter.*** [x](https://media.giphy.com/media/OY9XK7PbFqkNO/giphy.gif) ***Jetzt k√∂nnte ich eine heilende Hand gebrauchen...***
!mmm         end if
!mmm       else
!mmm         if criticalAttack
!mmm           chat     [DefenseFailureCritDamage]: Ich winde mich, strecke mich, versuche mich zu wehren, aber es reicht einfach nicht: mich erwischt ein schwerer **kritischer Treffer**.
!mmm         else 
!mmm           chat     [DefenseFailureRegDamage]:  Ich winde mich, strecke mich, versuche mich zu wehren, aber es reicht einfach nicht: mich erwischt ein schwerer Treffer.
!mmm         end if
!mmm         if effHealthLoss > 0.33 * maxHealth
!mmm           chat     [DefenseFailureHugeHit]:    AAAAAAARRG! Blut spritzt, Knochen brechen, solche Treffer halte ich nicht viele aus!
!mmm         else if effHealthLoss > .2 * maxHealth
!mmm           chat     [DefenseFailureSeriousHit]: AAAH! Das darf mir nicht oft passieren.
!mmm         else if effHealthLoss > .1 * maxHealth
!mmm           chat     [DefenseFailureModestHit]:  Aua!
!mmm         else if effHealthLoss > 0
!mmm           chat     [DefenseFailureMinorHit]:   Au. Pieks. Na warte!
!mmm         else 
!mmm           chat     [DefenseFailureNoDamage]:   Doch kein Problem f√ºr meine $[armorLabel]{armorLabel}, au√üer Schwei√ü verliere ich nichts.
!mmm         end if
!mmm         if newHealth < 0 and timeToDie < 0
!mmm           chat     [EffectsImmediateDeath]:    War sch√∂n mit euch allen. Bis dann, und danke f√ºr den Fisch! $[ownName]{cOwnID.name}, out.${"<"}br>[x](https://media.giphy.com/media/96quMvXlWpwRO/giphy.gif)
!mmm         else if newHealth < 0 and timeToDie >= 0
!mmm           chat     [EffectsCriticalInjury]:    ***Ich bin lebensgef√§hrlich verletzt.*** Ich habe noch **$[timeToDie]{timeToDie+0} Minuten** zu leben.${"<"}br>[x](https://media.giphy.com/media/OY9XK7PbFqkNO/giphy.gif)${"<"}br>***Jetzt k√∂nnte ich eine heilende Hand gebrauchen...***
!mmm         end if
!mmm       end if 
!mmm     end combine
!mmm   end if
!rem
!rem   // Output data table using data table execution code from library
!mmm   do m3mgdDefenseDataTable()
!mmm
!rem   // Output modifier log and PP reminder
!mmm   chat: /w GM ${"&"}{template:default} ${modifierLog}
!mmm   chat: /w "${cOwnID.name}" ${"&"}{template:default} ${modifierLog}
!mmm   if parryWeapon and parryWeaponUsable and iscritical(cDefenseRoll)
!mmm     chat: /w "${cOwnID.name}" ***Nicht vergessen: PP f√ºr ${cWeaponLabel} einstecken!*** [x](https://media.giphy.com/media/hKafco7mFwBioBxqFT/giphy.gif)
!mmm     chat: /w GM               ***Nicht vergessen: PP f√ºr ${cWeaponLabel} einstecken!*** [x](https://media.giphy.com/media/hKafco7mFwBioBxqFT/giphy.gif)
!mmm   end if
!mmm end script