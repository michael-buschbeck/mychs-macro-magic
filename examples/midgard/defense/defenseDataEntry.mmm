!rem // defenseDataEntry
!mmm script
!mmm   set scriptVersion = "defenseDataEntry 2.1.2 (2022-01-24)"
!mmm
!mmm   if version < 1.26 or not m3mgdExchange
!mmm     do whisperback("MMM version too low or game-global m3mgdExchange variables missing.")
!mmm     exit script
!mmm   end if
!mmm
!rem   // Flush data structure
!mmm
!mmm   set flushedCount = m3mgdFlushExchange(m3mgdExchange, m3mgdExchangeAttrList)
!mmm   if not flushedCount
!mmm     do whisperback("Something went wrong, did m3mgdFlushExchange() not execute properly?")
!mmm     exit script
!mmm   end if
!mmm
!rem   // Collect manually entered attack data
!mmm
!mmm   set attackType = "?{Art des Angriffs| Nahkampf,melee| Fernkampf,ranged}"
!mmm   set attackerID = "@{target|Wer greift an?|token_id}"
!mmm   set targetID = "@{target|Wer wird angegriffen (Ziel)?|token_id}"
!mmm   set attackResult = "?{Angriffswert|0}"
!mmm   set criticalAttack = "?{Kritischer Erfolg beim Angriff|Nein,0|Ja,1}"
!mmm   set attackDamage = "?{Schaden laut Angreifer|0}"
!mmm   set attackDamageRoll = "?{Schadenswurf (Ergebnis von 1W6+1 ohne Boni) laut Angreifer|0}"
!mmm   set armorPiercing = "?{Angriff mit Lang-/Kompositbogen oder schwerer Armbrust| Nein,0| Ja,1}"
!mmm   set weaponType = "?{Waffentyp|waffenlos|Einhandschwert|Zweihandschwert|Fechtwaffe|Stichwaffe|Stockwaffe|Spießwaffe|Kettenwaffe|Einhandschlagwaffe|Zweihandschlagwaffe|Zauberstab|Bogen|Langbogen|Kompositbogen|schwere Armbrust|leichte Armbrust|Handarmbrust|Schleuder|Blasrohr|Wurfspieß|Wurfscheibe|Wurfklinge|Stielwurfwaffe}"
!mmm   if attackResult < 20
!mmm     do whisperback("Abbruch: Angriff mit **" & attackResult & "** war nicht erfolgreich, keine Abwehr nötig.")
!mmm     exit script
!mmm   end if
!mmm
!rem   // Store attack data in exchange structure
!mmm
!mmm   do setattr(m3mgdExchange, m3mgdAttrAttackType, attackType)
!mmm   do setattr(m3mgdExchange, m3mgdAttrAttackerID, attackerID)
!mmm   do setattr(m3mgdExchange, m3mgdAttrAttackTargetID, targetID)
!mmm   do setattr(m3mgdExchange, m3mgdAttrAttackResult, attackResult)
!mmm   do setattrmax(m3mgdExchange, m3mgdAttrAttackResult, (criticalAttack == 1))
!mmm   do setattr(m3mgdExchange, m3mgdAttrAttackDamage, attackDamage)
!mmm   do setattr(m3mgdExchange, m3mgdAttrAttackDamageRoll, attackDamageRoll)
!mmm   do setattr(m3mgdExchange, m3mgdAttrAttackArmorPiercing, armorPiercing)
!mmm   do setattr(m3mgdExchange, m3mgdAttrAttackWeaponType, weaponType)
!mmm   
!mmm end script
%{MacroSheet|defense}