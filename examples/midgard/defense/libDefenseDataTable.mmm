!rem // m3mgdDefenseDataTable()
!rem //
!mmm function m3mgdDefenseDataTable()
!mmm
!rem   // Prepare output
!rem
!mmm   set prvEndurance = script.endurance
!mmm   set prvHealth = script.health
!mmm   set shapeMoji = ""
!mmm   if not script.cOwnID.(cEnduranceAttr).max == 0 and prvEndurance <= 0
!mmm     set shapeMoji = "ðŸ¥´"
!mmm   end if
!mmm   if not script.cOwnID.(cHealthAttr).max == 0 and prvHealth <= 0
!mmm     set shapeMoji = "ðŸ’€"
!mmm   end if
!mmm
!mmm   if script.criticalAttack
!mmm     set attackResult = highlight(script.attackResult, "good", "Kritisch erfolgreicher Angriff von " & script.m3mgdExchange.(m3mgdAttrAttackerID).name)
!mmm   else
!mmm     set attackResult = highlight(script.attackResult, "info", "Angriff von " & script.m3mgdExchange.(m3mgdAttrAttackerID).name)
!mmm   end if
!mmm   set attackDamage = highlight(script.m3mgdExchange.(m3mgdAttrAttackDamage), "info", script.m3mgdExchange.(m3mgdAttrAttackType))
!mmm   set effEnduranceLoss = highlight(prvEndurance - newEndurance, "normal")
!mmm   set effHealthLoss = highlight(prvHealth - newHealth, "normal")
!rem
!rem   // Build data table
!rem
!mmm   combine chat
!mmm
!mmm     if script.cGMSilentMode == 1
!mmm       chat: /w GM 
!mmm     end if
!mmm
!mmm     chat: ${"&"}{template:default} 
!mmm
!mmm     if script.m3mgdExchange.(m3mgdAttrAttackType) eq "magic"
!mmm       chat: {{name=${shapeMoji}${script.cOwnID.name}: Abwehr gegen ${script.m3mgdExchange.(m3mgdAttrAttackType).max} }}
!mmm     else if _weaponLabel eq "Abwehr ohne Schild"
!mmm       chat: {{name=${shapeMoji}${script.cOwnID.name}: ${script.cWeaponLabel} }}
!mmm     else
!mmm       chat: {{name=${shapeMoji}${script.cOwnID.name}: Abwehr mit ${script.cWeaponLabel} }}
!mmm     end if 
!mmm
!mmm     if script.armorPiercing == 1 and attackDamage > 3
!mmm       chat: {{Angriff=**${attackResult}&nbsp;/&nbsp;${attackDamage}&nbsp;Schaden** (RÃ¼stungsschutz wg. Geschosstyp auf 3 begrenzt)}}
!mmm     else if attackDamage & "" eq "0"
!mmm       chat: {{Angriff=**${attackResult}&nbsp;/&nbsp;Sonderschaden**}}
!mmm     else if attackDamage > 0
!mmm       chat: {{Angriff=**${attackResult}&nbsp;/&nbsp;${attackDamage}&nbsp;Schaden**}}
!mmm     else
!mmm       chat: {{Angriff=${attackResult}}}
!mmm     end if
!mmm
!mmm     if script.defenseSuccess
!mmm       chat: {{Abwehr=${script.defenseResult} **Leichter Treffer**}}
!mmm       do spawnfx("bubbling-water", script.cOwnID.left, script.cOwnID.top)
!mmm     else if script.criticalAttack == true
!mmm       chat: {{Abwehr=${script.defenseResult} **Schwerer kritischer Treffer**}}
!mmm       do spawnfx("nova-blood", script.cOwnID.left, script.cOwnID.top)
!mmm     else 
!mmm       chat: {{Abwehr=${script.defenseResult} **Schwerer Treffer**}}
!mmm       do spawnfx("bubbling-blood", script.cOwnID.left, script.cOwnID.top)
!mmm     end if
!mmm
!mmm     if script.defenseSuccess and effEnduranceLoss > 0
!mmm       chat: {{Schaden=**${effEnduranceLoss}&nbsp;AP**}}
!mmm     else if not script.defenseSuccess and attackDamage & "" eq "0"
!mmm       chat: {{Schaden=**SPEZIAL**}}
!mmm     else if not script.defenseSuccess
!mmm       chat: {{Schaden=**${effHealthLoss}&nbsp;LP&nbsp;/&nbsp;${effEnduranceLoss}&nbsp;AP**}}
!mmm     end if
!mmm
!mmm     if script.criticalAttack == true and script.m3mgdExchange.(m3mgdAttrAttackType) eq "magic"
!mmm       chat: {{Kritischer Zaubererfolg=Zauber wirkt **doppelt so stark** oder ist **halb so teuer**: Nach Wahl des Zauberers umsetzen.}}
!mmm     else if script.criticalAttack == true
!mmm       chat: {{Kritischer Schaden=ðŸŽ²ðŸŽ²ðŸŽ²}}
!mmm     end if
!mmm
!mmm     if script.newHealth < 0 and script.timeToDie < 0
!mmm     
!mmm       chat: {{Zustand=**Sofortiger Tod &#10013;**}}
!mmm     
!mmm     else
!mmm     
!mmm       chat: {{Zustand=${script.newHealth} **LP** / ${script.newEndurance} **AP**}}
!mmm       chat: {{${m3mgdHealthStatusLabel(script.cOwnID, script.cHealthAttr)}=${m3mgdHealthStatusEffectsDesc(script.cOwnID, script.cHealthAttr)}}}
!mmm       chat: {{${m3mgdEnduranceStatusLabel(script.cOwnID, script.cEnduranceAttr)}=${m3mgdEnduranceStatusEffectsDesc(script.cOwnID, script.cEnduranceAttr)}}}
!mmm     
!mmm     end if
!mmm
!mmm   end combine
!mmm
!mmm   return true
!mmm   
!mmm end function