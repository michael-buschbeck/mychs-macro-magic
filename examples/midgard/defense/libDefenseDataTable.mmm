!rem // begin MMM fragment
!rem
!mmm   set CURRENT = "libDefenseDataTable 1.0.0 (2021-12-20)"
!mmm
!mmm   if not (cGMSilentMode == 1 or cGMSilentMode == 0)
!mmm     do whisperback(CURRENT & ": invalid variable value - cGMSilentMode")
!mmm     exit script
!mmm   end if
!mmm
!mmm   set cOwnID = cOwnID.token_id
!mmm   if isdenied(cOwnID) or isunknown(cOwnID)
!mmm     do whisperback(CURRENT & ": invalid variable value - cOwnID: " & getreason(cOwnID))
!mmm     exit script
!mmm   end if
!mmm
!mmm   set attackType = m3mgdExchange.(m3mgdAttrAttackType)
!mmm   if isdenied(attackType) or isunknown(attackType)
!mmm     do whisperback(CURRENT & ": Access to data exchange sheet failed: " & getreason(attackType))
!mmm     exit script
!mmm   end if
!mmm   set attackTypeSpell = m3mgdExchange.(m3mgdAttrAttackType).max
!rem
!rem   // Build data table
!rem
!mmm   set shapeMoji = ""
!mmm   if not cOwnID.(cEnduranceAttr).max == 0 and cOwnID.(cEnduranceAttr) <= 0
!mmm     set shapeMoji = "ðŸ¥´"
!mmm   end if
!mmm   if not cOwnID.(cHealthAttr).max == 0 and cOwnID.(cHealthAttr) <= 0
!mmm     set shapeMoji = "ðŸ’€"
!mmm   end if
!rem
!rem   // Build data table
!rem
!mmm   combine chat
!mmm
!mmm     if cGMSilentMode == 1
!mmm       chat: /w GM 
!mmm     end if
!mmm
!mmm     chat: ${"&"}{template:default} 
!mmm
!mmm     if attackType eq "magic"
!mmm       chat: {{name=${shapeMoji}&nbsp;${cOwnID.name}: Abwehr gegen ${attackTypeSpell} }}
!mmm     else if cWeaponLabel eq "Abwehr ohne Schild"
!mmm       chat: {{name=${shapeMoji}&nbsp;${cOwnID.name}: ${cWeaponLabel} }}
!mmm     else
!mmm       chat: {{name=${shapeMoji}&nbsp;${cOwnID.name}: Abwehr mit ${cWeaponLabel} }}
!mmm     end if 
!mmm
!mmm     if armorPiercing == 1 and attackDamage > 3
!mmm       chat: {{Angriff=**${attackResult}&nbsp;/&nbsp;${attackDamage}&nbsp;Schaden** (RÃ¼stungsschutz wg. Geschosstyp auf 3 begrenzt)}}
!mmm     else if attackDamage & "" eq "0"
!mmm       chat: {{Angriff=**${attackResult}&nbsp;/&nbsp;Sonderschaden**}}
!mmm     else if attackDamage > 0
!mmm       chat: {{Angriff=**${attackResult}&nbsp;/&nbsp;${attackDamage}&nbsp;Schaden**}}
!mmm     else
!mmm       chat: {{Angriff=${attackResult}}}
!mmm     end if
!mmm
!mmm     if defenseSuccess
!mmm       chat: {{Abwehr=${defenseResult} **Leichter Treffer**}}
!mmm       do spawnfx("bubbling-water", cOwnID.left, cOwnID.top)
!mmm     else if criticalAttack == true
!mmm       chat: {{Abwehr=${defenseResult} **Schwerer kritischer Treffer**}}
!mmm       do spawnfx("nova-blood", cOwnID.left, cOwnID.top)
!mmm     else 
!mmm       chat: {{Abwehr=${defenseResult} **Schwerer Treffer**}}
!mmm       do spawnfx("bubbling-blood", cOwnID.left, cOwnID.top)
!mmm     end if
!mmm
!mmm     if defenseSuccess and effEnduranceLoss > 0
!mmm       chat: {{Schaden=**${effEnduranceLoss}&nbsp;AP**}}
!mmm     else if not defenseSuccess and attackDamage & "" eq "0"
!mmm       chat: {{Schaden=**SPEZIAL**}}
!mmm     else if not defenseSuccess
!mmm       chat: {{Schaden=**${effHealthLoss}&nbsp;LP&nbsp;/&nbsp;${effEnduranceLoss}&nbsp;AP**}}
!mmm     end if
!mmm
!mmm     if criticalAttack == true and attackType eq "magic"
!mmm       chat: {{Kritischer Zaubererfolg=Zauber wirkt **doppelt so stark** oder ist **halb so teuer**: Nach Wahl des Zauberers umsetzen.}}
!mmm     else if criticalAttack == true
!mmm       chat: {{Kritischer Schaden=ðŸŽ²ðŸŽ²ðŸŽ²}}
!mmm     end if
!mmm
!mmm     if newHealth < 0 and timeToDie < 0
!mmm       chat: {{Zustand=**Sofortiger Tod &#10013;**}}
!mmm     else 
!mmm       chat: {{Zustand=${newHealth} **LP** / ${newEndurance} **AP**}}
!mmm       if not cOwnID.(cHealthAttr).max == 0 and newHealth > 3 and newHealth <= 0.5 * maxHealth
!mmm         chat: {{Schwer verwundet=ðŸŸ¡ max. halbe AP, halbe Bewegung}}
!mmm       else if not cOwnID.(cHealthAttr).max == 0 and newHealth > 0 and newHealth <= 3
!mmm         chat: {{Extrem schwer verwundet=ðŸ”´ handlungsunfÃ¤hig, B=4, PW:Kon nach 10min, sonst Schock}}
!mmm       else if not cOwnID.(cHealthAttr).max == 0 and newHealth == 0
!mmm         chat: {{Dem Tode nah=ðŸ’€ handlungsunfÃ¤hig, B=0, PW:Kon nach 10min, sonst Schock}}
!mmm       else if newHealth < 0 and defenseSuccess
!mmm         chat: {{TÃ¶dlich verwundet=ðŸ’€ **Countdown lÃ¤uft**}}
!mmm       else if newHealth < 0 and not defenseSuccess and timeToDie >= 0
!mmm         chat: {{TÃ¶dlich verwundet=ðŸ’€ **${timeToDie} Minuten bis zum Tod**}}
!mmm       end if
!mmm       if not cOwnID.(cEnduranceAttr).max == 0 and newEndurance == 0 and (newHealth >= 0 and not (timeToDie < 0))
!mmm         chat: {{ErschÃ¶pft=ðŸŸ¢ (-4 auf alles, max. Last reduziert)}}
!mmm       end if
!mmm     end if
!mmm
!mmm   end combine
!rem // end MMM fragment