!mmm script
!mmm
!mmm   set DE = { round: "Runde", rounds: "Runden", defense: "Abwehr", defenses: "Abwehren", attack: "Angriff", attacks: "Angriffe" }
!mmm
!mmm   for token in gettokens()
!mmm 
!mmm     if not isdenied(token.token_id)
!mmm
!mmm       set tokenID = token.token_id
!mmm   
!mmm       combine chat using ""
!mmm       
!mmm         chat: /w "${tokenID.character_name}" ${"&"}{template:default} 
!mmm         chat: {{name=${m3mgdShapeMoji(tokenID) & tokenID.name}: Rundeneffekte}} 
!mmm
!mmm         for statusRule in m3mgdInjuryStatus... where tokenID.(...key) eq "shown"
!mmm
!mmm           if statusRule.value.combatRoundEffects and not timeToDie < 0
!mmm
!mmm             chat: {{ ${statusRule.value.icon}${statusRule.value.label}=
!mmm
!mmm             for roundEffect in statusRule.value.combatRoundEffects...
!mmm
!mmm               if roundEffect.key eq "health"
!mmm 
!mmm                 set newHealth = m3mgdModifyHealth(roundEffect.value, tokenID, m3mgdGetHealthAttribute(tokenID))
!mmm                 chat: ${" "}**LP:**&nbsp;${highlight(roundEffect.value, "bad", statusRule.value.desc)}&nbsp;=>&nbsp;${highlight(newHealth, "normal")}
!mmm                 if newHealth < 0
!mmm                   set timeToDie = roll("1d6-" & abs(newHealth)) 
!mmm                   chat: ${highlight(timeToDie, "bad")}&nbsp;**Minuten bis zum Tod!**
!mmm                 end if
!mmm
!mmm               else if roundEffect.key eq "endurance"
!mmm
!mmm                 set newEndurance = m3mgdModifyEndurance(roundEffect.value, tokenID, m3mgdGetEnduranceAttribute(tokenID))
!mmm                 chat: ${" "}**AP:**&nbsp;${highlight(roundEffect.value, "bad", statusRule.value.desc)}&nbsp;=>&nbsp;${highlight(newEndurance, "normal")}
!mmm
!mmm               else if roundEffect.key eq "death"
!mmm
!mmm                 set deathRoll = roll("1d100")
!mmm                 if deathRoll < roundEffect.value
!mmm                   chat: ${highlight(deathRoll, "bad", "<" & roundEffect.value)} war **tÃ¶dlich:** ${tokenID.name} ist in diesem Moment von uns gegangen.
!mmm                   set newHealth = -1
!mmm                   set timeToDie = -1
!mmm                 else
!mmm                   chat: Puh, GlÃ¼ck gehabt. Mit ${highlight(deathRoll, "good", ">=" & roundEffect.value)} bin ich dem Tod nochmal von der Schippe gesprungen.
!mmm                 end if
!mmm
!mmm               end if 
!mmm
!mmm             end for
!mmm
!mmm             chat: }}
!mmm
!mmm           end if
!mmm
!mmm         end for
!mmm     
!mmm         set counterUpdates = m3mgdUpdatePersistentEffectsCounters(tokenID, "round")
!mmm         if counterUpdates
!mmm           chat: {{ ðŸ”µ Updates=${counterUpdates} }}
!mmm         end if
!mmm
!mmm         if m3mgdHasActivePersistentEffects(tokenID)
!mmm           for statusRule in m3mgdGetActivePersistentEffects(tokenID)
!mmm
!mmm             set activeCounter = activeCounter + 1
!mmm             chat: {{ #${activeCounter}, noch ${statusRule.effect.expiry.count} ${DE.(statusRule.effect.expiry.type & "s")}= ${highlight(statusRule.label, "info", statusRule.desc)} 
!mmm             chat: ${stringify(m3mgdPersistentEffectLabels(statusRule.effect), "", "; ")}
!mmm             chat: }}
!mmm           
!mmm           end for
!mmm         end if
!mmm
!mmm         if newHealth < 0 and timeToDie < 0
!mmm         
!mmm           chat: {{Zustand=**Sofortiger Tod &#10013;**}}
!mmm         
!mmm         else
!mmm         
!mmm           chat: {{${m3mgdHealthStatusLabel(tokenID)}=${m3mgdHealthStatusEffectsDesc(tokenID)}}}
!mmm           chat: {{${m3mgdEnduranceStatusLabel(tokenID)}=${m3mgdEnduranceStatusEffectsDesc(tokenID)}}}
!mmm         
!mmm         end if
!mmm         
!mmm       end combine
!mmm
!mmm     end if
!mmm     
!mmm   end for
!mmm   
!mmm end script