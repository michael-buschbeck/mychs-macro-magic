!mmm script
!mmm
!mmm   set DE = { round: "Runde", rounds: "Runden", defense: "Abwehr", defenses: "Abwehren", attack: "Angriff", attacks: "Angriffe" }
!mmm
!mmm   for charName in m3mgdPlayerCharacters
!mmm 
!mmm     if not isdenied(charName.character_id)
!mmm
!mmm       set charID = charName.character_id
!mmm   
!mmm       combine chat using ""
!mmm       
!mmm         chat: /w "${charID.character_name}" ${"&"}{template:default} 
!mmm         chat: {{name=${m3mgdShapeMoji(charID) & charID.name}: Rundeneffekte}} 
!mmm
!mmm         for statusRule in m3mgdInjuryStatus... where charID.("status_" & ...key) eq "shown"
!mmm
!mmm           if statusRule.value.combatRoundEffects and not timeToDie < 0
!mmm
!mmm             chat: {{ ${statusRule.value.icon}${statusRule.value.label}=
!mmm
!mmm             for roundEffect in statusRule.value.combatRoundEffects...
!mmm
!mmm               if roundEffect.key eq "health"
!mmm 
!mmm                 set newHealth = m3mgdModifyHealth(roundEffect.value, charID, m3mgdGetHealthAttribute(charID))
!mmm                 chat: ${" "}**LP:**&nbsp;${highlight(roundEffect.value, "bad", statusRule.value.desc)}&nbsp;=>&nbsp;${highlight(newHealth, "normal")}
!mmm                 if newHealth < 0
!mmm                   set timeToDie = roll("1d6-" & abs(newHealth)) 
!mmm                   chat: ${highlight(timeToDie, "bad")}&nbsp;**Minuten bis zum Tod!**
!mmm                 end if
!mmm
!mmm               else if roundEffect.key eq "endurance"
!mmm
!mmm                 set newEndurance = m3mgdModifyEndurance(roundEffect.value, charID, m3mgdGetEnduranceAttribute(charID))
!mmm                 chat: ${" "}**AP:**&nbsp;${highlight(roundEffect.value, "bad", statusRule.value.desc)}&nbsp;=>&nbsp;${highlight(newEndurance, "normal")}
!mmm
!mmm               else if roundEffect.key eq "death"
!mmm
!mmm                 set deathRoll = roll("1d100")
!mmm                 if deathRoll < roundEffect.value
!mmm                   chat: ${highlight(deathRoll, "bad", "<" & roundEffect.value)} war **tÃ¶dlich:** ${charID.name} ist in diesem Moment von uns gegangen.
!mmm                   set newHealth = -1
!mmm                   set timeToDie = -1
!mmm                 else
!mmm                   chat: Puh, GlÃ¼ck gehabt. Mit ${highlight(deathRoll, "good", ">=" & roundEffect.value)} bin ich dem Tod nochmal von der Schippe gesprungen.
!mmm                 end if
!mmm
!mmm               end if 
!mmm
!mmm             end for
!mmm
!mmm             chat: }}
!mmm
!mmm           end if
!mmm
!mmm         end for
!mmm     
!mmm         set counterUpdates = m3mgdUpdatePersistentEffectsCounters(charID, "round")
!mmm         if counterUpdates
!mmm           chat: {{ ðŸ”µ Updates=${counterUpdates} }}
!mmm         end if
!mmm
!mmm         if charID.m3mgdActivePersistentEffects
!mmm           for statusRule in deserialize(charID.m3mgdActivePersistentEffects)
!mmm
!mmm             set activeCounter = activeCounter + 1
!mmm             chat: {{ #${activeCounter}, noch ${statusRule.effect.expiry.count} ${DE.(statusRule.effect.expiry.type & "s")}= ${highlight(statusRule.label, "info", statusRule.desc)} 
!mmm             for mod in statusRule.rollModifiers...
!mmm               chat: ${sign(mod.value, "display")} ${mod.key}; 
!mmm             end for
!mmm             if statusRule.effect.noAttack
!mmm               chat: kein Angriff;
!mmm             end if
!mmm             if statusRule.effect.noDefense
!mmm               chat: keine Abwehr;
!mmm             end if
!mmm             if statusRule.effect.noFocusedDefense
!mmm               chat: keine konzentrierte Abwehr;
!mmm             end if
!mmm             if statusRule.effect.marker
!mmm               chat: ${statusRule.effect.marker};
!mmm             end if
!mmm             if statusRule.effect.cappedAttribute
!mmm               chat: **${statusRule.effect.cappedAttribute}** reduziert
!mmm             end if
!mmm             chat: }}
!mmm           
!mmm           end for
!mmm         end if
!mmm
!mmm         if newHealth < 0 and timeToDie < 0
!mmm         
!mmm           chat: {{Zustand=**Sofortiger Tod &#10013;**}}
!mmm         
!mmm         else
!mmm         
!mmm           chat: {{${m3mgdHealthStatusLabel(charID)}=${m3mgdHealthStatusEffectsDesc(charID)}}}
!mmm           chat: {{${m3mgdEnduranceStatusLabel(charID)}=${m3mgdEnduranceStatusEffectsDesc(charID)}}}
!mmm         
!mmm         end if
!mmm         
!mmm       end combine
!mmm
!mmm     end if
!mmm     
!mmm   end for
!mmm   
!mmm end script