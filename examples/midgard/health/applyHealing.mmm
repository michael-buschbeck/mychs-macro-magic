!rem // applyHealing applies both healing and energy boosts (or either)
!rem //
!mmm script
!mmm   set scriptVersion = "applyHealing 2.0.0 (2022-02-19)"
!mmm
!mmm   set customizable cCheckVersion = false
!mmm   if cCheckVersion
!mmm     do whisperback(scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!mmm   if version < 1.26 or not m3mgdExchange
!mmm     do whisperback("MMM version too low or game-global m3mgdExchange variables missing.")
!mmm     exit script
!mmm   end if
!mmm
!mmm   if selected and sender.token_id ne selected
!mmm     set customizable cOwnID = selected.token_id
!mmm   else 
!mmm     set customizable cOwnID = sender.token_id
!mmm   end if
!mmm   set cHealthAttr = m3mgdGetHealthAttribute(cOwnID)
!mmm   set cEnduranceAttr = m3mgdGetEnduranceAttribute(cOwnID)
!mmm   
!rem   // Points to be applied are taken, first, from customize block, and only if there was none, from the data exchange
!mmm   set customizable maxEnduranceGain = m3mgdExchange.(m3mgdAttrEnduranceGain)
!mmm   set customizable maxHealthGain = m3mgdExchange.(m3mgdAttrHealthGain)
!mmm   
!rem   // If neither is present, spill out customize block for manual entry & rerun the script
!mmm   if not (maxHealthGain > 0 or maxEnduranceGain > 0)
!mmm   
!mmm     set payload = "!mmm customize&#13;"
!mmm     set payload = payload & literal("!mmm set maxHealthGain=?" & "{Gewonnene Lebenspunkte (LP)|0}") & "&#13;"
!mmm     set payload = payload & literal("!mmm set maxEnduranceGain=?" & "{Gewonnene Ausdauerpunkte (AP)|0}") & "&#13;"
!mmm     set payload = payload & "!mmm end customize&#13;"
!mmm     set payload = payload & "&#x25;{MacroSheet|applyHealing}"
!mmm     chat: /w "${cOwnID.character_name}" [**Lebens-/Ausdauergewinn eingeben**](${payload})
!mmm     exit script
!mmm
!mmm   end if 
!mmm
!mmm   set newHealth = m3mgdModifyHealth(maxHealthGain, cOwnID, cHealthAttr)
!mmm   
!mmm   set prvEndurance = cOwnID.(cEnduranceAttr)
!mmm   set newEndurance = m3mgdModifyEndurance(maxEnduranceGain, cOwnID, cEnduranceAttr)
!mmm   set effEnduranceGain = newEndurance - prvEndurance
!mmm   set enduranceHighlight = "normal"
!mmm   if effEnduranceGain < maxEnduranceGain
!mmm     set enduranceHighlight = "important"
!mmm     if newEndurance == cOwnID.(cEnduranceAttr).max
!mmm       set enduranceLog = "Auf AP.max begrenzt"
!mmm     else if newEndurance <= m3mgdAttrCeiling(cOwnID, cEnduranceAttr)
!mmm       set enduranceLog = "Auf " & m3mgdAttrCeiling(cOwnID, cEnduranceAttr) & " begrenzt"
!mmm     else 
!mmm       set enduranceLog = "Fehler? Grund fÃ¼r reduzierten AP-Gewinn unbekannt"
!mmm     end if
!mmm   end if
!mmm
!mmm   if newHealth <= 0 and timeToDie == undef
!mmm     set timeToDie = "(unbekannt)"
!mmm   end if
!mmm
!mmm   combine chat
!mmm     
!mmm     chat: /w "${cOwnID.character_name}" ${"&"}{template:default} {{name=${m3mgdShapeMoji() & cOwnID.name}: Heil-/Ausdauereffekte}} 
!mmm     
!mmm     chat: {{Heileffekt=${highlight("+" & maxHealthGain, "normal")} **=>** ${newHealth} **LP**}}
!mmm
!mmm     chat: {{Ausdauereffekt=${highlight("+" & effEnduranceGain & "/" & maxEnduranceGain, enduranceHighlight, enduranceLog)} **=>** ${newEndurance} **AP**}}
!mmm     
!mmm     if newHealth < 0 and timeToDie < 0
!mmm     
!mmm       chat: {{Zustand=**Sofortiger Tod &#10013;**}}
!mmm     
!mmm     else
!mmm     
!mmm       chat: {{${m3mgdHealthStatusLabel()}=${m3mgdHealthStatusEffectsDesc()}}}
!mmm       chat: {{${m3mgdEnduranceStatusLabel()}=${m3mgdEnduranceStatusEffectsDesc()}}}
!mmm     
!mmm     end if
!mmm     
!mmm   end combine
!mmm
!mmm end script