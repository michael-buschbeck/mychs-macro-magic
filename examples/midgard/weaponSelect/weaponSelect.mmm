!rem // Midgard combat weapon selector script
!rem //
!mmm script
!mmm   set scriptVersion = "weaponSelect v1.2.1 (2024-08-11)"
!rem
!mmm   set customizable cCheckVersion = false
!mmm   if cCheckVersion
!mmm     do whisperback(scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!rem   // MMM compatibility check: die if MMM version too low
!mmm   if version < 1.26
!mmm     do whisperback("Abbruch: MMM-Version 1.26.0 oder höher erforderlich.")
!mmm     exit script
!mmm   end if
!mmm   
!mmm   set customizable cWeaponGroups = "melee", "ranged"
!mmm   set customizable cOwnID = sender.token_id
!mmm   
!rem   // Refetch token_id, just if customize block provided a name or character ID
!mmm   set cOwnID = cOwnID.token_id
!mmm   if isdenied(cOwnID)
!mmm     do whisperback("Abbruch: Keine Zuordnung zu einem zugreifbaren Charakter möglich - " & getreason(cOwnID)) 
!mmm     exit script
!mmm   end if
!mmm
!rem   // Inform user, abort attack if injury or critical effect status does not allow for it, and count down persistent effects if required
!mmm   if m3mgdIsUnfitToAttack(cOwnID)
!mmm     exit script
!mmm   end if
!rem
!rem   // MAIN: Generate weapon selector menus for each weapon group requested 
!mmm
!mmm   set sender = cOwnID.token_name
!mmm   combine chat
!mmm     chat: /w "${cOwnID.character_name}"
!mmm     for weaponGroup in cWeaponGroups
!mmm       do chat(m3mgdWeaponSelectorChatMenu(cOwnID, weaponGroup))
!mmm     end for
!mmm   end combine
!mmm
!mmm end script