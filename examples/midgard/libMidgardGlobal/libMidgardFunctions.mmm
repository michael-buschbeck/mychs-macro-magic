!rem // m3mgdValidateOwnTokenID(tokenID)
!rem // 
!rem // 
!rem // 
!rem // 
!mmm function m3mgdValidateOwnTokenID(tokenID)
!mmm 
!mmm   if tokenID == default
!mmm     set id = script.cOwnID.token_id
!mmm   else
!mmm     set id = tokenID.token_id
!mmm   end if
!mmm 
!mmm   if isunknown(id) or isdenied(id)
!mmm     do whisperback("Token ID invalid: " & tokenID & " - " & getreason(id))
!mmm     return false
!mmm   end if
!mmm 
!mmm   return id 
!mmm 
!mmm end function
!rem // 
!rem // m3mgdFlushExchange(dataExchangeID, attrList)
!rem // 
!rem //   Expects a global data exchange character sheet as script.m3mgdExchange or as an argument
!rem // 
!rem //   Returns the number of attributes set to "" in both their current and max values
!rem // 
!mmm function m3mgdFlushExchange(dataExchangeID, attrList)
!mmm
!mmm   set flushedCounter = 0
!mmm   for attribute in attrList
!mmm     do setattr(dataExchangeID, attribute, "")
!mmm     do setattrmax(dataExchangeID, attribute, "")
!mmm     set flushedCounter = flushedCounter + 1
!mmm   end for
!mmm
!mmm   return flushedCounter
!mmm 
!mmm end function
!rem // 
!rem // m3mgdEnduranceStatusLabel(tokenID, enduranceAttr)
!rem // 
!rem // TODO: validate arguments, handle defaults/remove enduranceAttr arg once access to global script.foo variables works
!rem // 
!mmm function m3mgdEnduranceStatusLabel(tokenID, enduranceAttr)
!mmm   
!mmm   set tokenID = m3mgdValidateOwnTokenID(tokenID)
!mmm   if not tokenID
!mmm     return false
!mmm   end if
!mmm   
!mmm   if enduranceAttr == default and not (isunknown(tokenID.(script.cEnduranceAttr)) or isdenied(tokenID.(script.cEnduranceAttr)))
!mmm     set enduranceAttr = script.cEnduranceAttr
!mmm   else
!mmm     do whisperback("Fatal call to m3mgdEnduranceStatusLabel(): default enduranceAttr invalid")
!mmm     return false
!mmm   end if
!mmm
!mmm   if tokenID.status_green eq "shown"
!mmm     
!mmm     return "ErschÃ¶pft"
!mmm     
!mmm   else
!mmm     
!mmm     return "Geht noch!"
!mmm     
!mmm   end if
!mmm     
!mmm end function
!rem // 
!rem // m3mgdEnduranceStatusEffectsDesc(tokenID, enduranceAttr)
!rem // 
!rem // TODO: validate arguments, handle defaults/remove enduranceAttr arg once access to global script.foo variables works
!rem // 
!mmm function m3mgdEnduranceStatusEffectsDesc(tokenID, enduranceAttr)
!mmm   
!mmm   set tokenID = m3mgdValidateOwnTokenID(tokenID)
!mmm   if not tokenID
!mmm     return false
!mmm   end if
!mmm
!mmm   if tokenID.status_green eq "shown"
!mmm     
!mmm     return "ðŸŸ¢ -4 auf alles, max. Last reduziert"
!mmm     
!mmm   else
!mmm     
!mmm     return "keine EinschrÃ¤nkungen"
!mmm     
!mmm   end if
!mmm     
!mmm end function
!rem // 
!rem // m3mgdEnduranceStatusHighlight(tokenID, enduranceAttr)
!rem // 
!rem // TODO: validate arguments, handle defaults/remove enduranceAttr arg once access to global script.foo variables works
!rem // 
!mmm function m3mgdEnduranceStatusHighlight(tokenID, enduranceAttr)
!mmm   
!mmm   set tokenID = m3mgdValidateOwnTokenID(tokenID)
!mmm   if not tokenID
!mmm     return false
!mmm   end if
!mmm
!mmm   if tokenID.(enduranceAttr) == tokenID.(enduranceAttr).max
!mmm     
!mmm     return "good"
!mmm     
!mmm   else if tokenID.status_green eq "shown"
!mmm     
!mmm     return "bad"
!mmm     
!mmm   else
!mmm     
!mmm     return "normal"
!mmm     
!mmm   end if
!mmm     
!mmm end function
!rem // 
!rem // m3mgdHealthStatusLabel(tokenID, healthAttr)
!rem // 
!rem // TODO: validate arguments, handle defaults/remove healthAttr arg once access to global script.foo variables works
!rem // 
!mmm function m3mgdHealthStatusLabel(tokenID, healthAttr)
!mmm   
!mmm   set tokenID = m3mgdValidateOwnTokenID(tokenID)
!mmm   if not tokenID
!mmm     return false
!mmm   end if
!mmm
!mmm   if tokenID.status_yellow eq "shown"
!mmm     
!mmm     return "Schwer verwundet"
!mmm     
!mmm   else if tokenID.status_red eq "shown"
!mmm     
!mmm     return "Extrem schwer verwundet"
!mmm     
!mmm   else if tokenID.status_death_zone eq "shown" and tokenID.(healthAttr) >= 0
!mmm     
!mmm     return "Dem Tode nah"
!mmm     
!mmm   else if tokenID.status_death_zone eq "shown" and tokenID.(healthAttr) < 0
!mmm     
!mmm     return "TÃ¶dlich verwundet"
!mmm     
!mmm   else
!mmm     
!mmm     return "Soweit in Ordnung!"
!mmm     
!mmm   end if
!mmm     
!mmm end function
!rem // 
!rem // m3mgdHealthStatusEffectsDesc(tokenID, healthAttr)
!rem // 
!rem // TODO: validate arguments, handle defaults/remove healthAttr arg once access to global script.foo variables works
!rem // 
!mmm function m3mgdHealthStatusEffectsDesc(tokenID, healthAttr)
!mmm   
!mmm   set tokenID = m3mgdValidateOwnTokenID(tokenID)
!mmm   if not tokenID
!mmm     return false
!mmm   end if
!mmm
!mmm   if tokenID.status_yellow eq "shown"
!mmm     
!mmm     return "ðŸŸ¡ max. halbe AP, halbe Bewegung"
!mmm     
!mmm   else if tokenID.status_red eq "shown"
!mmm     
!mmm     return "ðŸ”´ handlungsunfÃ¤hig, B=4, PW:Kon nach 10min, sonst Schock"
!mmm     
!mmm   else if tokenID.status_death_zone eq "shown" and tokenID.(healthAttr) >= 0
!mmm     
!mmm     return "ðŸ’€ handlungsunfÃ¤hig, B=0, PW:Kon nach 10min, sonst Schock"
!mmm     
!mmm   else if tokenID.status_death_zone eq "shown" and tokenID.(healthAttr) < 0
!mmm     
!mmm     return "ðŸ’€ **Countdown lÃ¤uft**"
!mmm     
!mmm   else
!mmm     
!mmm     return "keine EinschrÃ¤nkungen"
!mmm     
!mmm   end if
!mmm     
!mmm end function
!rem // 
!rem // m3mgdHealthStatusHighlight(tokenID, healthAttr)
!rem // 
!rem // TODO: validate arguments, handle defaults/remove healthAttr arg once access to global script.foo variables works
!rem // 
!mmm function m3mgdHealthStatusHighlight(tokenID, healthAttr)
!mmm   
!mmm   set tokenID = m3mgdValidateOwnTokenID(tokenID)
!mmm   if not tokenID
!mmm     return false
!mmm   end if
!mmm
!mmm   if tokenID.(healthAttr) == tokenID.(healthAttr).max
!mmm     
!mmm     return "good"
!mmm     
!mmm   else if tokenID.status_yellow eq "shown"
!mmm     
!mmm     return "important"
!mmm     
!mmm   else if tokenID.status_red eq "shown"
!mmm     
!mmm     return "bad"
!mmm     
!mmm   else if tokenID.status_death_zone eq "shown"
!mmm     
!mmm     return "bad"
!mmm     
!mmm   else
!mmm     
!mmm     return "normal"
!mmm     
!mmm   end if
!mmm     
!mmm end function
!rem // 
!rem // m3mgdEnduranceBoost(enduranceGain, tokenID, enduranceAttr)
!rem // 
!rem // enduranceGain       Number of endurance points applied to character
!rem //   
!rem // Returns the new level of endurance
!rem //
!mmm function m3mgdEnduranceBoost(enduranceGain, tokenID, enduranceAttr)
!mmm
!mmm   set tokenID = m3mgdValidateOwnTokenID(tokenID)
!mmm   if not tokenID
!mmm     return false
!mmm   end if
!mmm   if enduranceAttr == default and not (isunknown(script.cOwnID.(script.cEnduranceAttr)) or isdenied(script.cOwnID.(script.cEnduranceAttr)))
!mmm     set enduranceAttr = script.cEnduranceAttr
!mmm   else
!mmm     do whisperback("Fatal call to m3mgdEnduranceBoost(): default health attribute invalid - " & script.cEnduranceAttr)
!mmm     return false
!mmm   end if
!mmm   
!rem   // Check if character is actually alive
!mmm
!mmm   set prvEndurance = tokenID.(enduranceAttr)
!mmm   set maxEndurance = tokenID.(enduranceAttr).max
!mmm   set newEndurance = false
!mmm
!mmm   if maxEndurance == 0
!mmm
!mmm     do whisperback(tokenID.token_name & " ist kein Wesen, das Ausdauerpunkte gewinnen kann (AP.max=0).")
!mmm
!mmm   else
!mmm
!rem     // Process endurance gain and applicable consequences 
!mmm
!mmm     if prvEndurance + enduranceGain > maxEndurance
!mmm       set effEnduranceGain = highlight(maxEndurance - prvEndurance, "important", "begrenzt auf Maximum")
!mmm     else
!mmm       set effEnduranceGain = highlight(enduranceGain, "normal")
!mmm     end if 
!mmm   
!mmm     set newEndurance = prvEndurance + effEnduranceGain
!mmm   
!mmm     if newEndurance == maxEndurance
!mmm   
!mmm       set newEndurance = highlight(newEndurance, "good", "= " & prvEndurance & " + " & effEnduranceGain)
!mmm   
!mmm     else if newEndurance <= 0
!mmm   
!mmm       set newEndurance = highlight(newEndurance, "bad", "ðŸŸ¢ (= " & prvEndurance & " + " & effEnduranceGain & ")")
!mmm       do setattr(tokenID, "status_green", true)
!mmm   
!mmm     else if newEndurance < maxEndurance
!mmm   
!mmm       set newEndurance = highlight(newEndurance, "normal", "Och, das bisschen SchweiÃŸ! (= " & prvEndurance & " + " & effEnduranceGain & ")")
!mmm       do setattr(tokenID, "status_green", false)
!mmm   
!mmm     else
!mmm   
!mmm       set newEndurance = highlight(newEndurance, "good", "Bin sowas von topfit! (= " & prvEndurance & " + " & effEnduranceGain & ")")
!mmm       do setattr(tokenID, "status_green", false)
!mmm   
!mmm     end if
!mmm   
!mmm     do setattr(tokenID, enduranceAttr, newEndurance)
!mmm   
!mmm   end if
!mmm   
!mmm   return newEndurance
!mmm   
!mmm end function
!rem // 
!rem // m3mgdHealthBoost(healthGain, [tokenID], [healthAttr])
!rem // 
!rem // healthGain      Number of health points applied to character
!rem //   
!rem // Returns the number of health points effectively applied (differs from parameter if max HP are hit)
!rem //
!mmm function m3mgdHealthBoost(healthGain, tokenID, healthAttr)
!mmm
!mmm   set tokenID = m3mgdValidateOwnTokenID(tokenID)
!mmm   if not tokenID
!mmm     return false
!mmm   end if
!mmm   if healthAttr == default and not (isunknown(script.cOwnID.(script.cHealthAttr)) or isdenied(script.cOwnID.(script.cHealthAttr)))
!mmm     set healthAttr = script.cHealthAttr
!mmm   else
!mmm     do whisperback("Fatal call to m3mgdHealthBoost(): default health attribute invalid - " & script.cHealthAttr)
!mmm     return false
!mmm   end if
!mmm   
!rem   // Check if character is actually alive
!mmm
!mmm   set prvHealth = tokenID.(healthAttr)
!mmm   set maxHealth = tokenID.(healthAttr).max
!mmm
!mmm   if maxHealth == 0
!mmm
!mmm     do whisperback(tokenID.token_name & " ist kein Lebewesen (LP=0), dass geheilt werden kann.")
!mmm
!mmm   else
!mmm
!rem     // Process health gain and applicable consequences 
!mmm
!mmm     if prvHealth + healthGain > maxHealth
!mmm       set effHealthGain = highlight(maxHealth - prvHealth, "important", "begrenzt auf Maximum")
!mmm     else
!mmm       set effHealthGain = highlight(healthGain, "normal")
!mmm     end if 
!mmm   
!mmm     set newHealth = prvHealth + effHealthGain
!mmm   
!mmm     if newHealth == maxHealth
!mmm   
!mmm       set newHealth = highlight(newHealth, "good", "= " & prvHealth & " + " & effHealthGain)
!mmm   
!mmm     else if newHealth <= 0
!mmm   
!mmm       set newHealth = highlight(newHealth, "bad", "ðŸ’€ (fast) tot (= " & prvHealth & " + " & effHealthGain & ")")
!mmm       do setattr(tokenID, "status_death_zone", true)
!mmm       do setattr(tokenID, "status_red", false)
!mmm       do setattr(tokenID, "status_yellow", false)
!mmm   
!mmm     else if newHealth <= 3
!mmm   
!rem       // Health <= 3: Red token marker => no actions, movement reduced to 4, 10-minute countdown to shock
!mmm       set newHealth = highlight(newHealth, "bad", "ðŸ”´ sehr schwer verletzt (= " & prvHealth & " + " & effHealthGain & ")")
!mmm       do setattr(tokenID, "status_death_zone", false)
!mmm       do setattr(tokenID, "status_red", true)
!mmm       do setattr(tokenID, "status_yellow", false)
!mmm   
!mmm     else if newHealth <= .5 * maxHealth
!mmm   
!rem       // Health <= 50%: Yellow token marker => endurance and movement to be limited to 50%
!mmm       set newHealth = highlight(newHealth, "important", "ðŸŸ¡ schwer verletzt (= " & prvHealth & " + " & effHealthGain & ")")
!mmm       do setattr(tokenID, "status_death_zone", false)
!mmm       do setattr(tokenID, "status_red", false)
!mmm       do setattr(tokenID, "status_yellow", true)
!mmm   
!mmm     else if newHealth < maxHealth
!mmm   
!mmm       set newHealth = highlight(newHealth, "normal", "Nur ein paar Kratzer! (= " & prvHealth & " + " & effHealthGain & ")")
!mmm       do setattr(tokenID, "status_death_zone", false)
!mmm       do setattr(tokenID, "status_red", false)
!mmm       do setattr(tokenID, "status_yellow", false)
!mmm   
!mmm     else
!mmm   
!mmm       set newHealth = highlight(newHealth, "normal", "Kerngesund! (= " & prvHealth & " + " & effHealthGain & ")")
!mmm       do setattr(tokenID, "status_death_zone", false)
!mmm       do setattr(tokenID, "status_red", false)
!mmm       do setattr(tokenID, "status_yellow", false)
!mmm   
!mmm     end if
!mmm   
!mmm     do setattr(tokenID, healthAttr, newHealth)
!mmm   
!mmm   end if
!mmm   
!mmm   return newHealth
!mmm   
!mmm end function
!rem // 
!rem // m3mgdGetViewAngle(fromTokenID, toTokenID)
!rem //
!rem // Returns fromTokenID's angle of vision towards toTokenID, in degrees. 
!rem // The reverse (toTokenID's angle of vision towards fromTokenID) is the result minus 180.
!rem //
!mmm function m3mgdGetViewAngle(fromTokenID, toTokenID)
!mmm
!mmm   set offsetYfromFoe = round((fromTokenID.top  - toTokenID.top)  * distscale())
!mmm   set offsetXfromFoe = round((fromTokenID.left - toTokenID.left) * distscale())
!mmm   set ownDirectionFromFoe = round(atan(offsetYfromFoe, offsetXfromFoe)) - 90
!mmm   set foeDirectionFromMe = round(atan(-offsetYfromFoe, -offsetXfromFoe)) - 90
!mmm
!mmm   set myViewAngle = ((foeDirectionFromMe - round(fromTokenID.rotation) % 360) + 180) % 360 - 180
!mmm
!mmm   return myViewAngle
!mmm
!mmm end function
!rem // 
!rem // m3mgdGetDistance(fromTokenID, toTokenID)
!rem //
!rem // Returns distance between fromTokenID and toTokenID, in distunits(). 
!rem //
!mmm function m3mgdGetDistance(fromTokenID, toTokenID)
!mmm
!mmm   set absDistY = ((abs(fromTokenID.top  - toTokenID.top))  - .5 * fromTokenID.height - .5 * toTokenID.height) * distscale()
!mmm   set absDistX = ((abs(fromTokenID.left - toTokenID.left)) - .5 * fromTokenID.width  - .5 * toTokenID.width)  * distscale()
!mmm
!mmm   return round(max(absDistY, absDistX))
!mmm
!mmm end function
!rem // 
!rem // m3mgdShapeMoji(tokenID, [healthAttr], [enduranceAttr])
!rem //
!rem // Returns visual indicator of health and exhaustion or lack to access to health/endurance attributes.
!rem // If healthAttr/enduranceAttr are not provided, they default to script.cHealthAttr/cEnduranceAttr.
!rem //
!mmm function m3mgdShapeMoji(tokenID, healthAttr, enduranceAttr)
!mmm   set tokenID = m3mgdValidateOwnTokenID(tokenID)
!mmm   if not tokenID
!mmm     return false
!mmm   end if
!mmm   if healthAttr == default and script.cHealthAttr ne ""
!mmm     set healthAttr = script.cHealthAttr
!mmm   else
!mmm     do whisperback("No health attribute provided as argument or at script level")
!mmm     return false
!mmm   end if
!mmm   if enduranceAttr == default and script.cEnduranceAttr ne ""
!mmm     set enduranceAttr = script.cEnduranceAttr
!mmm   else
!mmm     do whisperback("No endurance attribute provided as argument or at script level")
!mmm     return false
!mmm   end if
!mmm
!mmm   if isdenied(tokenID.(healthAttr)) or isdenied(tokenID.(enduranceAttr))
!mmm     return highlight("ðŸ”’", "bad", tokenID.name & ".AP/LP: Zugriff auf mind. ein Attribut verweigert")
!mmm   end if 
!mmm
!mmm   if not tokenID.(healthAttr).max == 0 and tokenID.(healthAttr) <= 0
!mmm     return highlight("ðŸ’€", "bad", "Dem Tode nah... jetzt zÃ¤hlt jede Sekunde!")
!mmm   end if
!mmm
!mmm   set shapeMoji = ""
!mmm   if tokenID.(healthAttr).max != 0 and tokenID.(healthAttr) < .5 * tokenID.(healthAttr).max
!mmm     set shapeMoji = shapeMoji & "ðŸ¤•"
!mmm   end if 
!mmm   if tokenID.(enduranceAttr).max != 0 and tokenID.(enduranceAttr) <= 0
!mmm     set shapeMoji = shapeMoji & "ðŸ¥´"
!mmm   end if 
!mmm   if shapeMoji eq ""
!mmm     set shapeMoji = "ðŸ˜€"
!mmm   end if 
!mmm     
!mmm   return shapeMoji
!mmm     
!mmm end function