!rem // _libMidgardRuneBlades()
!rem // 
!rem //   Initializer / publisher function. Call once from autorun.
!rem //
!mmm function _libMidgardRuneBlades()
!mmm   
!mmm   set libVersion = "libMidgardRuneBlades v1.0.0-pre (2022-02-11)"
!mmm   set sender = "MacroSheetLibrary"
!mmm
!rem   // Game-global constants for publication
!rem
!mmm   set m3mgdFireMoleActiveMarker = "status_all_for_one"
!mmm   set m3mgdEarthBondStatusMarker = "status_cobweb"
!mmm   set m3mgdEarthBondPCAttrTargetID = "m3mgd_earthBond_targetID"
!mmm
!mmm   publish to game: m3mgdFireMoleActiveMarker, m3mgdEarthBondPCAttrTargetID, m3mgdEarthBondStatusMarker
!mmm
!rem   // Publish game-global functions defined below
!rem
!mmm   publish to game: m3mgdRuneEffectFireMole, m3mgdRuneEffectEarthBond, m3mgdCancelRuneEffects
!mmm
!mmm   do chat(libVersion & " loaded.")
!mmm   
!mmm end function
!rem
!rem // m3mgdRuneEffectFireMole(tokenID, weaponLabel, command)
!rem // 
!rem //   Executes command on tokenID's rune blade Firemole (registered in character sheet as weaponLabel).
!rem //   Known commands: "toggle-first", "start-first", "end-first", "cancel-first" (regarding the first rune effect)
!rem // 
!mmm function m3mgdRuneEffectFireMole(tokenID, weaponLabel, command)
!mmm 
!mmm   set weaponToggleCost = 1
!mmm   set activateGIF = "https://media.giphy.com/media/3og0IDCumL5GLx7zag/giphy.gif"
!mmm   set deactivateGIF = "https://media.giphy.com/media/hSvQn8tqv16bkzCGYI/giphy.gif"
!mmm
!mmm   if tokenID.character_name ne "Yorric MacRathgar"
!mmm     do whisperback("*Feuermal* ist an Yorric MacRathgar gebunden und reagiert nicht auf " & tokenID.name)
!mmm     return false
!mmm   end if
!mmm   set sender = tokenID.character_name
!mmm
!mmm   if tokenID.(script.cEnduranceAttr) <= 0
!mmm
!mmm     return false
!mmm     
!mmm   else if (command eq "toggle-first" or command eq "start-first") and not tokenID.(m3mgdFireMoleActiveMarker) eq "shown"
!mmm     
!rem     // First rune effect, command "toggle"/"start", state "not active": Spark magic flames around the blade
!mmm     chat: Ich konzentriere mich für 10 Sekunden, und es beginnen Flammen um meine Runenklinge *Feuermal* zu züngeln. [x](${activateGIF})
!mmm     
!mmm     if not setattr(tokenID, m3mgdFireMoleActiveMarker, true) eq "shown"
!mmm       do whisperback("m3mgdRuneEffectFireMole(" & command & "): Failure setting status marker '" & m3mgdFireMoleActiveMarker & "' to shown.")
!mmm       return false
!mmm     end if
!mmm     
!mmm     set endurance = m3mgdModifyEndurance(-1 * weaponToggleCost, tokenID, script.(cEnduranceAttr))
!mmm     chat: /w "${tokenID.character_name}" Das hat ${weaponToggleCost} AP gekostet.
!mmm     chat: /w GM ${tokenID.character_name} hat die magischen Flammen seiner Runenklinge aktiviert. **Dauer: 10s.** Kosten: ${weaponToggleCost} AP (schon abgezogen). 
!mmm     
!mmm   else if (command eq "toggle-first" or command eq "end-first") and tokenID.(m3mgdFireMoleActiveMarker) eq "shown"
!mmm     
!rem     // First rune effect, command "toggle"/"end", state "active": Douse magic flames around the blade
!mmm     chat: Ich konzentriere mich für 10 Sekunden, und die Flammen um meine Runenklinge *Feuermal* erlöschen. [x](${deactivateGIF})
!mmm     
!mmm     if not setattr(tokenID, m3mgdFireMoleActiveMarker, false) eq "false"
!mmm       do whisperback("m3mgdRuneEffectFireMole(" & command & "): Failure setting status marker '" & m3mgdFireMoleActiveMarker & "' to false.")
!mmm       return false
!mmm     end if
!mmm     
!mmm     set endurance = m3mgdModifyEndurance(-1 * weaponToggleCost, tokenID, script.(cEnduranceAttr))
!mmm     chat: /w "${tokenID.character_name}" Das hat ${weaponToggleCost} AP gekostet.
!mmm     chat: /w GM ${tokenID.character_name} hat die magischen Flammen seiner Runenklinge gelöscht. **Dauer: 10s.** Kosten: ${weaponToggleCost} AP (schon abgezogen).
!mmm   
!mmm   else if (command eq "cancel-first") and tokenID.(m3mgdFireMoleActiveMarker) eq "shown"
!mmm     
!rem     // First rune effect, command "cancel", state "active": Abruptly douse magic flames around the blade, no endurance cost
!mmm     chat: Plötzlich erlöschen die Flammen um meine Runenklinge *Feuermal*. [x](${deactivateGIF})
!mmm     
!mmm     if not setattr(tokenID, m3mgdFireMoleActiveMarker, false) eq "false"
!mmm       do whisperback("m3mgdRuneEffectFireMole(" & command & "): Failure setting status marker '" & m3mgdFireMoleActiveMarker & "' to false.")
!mmm       return false
!mmm     end if
!mmm     
!mmm     chat: /w "${tokenID.character_name}" Du bist erschöpft, die magischen Flammen sind von selbst erloschen.
!mmm     chat: /w GM ${tokenID.character_name} ist erschöpft, die magischen Flammen seiner Runenklinge sind sofort erloschen. Keine AP-Kosten.
!mmm   
!mmm   end if
!mmm   
!mmm   return true
!mmm   
!mmm end function
!rem
!rem // m3mgdRuneEffectEarthBond(weaponWielderID, weaponLabel, command, [targetID])
!rem // 
!rem //   CURRENTLY ONLY ALLOWS FOR RELEASE COMMANDS
!rem //   Executes command on weaponWielderID's earth bond capability (from the rune blade weaponLabel) against the stored target or [targetID]
!rem // 
!mmm function m3mgdRuneEffectEarthBond(weaponWielderID, weaponLabel, command, targetID)
!mmm   
!mmm   set earthBondToggleCost = 1
!mmm   set deactivateGIF = ""
!mmm
!mmm   set sender = weaponWielderID.character_name
!mmm
!mmm   if isdefault(targetID)
!mmm     
!rem     // Retrieve target ID
!mmm     set targetID = weaponWielderID.(m3mgdEarthBondPCAttrTargetID)
!mmm     if isunknown(targetID) or isdenied(targetID) or isdenied(targetID.token_id) or isunknown(targetID.token_id) or isdenied(targetID.(m3mgdEarthBondStatusMarker))
!mmm       do whisperback("Target token '" & targetID & "' not a token ID, or does not allow access to attributes:" & getreason(targetID))
!mmm       return false
!mmm     end if
!mmm 
!mmm     if targetID.(m3mgdEarthBondStatusMarker) ne "shown"
!mmm       do whisperback(targetID.name & " trägt aber keine Erdfessel oder '" & m3mgdEarthBondStatusMarker & "' wurde schon entfernt.")
!mmm       return false
!mmm     end if
!mmm
!mmm   end if
!mmm
!mmm   if command eq "releaseByCreator" or command eq "releaseByExhaustion"
!mmm
!mmm     if command eq "releaseByCreator"
!mmm       do m3mgdModifyEndurance(-1 * earthBondToggleCost, weaponWielderID.token_id, "AP")
!mmm     end if
!mmm     combine chat
!mmm       chat: Die Erdfessel gibt ${targetID.name} frei, und plötzlich sieht der Untergrund wieder aus wie vorher, als wäre nichts gewesen.
!mmm       if deactivateGIF ne ""
!mmm         chat: [x](${deactivateGIF})
!mmm       end if
!mmm     end combine
!mmm
!mmm   else if command eq "escapeByPatience"
!mmm     
!mmm     set sender = targetID.token_name
!mmm     set dxRoll = roll("1d100")
!mmm     
!mmm     if dxRoll > targetID.Gw
!mmm     
!mmm       chat: /w GM **Lösen aus der Erdfessel:** PW:Gw(${targetID.Gw}) ist mit ${dxRoll} **fehlgeschlagen.**
!mmm       chat: ${targetID.name} versucht sich allzu ruckartig von ${weaponWielderID.name}s Erdfessel zu befreien und verstrickt sich damit nur weiter. So ein Mist, verdammter! Und dafür hab ich 5 Minuten verschwendet.
!mmm       return false
!mmm     
!mmm     else
!mmm     
!mmm       combine chat
!mmm         chat: /w GM **Lösen aus der Erdfessel:** PW:Gw(${targetID.Gw}) ist mit ${dxRoll} **gelungen.**
!mmm         chat: ${targetID.name} befreit sich laaaaangsam, aber immerhin elegant von ${weaponWielderID.name}s Erdfessel. Es kann so einfach sein -- das waren 5 gut investierte Minuten!
!mmm         if deactivateGIF ne ""
!mmm           chat: [x](${deactivateGIF})
!mmm         end if
!mmm       end combine
!mmm     
!mmm     end if
!mmm
!mmm   else if command eq "escapeByForce"
!mmm
!mmm     set sender = targetID.token_name
!mmm     set stRoll = roll("1d100")
!mmm     set foeActOfStrength = roll(targetID.token_id, "@"&"{KraftaktW}")
!mmm     
!mmm     if stRoll > foeActOfStrength
!mmm     
!mmm       chat: /w GM **Ausbruch aus Erdfessel:** PW:KraftaktW(${foeActOfStrength}) ist mit ${stRoll} **fehlgeschlagen.**
!mmm       chat: ${targetID.name} versucht sich mit einem schier übermensch*lichen Kraftakt von ${weaponWielderID.name}s Erdfessel zu befreien und verstrickt sich damit nur weiter. So ein Mist, verdammter!
!mmm       return false
!mmm     
!mmm     else
!mmm     
!mmm       combine chat
!mmm         chat: /w GM **Ausbruch aus Erdfessel:** PW:KraftaktW(${foeActOfStrength}) ist mit ${stRoll} **gelungen.**
!mmm         chat: ${targetID.name} bricht mit einem übermensch*lichen Kraftakt aus ${weaponWielderID.name}s Erdfessel aus. Muckis, Baby!
!mmm         if deactivateGIF ne "" 
!mmm           chat: [x](${deactivateGIF})
!mmm         end if
!mmm       end combine
!mmm     
!mmm     end if
!mmm
!mmm   end if
!mmm
!mmm   do setattr(targetID, m3mgdEarthBondStatusMarker, false)
!mmm   do setattr(weaponWielderID, m3mgdEarthBondStatusMarker, false)
!mmm   do setattr(weaponWielderID, m3mgdEarthBondPCAttrTargetID, "")
!mmm
!mmm   return true
!mmm
!mmm end function
!rem
!rem // m3mgdCancelRuneEffects(weaponWielderID)
!rem // 
!rem //   Stops any active rune effects originating with weaponWielderID
!rem // 
!mmm function m3mgdCancelRuneEffects(weaponWielderID)
!mmm   
!mmm   if isdefault(weaponWielderID)
!mmm     return false
!mmm   end if
!mmm   
!mmm   if weaponWielderID.(m3mgdFireMoleActiveMarker)
!mmm   
!mmm     if m3mgdRuneEffectFireMole(weaponWielderID, default, "cancel-first")
!mmm   
!mmm       return true
!mmm   
!mmm     else
!mmm   
!mmm       do whisperback("cancelRuneEffects(): call to m3mgdRuneEffectFireMole(cancel-first) failed.")
!mmm       return false
!mmm   
!mmm     end if
!mmm   
!mmm   else if weaponWielderID.(m3mgdEarthBondStatusMarker)
!mmm   
!mmm     if m3mgdRuneEffectEarthBond(weaponWielderID, default, "releaseByExhaustion")
!mmm   
!mmm       return true
!mmm   
!mmm     else
!mmm   
!mmm       do whisperback("cancelRuneEffects(): call to runeEffectEarthBond(releaseByExhaustion) failed.")
!mmm       return false
!mmm   
!mmm     end if
!mmm   
!mmm   end if
!mmm   
!mmm   return true
!mmm
!mmm end function