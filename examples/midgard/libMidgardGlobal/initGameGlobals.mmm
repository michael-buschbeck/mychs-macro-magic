!rem // initGameGlobals
!rem // 
!rem //   defines game-global variables and loads global functions for use across Midgard scripts
!rem // 
!rem // Sets ("exports") variables:
!rem //   m3mgdExchange                 Exchange data sheet ID. Use to access any attribute: m3mgdExchange.(m3mgdAttrAttackType)
!rem //   m3mgdExchangeDataSheet        Sheet name to look up the exchange data sheet, for portability between games
!rem //   m3mgdExchangeAttrList         List of valid attribute labels (contained in the list of variables below)
!rem //   m3mgdAttrAttackType           Attribute label for the attack type ("melee", "ranged", "magic"), with spell names stored in "max"
!rem //   m3mgdAttrAttackerID           Attribute label for the attacker's token ID
!rem //   m3mgdAttrAttackTargetID       Attribute label for the target's token ID
!rem //   m3mgdAttrAttackResult         Attribute label for the attack result (integer)
!rem //   m3mgdAttrAttackDamage         Attribute label for the attack damage (integer)
!rem //   m3mgdAttrAttackDamageRoll     Attribute label for the attack damage roll result (integer)
!rem //   m3mgdAttrAttackWeaponType     Attribute label for the attack weapon type (string)
!rem //   m3mgdAttrHealthGain           Attribute label for the number of health points gained (e.g. through First Aid or a healing spell)
!rem //   m3mgdAttrEnduranceGain        Attribute label for the number of endurance points gained (e.g. through an energy drink or spell)
!rem // 
!mmm script
!mmm   set scriptVersion = "initGameGlobals 1.2.2 (2022-04-14)"
!mmm   set sender = "MacroSheetLibrary"
!mmm
!mmm   set customizable cCheckVersion = false
!mmm   if cCheckVersion
!mmm     do whisperback(scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!rem   // MMM compatibility check: die if MMM version too low
!mmm   set mmmVersionReq = 1.26
!mmm   if version < mmmVersionReq
!mmm     do whisperback(scriptVersion & " - Abbruch: MMM-Version " & mmmVersionReq & ".0 oder höher erforderlich.")
!mmm     exit script
!mmm   end if
!mmm
!mmm   chat: ${"<"}br>
!mmm
%{MacroSheetLibrary|libBasics}
!mmm   do _libBasics()
!mmm   
%{MacroSheetLibrary|libMidgard}
!mmm   do _libMidgard()
!mmm   
%{MacroSheetLibrary|libMidgardRuneBlades}
!mmm   do _libMidgardRuneBlades()
!mmm   
!mmm   combine chat
!mmm     set m3mgdPlayerCharacters = "Yorric MacRathgar", "Finn MacRathgar", "Ufix Steinbrot", "Wulfric MacConuilh", "Baigh MacBeorn", "Titos Panathos"
!mmm     for charName in m3mgdPlayerCharacters
!mmm       if not isdenied(charName.character_id) and (isunknown(charName.PC) or charName.PC ne "1")
!mmm         if setattr(charName.character_id, "PC", 1) == 1
!mmm           do chat("MacroSheetLibrary", "Registered player status for " & charName)
!mmm         else
!mmm           do chat("MacroSheetLibrary", "ERROR: No access or write error to character sheet '" & charName & "' which misses a player flag. Aborting.")
!mmm           exit script
!mmm         end if 
!mmm       end if
!mmm       if charName.PC eq "1"
!mmm         chat: ${"<"}br>**${charName}**.PC: ✅
!mmm       else 
!mmm         chat: ${"<"}br>**${charName}**.PC: ❌
!mmm       end if
!mmm     end for
!mmm   end combine
!mmm   publish to game: m3mgdPlayerCharacters
!mmm   
!mmm   set cssTableHeaderButton = "\" style=\"border:1px solid #FFFFFF;float:right; background-color:#ce0f69;margin:0px 0px 0px 8px;padding:3px;font-style:normal;font-size:12px;line-height:12px;vertical-align:sub; color:#FFFFFF;text-decoration:none;text-transform:uppercase"
!mmm   set cssTableCellButton = "\" style=\"border:1px solid #FFFFFF;background-color:#ce0f69;padding:1px;font-style:normal;font-size:1em;color:#FFFFFF;text-decoration:none;text-transform:uppercase"
!mmm   publish to game: cssTableHeaderButton, cssTableCellButton
!mmm
!mmm   function sudo(cmd)
!mmm     do whisperback("sudo: executing (privileged=" & privileged & ") '" & cmd & "' as '!mmm " & cmd & "'")
!mmm     chat: !mmm ${cmd}
!mmm   end function
!mmm   publish to game: sudo
!mmm   
!mmm   chat: /me loaded: ${scriptVersion}
!mmm   
!mmm end script