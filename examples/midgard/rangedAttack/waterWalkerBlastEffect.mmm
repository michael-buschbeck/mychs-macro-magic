!rem // waterWalkerBlastEffect
!rem // v1.0.2 2021-12-05
!rem
!mmm script
!mmm   set scriptVersion = "1.0.1 (2021-11-17)"
!rem
!rem   // MMM compatibility check: die if MMM version too low
!mmm   if version < 1.20
!mmm     do whisperback("Abbruch: MMM-Version 1.20.0 oder höher erforderlich.")
!mmm     exit script
!mmm   end if
!mmm
!rem   // Config (if script is used by more than one player, use config scripts to overwrite, otherwise just customize here)
!rem   //  cWeaponLabel         The label of the weapon, must be identical to your combat sheet
!mmm   set customizable cCheckVersion = false
!mmm   set customizable cWeaponWielder = "Yerrick MacRothgar"
!mmm   set customizable cWeaponLabel = "Wasserläufer (Langschwert)"
!mmm
!mmm   if cCheckVersion
!mmm     do whisperback("waterWalkerBlastEffect v" & scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!rem   // Initialize constants
!mmm   set m3mgdAttrBlastBearing = "m3mgd_waterWalkerBlastEffect_bearing"
!mmm   set m3mgdAttrTargetID = "m3mgd_waterWalkerBlastEffect_targetID"
!mmm 
!mmm   if not cWeaponWielder.name eq cWeaponWielder
!mmm     do whisperback("Character " & cWeaponWielder & " not accessible or something.")
!mmm     exit script
!mmm   end if
!mmm
!rem   // Identify target (user points to token)
!mmm   set foe = cWeaponWielder.(m3mgdAttrTargetID)
!mmm   if not foe or foe.permission ne "control"
!mmm     do whisperback("Target given as token ID '" & foe & "' may not be a correct token ID, or does not allow control of attributes")
!mmm     exit script
!mmm   end if
!rem
!rem   // Dazzle players with special effects
!mmm   do spawnfx("beam-water", cWeaponWielder.left, cWeaponWielder.top, foe.left, foe.top)
!mmm
!mmm   set stRoll = roll("1d100")
!mmm   if stRoll <= 0.5 * foe.St
!mmm     chat: Der Wasserstrahl mag gezwickt haben, aber ${foe.name} verzieht ansonsten keine Wimper: PW:St(${foe.St}) = ${stRoll}
!mmm   else if stRoll <= 0.8 * foe.St
!mmm     chat: ${foe.name} zuckt überrascht, als der Wasserstrahl trifft, aber wirklich aus der Ruhe bringt ihn/sie der nicht: PW:St(${foe.St}) = ${stRoll}
!mmm   else if stRoll <= foe.St
!mmm     chat: ${foe.name} spannt gerade noch rechtzeitig den Körper an, stellt sich dem Strahl entgegen und schafft es stehen zu bleiben: PW:St(${foe.St}) = ${stRoll}
!mmm   else
!mmm     set blastDistance = roll("1d6-1")
!mmm     if blastDistance == 0
!mmm       chat: Der harte Strahl lässt ${foe.name} auf der Stelle stolpern und stürzen: PW:St(${foe.St}) = ${stRoll} / Strahlstärke: ${blastDistance}
!mmm     else 
!mmm       set blastBearing = cWeaponWielder.(m3mgdAttrBlastBearing)
!mmm       do setattr(foe, "left", foe.left - (sin(blastBearing) * blastDistance / distscale()))
!mmm       do setattr(foe, "top", foe.top + (cos(blastBearing) * blastDistance / distscale()))
!mmm       chat: Der harte Wasserstrahl spült ${foe.name} ${blastDistance} Meter nach hinten und auf den Hosenboden: PW:St(${foe.St}) = ${stRoll} / Strahlstärke: ${blastDistance}
!mmm     end if
!mmm   end if
!mmm   
!mmm   do setattr(cWeaponWielder, m3mgdAttrTargetID, "")
!mmm   do setattr(cWeaponWielder, m3mgdAttrBlastBearing, "")
!mmm end script