!rem // Midgard Ranged Attack Script
!rem //
!mmm script
!mmm   set scriptVersion = "1.15.0-pre (2022-02-06)"
!mmm
!mmm   set customizable cCheckVersion = false
!mmm   if cCheckVersion
!mmm     do whisperback("Midgard Ranged Attack Script " & scriptVersion)
!mmm     exit script
!mmm   end if
!mmm
!rem   // MMM compatibility check: die if MMM version too low
!mmm   if version < 1.26
!mmm     do whisperback("Abbruch: MMM-Version 1.26.0 oder hÃ¶her erforderlich.")
!mmm     exit script
!mmm   end if
!mmm
!mmm   if not m3mgdExchange
!mmm     do whisperback("Game-global m3mgdExchange variables missing.")
!mmm     exit script
!mmm   end if
!rem
!rem   // Config: set defaults for customizable variables
!rem   //
!rem   //  cVerbose             Toggle narrative output: true for on, false for off (made for oeGStefan)
!rem   //  cWeaponLabel         The label of the weapon, must be identical to your combat sheet
!rem   //  cWeaponName          Optional: The name of a named weapon, e.g. "Sting"
!rem   //  cWeaponType          The type of weapon for narration, the "sword" in "I'm swinging my sword Glamdring"
!rem   //  cRangeUpperBound*    Upper bounds of close, mid and far ranges
!rem   //  cAmmoLabel           Ammunition descriptor (plural, for attribute and output)
!rem   //  cAmmoWarnThreshold   Proportion of maximum ammunition at which remaining ammo is highlighted
!rem   //  cAmmoMagic           Default: false, customize as true to enable extra damage from magic ammunition
!rem   //  cAmmoMagicLabel      Magic ammunition descriptor (plural, for attribute and output)
!rem   //  cAmmoMagicSkillBonus Optional modifier ("+x") for the attack roll if magic ammunition is used
!rem   //  cAmmoMagicDamage     Damage roll expression ("1d6+x") for optional additional magic damage
!rem   //  cFoeNameDefault      Output label for targets without names
!rem   //
!mmm   set customizable cVerbose = true
!mmm   set customizable cWeaponLabel = default
!mmm   set customizable cWeaponName = ""
!mmm   set customizable cWeaponType = default
!mmm   set customizable cRangeUpperBoundClose = default
!mmm   set customizable cRangeUpperBoundMid = default
!mmm   set customizable cRangeUpperBoundFar = default
!mmm   set customizable cWeaponMagicSkillBonus = ""
!mmm   set customizable cAmmoLabel = default
!mmm   set customizable cAmmoWarnThreshold = 0.25
!mmm   set customizable cAmmoMagic = false
!mmm   set customizable cAmmoMagicLabel = ""
!mmm   set customizable cAmmoMagicSkillBonus = 0
!mmm   set customizable cAmmoMagicDamage = ""
!mmm   set customizable cMagicEnduranceCost = 0
!mmm   set customizable cFoeNameDefault = "Fiesling ohne Namen"
!rem
!rem   //  cOwnID               Override if the script should work from a sender not identical with the character
!rem   //  cHealthAttr          Override if the script operates on a token for a generic NPC
!rem   //  cEnduranceAttr       Override if the script operates on a token for a generic NPC
!rem   //  cGMSilentMode        true/false: override if GM operates this script and wants to keep it silent
!rem   //  cAttackRoll          Override by player to see beautifully animated 3D dice
!mmm   set customizable cOwnID = sender.token_id
!mmm   set customizable cHealthAttr = "bar3"
!mmm   set customizable cEnduranceAttr = "bar2"
!mmm   set customizable cGMSilentMode = false
!mmm   set customizable cAttackRoll = roll("1d20")
!mmm
!rem   // Refetch token_id to ensure access in case of erroneous override in customize block
!mmm   set cOwnID = cOwnID.token_id
!mmm   if isdenied(cOwnID)
!mmm     do whisperback("Abbruch: Keine Zuordnung zu einem zugreifbaren Charakter mÃ¶glich - " & getreason(cOwnID)) 
!mmm     exit script
!mmm   end if
!rem
!rem   // Override chat sender for characters unconnected to the player (usually NPCs)
!mmm   if cOwnID.token_name ne sender
!mmm     set sender = cOwnID.token_name
!mmm   end if
!rem
!rem   // Init
!rem
!rem   // - Constants
!rem
!mmm   set emojiLock = "ðŸ”’"
!mmm   set emojiMagic = "âœ¨"
!mmm   set emojiKnife = "ðŸ”ª"
!mmm
!mmm   set whirlWindMagicMarker = "status_half_haze"
!mmm
!rem   // - Validate attack weapon and request manual entry if selection is missing
!mmm
!mmm   if cWeaponLabel eq "" or not cOwnID.(findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeEW"))
!mmm     set weaponsList = m3mgdListRangedAttackWeapons(cOwnID)
!mmm     if not weaponsList
!mmm       do whisperback(cOwnID.name & " hat keine Fernkampfwaffen bzw. -fÃ¤higkeiten.")
!mmm       exit script
!mmm     else if count(weaponsList) == 1
!mmm       set cWeaponLabel = cOwnID.(weaponsList)
!mmm     else if cWeaponLabel ne ""
!mmm       do whisperback("Fehler: " & cOwnID.name & " hat keine Fernkampfwaffe " & cWeaponLabel & ".")
!mmm       exit script
!mmm     else 
!mmm       do m3mgdWeaponSelectorChatMenu(cOwnID, "ranged")
!mmm       exit script
!mmm     end if
!mmm   end if
!mmm
!rem   // - Pull missing weapon properties for chosen attack weapon (works only for standard weapons)
!mmm
!mmm   set weaponProperties = m3mgdRangedWeaponProperties(cWeaponLabel)
!mmm   if not isunknown(weaponProperties)
!mmm     if isdefault(cWeaponType)
!mmm       set cWeaponType = weaponProperties[0]
!mmm     end if
!mmm     if isdefault(cRangeUpperBoundClose)
!mmm       set cRangeUpperBoundClose = weaponProperties[1]
!mmm     end if
!mmm     if isdefault(cRangeUpperBoundMid)
!mmm       set cRangeUpperBoundMid = weaponProperties[2]
!mmm     end if
!mmm     if isdefault(cRangeUpperBoundFar)
!mmm       set cRangeUpperBoundFar = weaponProperties[3]
!mmm     end if
!mmm     if isdefault(cAmmoLabel)
!mmm       set cAmmoLabel = weaponProperties[4]
!mmm     end if
!mmm   end if
!mmm
!rem   // - Fetch character (attacker) data
!rem
!mmm   set effAttack = cOwnID.(findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeEW"))
!mmm   set weaponDamageExpr = cOwnID.(findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeSchaden"))
!mmm   set weaponDamageBonus = cOwnID.(findattr(cOwnID, "Angriff", "Waffe", cWeaponLabel, "WaffeEfSchB"))
!mmm   if weaponDamageBonus >= 0
!mmm     set weaponDamageBonus = "+" & weaponDamageBonus
!mmm   end if
!mmm   set sharpshootingSkill = cOwnID.(findattr(cOwnID, "Fertigkeiten", "Fertigkeit", "ScharfschieÃŸen", "FW"))
!mmm   if (not effAttack) or (not weaponDamageExpr)
!rem     // Most probably, a weapon called cWeaponLabel does not exist, so the script cannot work
!mmm     do whisperback("Abbruch: " & cOwnID.name & " hat keine Waffe namens " & cWeaponLabel & ".")
!mmm     exit script
!mmm   else if cMagicEnduranceCost > 0 and cOwnID.(cEnduranceAttr) <= cMagicEnduranceCost
!mmm     do whisperback("Abbruch: " & cOwnID.name & " ist zu erschÃ¶pft fÃ¼r diesen magischen Angriff.")
!mmm     exit script
!mmm   end if
!rem
!rem   // - Fetch NPC (target) data
!rem
!mmm   set foeID = "@{target|Angriffsziel|token_id}"
!mmm   set foeName = foeID.name
!mmm   if isdenied(foeName) or isunknown(foeName)
!mmm     set foeName = cFoeNameDefault
!mmm   end if
!mmm   set foeEndurance = foeID.bar2
!mmm
!rem   // - Fetch ammo properties
!mmm
!mmm   if count(cAmmoLabel) == 2 and cAmmoLabel[1] eq "âˆž"
!mmm     set ammoCount = "âˆž"
!mmm     set ammoLabel = cAmmoLabel[0]
!mmm   else if count(cAmmoLabel) == 1 and not isunknown(cOwnID.(cAmmoLabel))
!mmm     set ammoCount = cOwnID.(cAmmoLabel)
!mmm     set ammoLabel = cAmmoLabel
!mmm   else
!mmm     do whisperback("Abbruch: Munitionsattribut '" & cAmmoLabel & "' falsch definiert oder fehlt.")
!mmm     exit script
!mmm   end if
!mmm   if cAmmoMagic
!mmm     set magicAmmoCount = cOwnID.(cAmmoMagicLabel)
!mmm     if isunknown(magicAmmoCount)
!mmm       do whisperback("Abbruch: " & cOwnID.character_name & " fehlt ein Attribut fÃ¼r die magische Munition " & cAmmoMagicLabel & ".")
!mmm       exit script
!mmm     end if
!mmm     set ammoCount = magicAmmoCount
!mmm     set ammoLabel = cAmmoMagicLabel
!mmm   end if
!mmm   if ammoCount ne "âˆž" and ammoCount <= 0
!mmm     chat [OutOfAmmo]: Ich greife nach meinen ${ammoLabel}n, um auf ${foeName} zu schieÃŸen, doch **OH SCHRECK -- keine ${ammoLabel} mehr drin!** Schade. Da hat ${foeName} nochmal GlÃ¼ck gehabt.
!mmm     exit script
!mmm   end if
!mmm
!rem   // Calculate direction, exit if direction to target is outside arc of view 
!mmm   set myViewAngle = m3mgdGetViewAngle(cOwnID, foeID)
!mmm   if (myViewAngle < -45) or (myViewAngle > 45)
!mmm     chat [OutsideFieldOfVision]: $[targetName]{foeName} ist auÃŸerhalb meines Blickfeldes. Schade.
!mmm     exit script
!mmm   end if
!mmm   set foeViewAngle = m3mgdGetViewAngle(foeID, cOwnID)
!mmm
!rem   // Deduce ammo spent, if not magically unlimited
!mmm   if ammoCount ne "âˆž"
!mmm     set ammoCount = ammoCount - 1
!mmm     if not setattr(cOwnID, ammoLabel, ammoCount) == ammoCount
!mmm       do whisperback("Problem beim Speichern des neuen Munitionsvorrats in '" & cOwnID.name & "'." & ammoLabel)
!mmm     end if
!mmm     set ammoCount = highlight(getattr(cOwnID, ammoLabel), "info")
!mmm     set ammoMax = getattrmax(cOwnID, ammoLabel)
!mmm     if ammoCount < 1
!mmm       set ammoCount = highlight(ammoCount, "bad")
!mmm     else if ammoCount <= round(cAmmoWarnThreshold * ammoMax)
!mmm       set ammoCount = highlight(ammoCount, "important", "Nur noch " & ammoCount & "/" & ammoMax & " Ã¼brig")
!mmm     end if
!mmm   end if
!rem
!rem   // Calculate distance, exit if distance exceeds range
!mmm   set distance = m3mgdGetDistance(cOwnID, foeID)
!mmm   set distanceUnits = distunits() 
!mmm   if distance > cRangeUpperBoundFar
!mmm     combine chat
!mmm       chat [OutsideStrikeDistance]: $[targetName]{foeName} ist auÃŸerhalb der Reichweite ($[weaponLabel]{cWeaponLabel}: $[maxRange]{cRangeUpperBoundFar}). Schade.
!mmm       chat: Mein ${cAmmoLabel} fliegt grob in Richtung ${foeName}, der aber viel zu weit weg ist.
!mmm       chat: ${"&"}{template:default} {{name=${cWeaponLabel}: AuÃŸer Reichweite}} 
!mmm       chat: {{Ziel=**${foeName}**}}
!mmm       chat: {{Entfernung=${distance} ${distanceUnits} 
!mmm       chat: (${round(distance/(distsnap()*distscale()))} Planquadrate, **auÃŸerhalb der Reichweite: >${cRangeUpperBoundFar}m**)}}
!mmm       if ammoCount ne "âˆž"
!mmm         chat: {{${cAmmoLabel}=${ammoCount}}}
!mmm       end if
!mmm     end combine
!mmm     exit script
!mmm   end if
!rem
!rem   // Collect attack modifiers
!mmm   set modifiers = 0
!mmm   set autoModifiers = 0
!mmm   set modifierLog = ""
!mmm   set modifierTooltip = ""
!rem   // If we have a magic weapon with a skill bonus, add that
!mmm   if cWeaponMagicSkillBonus
!mmm     set autoModifiers = autoModifiers + cWeaponMagicSkillBonus
!mmm     if cWeaponMagicSkillBonus > 0
!mmm       set cWeaponMagicSkillBonus = "+" & abs(cWeaponMagicSkillBonus)
!mmm     end if 
!mmm     set modifierLog = modifierLog & " {{" & cWeaponLabel & "=EW" & cWeaponMagicSkillBonus & "}} "
!mmm     set modifierTooltip = modifierTooltip & " " & cWeaponLabel & "&nbsp;&nbsp;&nbsp;EW" & cWeaponMagicSkillBonus & "<br/> "
!mmm   end if
!rem   // If we have magic ammunition with a skill bonus, add that
!mmm   if cAmmoMagicSkillBonus
!mmm     set autoModifiers = autoModifiers + cAmmoMagicSkillBonus
!mmm     if cAmmoMagicSkillBonus > 0
!mmm       set cAmmoMagicSkillBonus = "+" & cAmmoMagicSkillBonus
!mmm     end if 
!mmm     set modifierLog = modifierLog & " {{" & cAmmoMagicLabel & "=" & cAmmoMagicSkillBonus & "}} "
!mmm     set modifierTooltip = modifierTooltip & " " & cAmmoMagicLabel & "&nbsp;&nbsp;&nbsp;" & cAmmoMagicSkillBonus & "<br/> "
!mmm   end if
!rem   // 1. Distance
!mmm   if distance > cRangeUpperBoundMid
!mmm     set rangeBracket = "Fernbereich: " & (cRangeUpperBoundMid+1) & "-" & cRangeUpperBoundFar & "m"
!mmm     set rangeDescription = "groÃŸer Entfernung"
!mmm     set autoModifiers = autoModifiers - 4
!mmm     set modifierLog = modifierLog & " {{" & rangeBracket & "=-4}}"
!mmm     set modifierTooltip = modifierTooltip & " " & rangeBracket & "&nbsp;&nbsp;&nbsp;-4 "
!mmm   else if distance > cRangeUpperBoundClose
!mmm     set rangeBracket = "Mittelbereich: " & (cRangeUpperBoundClose+1) & "-" & cRangeUpperBoundMid & "m"
!mmm     set rangeDescription = "mittlerer Entfernung"
!mmm     set autoModifiers = autoModifiers - 2
!mmm     set modifierLog = modifierLog & " {{" & rangeBracket & "=-2}}"
!mmm     set modifierTooltip = modifierTooltip & " " & rangeBracket & "&nbsp;&nbsp;&nbsp;-2 "
!mmm   else
!mmm     set rangeBracket = "Nahbereich: 0-" & cRangeUpperBoundClose & "m"
!mmm     set rangeDescription = "geringer Entfernung"
!mmm     set modifierLog = modifierLog & " {{" & rangeBracket & "=+/-0}}"
!mmm     set modifierTooltip = modifierTooltip & " " & rangeBracket & "&nbsp;&nbsp;&nbsp;+/-0 "
!mmm   end if
!rem   // 2. Attacker exhausted
!mmm   if cOwnID.(cEnduranceAttr).max > 0 and cOwnID.(cEnduranceAttr) <= 0
!mmm     set autoModifiers = autoModifiers - 4
!mmm     set modifierLog = modifierLog & " {{Angreifer erschÃ¶pft ([" & cOwnID.name & "].AP:0)=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Angreifer&nbsp;erschÃ¶pft&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!rem   // 3. Target exhausted
!mmm   if isdenied(foeEndurance) 
!mmm     set modifierLog = modifierLog & " {{[" & foeName & "].AP&nbsp;" & emojiLock & "=**ggf. EW+2**}}"
!mmm     set modifierTooltip = modifierTooltip & " Ziel.AP&nbsp;" & emojiLock & "=ggf. EW+2 "
!mmm   else if foeID.bar2.max > 0 and foeEndurance <= 0
!mmm     set autoModifiers = autoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{Ziel erschÃ¶pft=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Ziel&nbsp;erschÃ¶pft&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!rem   // 4. Finn's magic sword Whirlwind
!mmm   if (foeName eq "Finn" or foeName eq "Finn MacRathgar") and foeID.(whirlWindMagicMarker) eq "shown"
!mmm     set autoModifiers = autoModifiers - 4
!mmm     set modifierLog = modifierLog & " {{Wirbelwinds Wirbel-Winde=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Wirbelwinde&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!rem   // 5. Collect additional standard modifiers from user via dropdown menu
!mmm   set semiAutoModCode = "?{Weitere Angriffsmodifikatoren| Normaler Angriff +/-0, ?{Normaler Angriff +/- ...&#124;  NORMAL HALT +/-0  &#44; 1 &#124;  Mini-Ziel (1/4xMensch) -4 &#44; 2 &#124;  Kleines Ziel/halb gedeckt (1/2xMensch) -2 &#44; 3 &#124;  GroÃŸes Ziel (2xMensch) +2 &#44; 5 &#124;  Riesiges Ziel (4xMensch) +4 &#44; 7 &#124;  Angriff ins Handgemenge (Zufallsopfer) +4 &#44; 17  &#125; | Spontaner Angriff -4, ?{Spontaner Angriff -4 +/- ...&#124;  SONST NIX +/-0  &#44; 11  &#124;  Mini-Ziel (1/4xMensch) -4 &#44; 22  &#124;  Kleines Ziel/halb gedeckt (1/2xMensch) -2 &#44; 33  &#124;  GroÃŸes Ziel (2xMensch) +2 &#44; 55  &#124;  Riesiges Ziel (4xMensch) +4 &#44; 77  &#124;  Schnelles Ziel (30m+/Runde) -2  &#44; 143 &#124;  Angriff ins Handgemenge (Zufallsopfer) +4 &#44; 187  &#125; | Schnelles Ziel (30m+/Runde) -2, ?{Schnelles Ziel -2 +/- ...&#124;  SONST NIX +/-0  &#44; 13  &#124;  Mini-Ziel (1/4xMensch) -4 &#44; 26  &#124;  Kleines Ziel/halb gedeckt (1/2xMensch) -2 &#44; 39  &#124;  GroÃŸes Ziel (2xMensch) +2 &#44; 65  &#124;  Riesiges Ziel (4xMensch) +4 &#44; 91  &#124;  Spontaner Angriff -4  &#44; 143 &#124;  SorgfÃ¤ltig gezielt +4 &#44; 247  &#125; | SorgfÃ¤ltig gezielt +4, ?{SorgfÃ¤ltig gezielt +4 +/- ...&#124;  SONST NIX +/-0  &#44; 19  &#124;  Mini-Ziel (1/4xMensch) -4 &#44; 38  &#124;  Kleines Ziel/halb gedeckt (1/2xMensch) -2 &#44; 57  &#124;  GroÃŸes Ziel (2xMensch) +2 &#44; 95  &#124;  Riesiges Ziel (4xMensch) +4 &#44; 133 &#124;  Schnelles Ziel (30m+/Runde) -2  &#44; 247  &#125; | ScharfschieÃŸen +4/Spezial, ?{ScharfschieÃŸen +4/Spezial +/- ...&#124;  SONST NIX +/-0  &#44; 23  &#124;  Mini-Ziel (1/4xMensch) -4 &#44; 46  &#124;  Kleines Ziel/halb gedeckt (1/2xMensch) -2 &#44; 69  &#124;  GroÃŸes Ziel (2xMensch) +2 &#44; 115 &#124;  Riesiges Ziel (4xMensch) +4 &#44; 161 &#124;  Schnelles Ziel (30m+/Runde) -2  &#44; 299  &#125;}"
!mmm   if semiAutoModCode % 23 == 0
!rem     // Check if PC has sharpshooting skill
!mmm     if not sharpshootingSkill
!mmm       do whisperback("Abbruch: " & cOwnID.name & " hat die Fertigkeit ScharfschieÃŸen nicht.")
!mmm       exit script
!mmm     end if
!mmm     set semiAutoModifiers = semiAutoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{ScharfschieÃŸen=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " ScharfschieÃŸen&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!mmm   if semiAutoModCode % 19 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{SorgfÃ¤ltig gezielt=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " SorgfÃ¤ltig&nbsp;gezielt&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!mmm   if semiAutoModCode % 17 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{Angriff ins Handgemenge=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Angriff&nbsp;ins&nbsp;Handgemenge&nbsp;&nbsp;&nbsp;+4 "
!rem     // TODO: Remind user to throw dice to identify actual target!
!mmm   end if
!mmm   if semiAutoModCode % 13 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers - 2
!mmm     set modifierLog = modifierLog & " {{Schnelles Ziel=-2}} "
!mmm     set modifierTooltip = modifierTooltip & " Schnelles&nbsp;Ziel&nbsp;&nbsp;&nbsp;-2 "
!mmm   end if
!mmm   if semiAutoModCode % 11 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers - 4
!mmm     set modifierLog = modifierLog & " {{Spontaner Angriff=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Spontaner&nbsp;Angriff&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!mmm   if semiAutoModCode % 7 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 4
!mmm     set modifierLog = modifierLog & " {{Riesiges Ziel=+4}} "
!mmm     set modifierTooltip = modifierTooltip & " Riesiges&nbsp;Ziel&nbsp;&nbsp;&nbsp;+4 "
!mmm   end if
!mmm   if semiAutoModCode % 5 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers + 2
!mmm     set modifierLog = modifierLog & " {{GroÃŸes Ziel=+2}} "
!mmm     set modifierTooltip = modifierTooltip & " GroÃŸes&nbsp;Ziel&nbsp;&nbsp;&nbsp;+2 "
!mmm   end if
!mmm   if semiAutoModCode % 3 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers - 2
!mmm     set modifierLog = modifierLog & " {{Kleines/halb verdecktes Ziel=-2}} "
!mmm     set modifierTooltip = modifierTooltip & " Kleines/halb&nbsp;verdecktes&nbsp;Ziel&nbsp;&nbsp;&nbsp;-2 "
!mmm   end if
!mmm   if semiAutoModCode % 2 == 0
!mmm     set semiAutoModifiers = semiAutoModifiers -4
!mmm     set modifierLog = modifierLog & " {{Sehr kleines Ziel=-4}} "
!mmm     set modifierTooltip = modifierTooltip & " Sehr&nbsp;kleines&nbsp;Ziel&nbsp;&nbsp;&nbsp;-4 "
!mmm   end if
!rem   // Ask for individual modifiers and sum up the various modifiers
!mmm   set manualModifiers = "?{Weitere spezielle Angriffsmodifikatoren|0}"
!mmm   if manualModifiers != 0
!mmm     if manualModifiers > 0
!mmm       set manualModifiers = "+" & (manualModifiers * 1)
!mmm     end if
!mmm     set modifierLog = modifierLog & " {{Benutzereingabe=" & manualModifiers & "}} "
!mmm     set modifierTooltip = modifierTooltip & " Benutzereingabe&nbsp;&nbsp;&nbsp;" & manualModifiers & " "
!mmm   end if
!mmm   set modifiers = modifiers + autoModifiers + semiAutoModifiers + manualModifiers
!rem
!rem   // Check for attack from behind
!mmm   if (foeViewAngle < -135) or (foeViewAngle > 135)
!mmm     set attackFromBehind = true
!mmm   else
!mmm     set attackFromBehind = false
!mmm   end if
!mmm
!rem   // For magic weapons: Process optional endurance cost
!mmm   if cMagicEnduranceCost > 0
!mmm     do m3mgdModifyEndurance(-1 * cMagicEnduranceCost, cOwnID, cEnduranceAttr)
!mmm     set modifierLog = modifierLog & "{{ " & cWeaponLabel & "=-" & cMagicEnduranceCost & " AP }}"
!mmm   end if
!rem
!rem   // Run & format rolls
!rem
!mmm   set attackResult = cAttackRoll + effAttack + modifiers
!mmm   if modifiers == 0
!mmm     set modifiers = "+/-0"
!mmm   else if modifiers > 0
!mmm     set modifiers = "+" & modifiers
!mmm   end if
!mmm   if modifierLog eq ""
!mmm     set modifierLog = "{{Boni/Mali=keine}}"
!mmm   end if
!mmm   set modifiers = highlight(modifiers, "info", modifierTooltip)
!mmm   set attackResultLog = attackResult & " = (1d20 = " & cAttackRoll & ") + (EW = " & effAttack & ") " & modifiers
!mmm   if iscritical(cAttackRoll)
!mmm     set attackResult = highlight(attackResult, "good", attackResultLog)
!mmm     set criticalAttack = true
!mmm   else if isfumble(cAttackRoll)
!mmm     set attackResult = highlight(attackResult, "bad", attackResultLog)
!mmm     set criticalAttack = false
!mmm   else 
!mmm     set attackResult = highlight(attackResult, "normal", attackResultLog)
!mmm     set criticalAttack = false
!mmm   end if
!mmm 
!mmm   if attackResult >= 20
!rem     // Damage roll (and make sure it doesn't get negative)
!mmm     set damageRoll = roll(weaponDamageExpr)
!mmm     set damageResult = damageRoll + weaponDamageBonus
!mmm     set damageResultLog = "(" & weaponDamageExpr & " = " & damageRoll & ") " & weaponDamageBonus
!mmm     if damageResult < 0
!mmm       set damageResult = highlight(0, "bad")
!mmm     end if
!mmm     set damageResultBasic = highlight(damageResult, damageResult, damageResultLog)
!rem     // Special case: sharpshooting
!mmm     set sharpExtraDamage = 0
!mmm     if semiAutoModCode % 23 == 0
!mmm       set sharpExtraRoll = roll("1d20")
!mmm       set sharpExtraResult = sharpExtraRoll + effAttack + modifiers - 4 + sharpshootingSkill
!mmm       set sharpExtraTooltip = sharpExtraResult & " = (1d20 = " & sharpExtraRoll & ") + (EW = " & effAttack & ") + " & modifiers - 4 & " + (FW:ScharfschieÃŸen = " & sharpshootingSkill & ")"
!mmm       if iscritical(sharpExtraRoll)
!mmm         set sharpExtraResult = highlight(sharpExtraResult, "good", sharpExtraTooltip)
!mmm       else if isfumble(sharpExtraRoll)
!mmm         set sharpExtraResult = highlight(sharpExtraResult, "bad", sharpExtraTooltip)
!mmm       else
!mmm         set sharpExtraResult = highlight(sharpExtraResult, "normal", sharpExtraTooltip)
!mmm       end if
!mmm       if sharpExtraResult >= 20
!mmm         set sharpExtraDamage = round(sharpExtraResult * .1)
!mmm         set damageResult = damageResult + sharpExtraDamage
!mmm         set damageResultLog = damageResultLog & " + (Scharfschuss-Bonus = " & sharpExtraDamage & ")"
!mmm       end if
!mmm     end if
!rem     // Special case: magic ammunition with extra damage
!mmm     if cAmmoMagic and cAmmoMagicDamage ne ""
!mmm       set magicExtraDamage = roll(cAmmoMagicDamage)
!mmm       if magicExtraDamage < 0
!mmm         set magicExtraDamage = highlight(0, "bad")
!mmm       end if
!mmm       set damageResult = damageResult + magicExtraDamage
!mmm       set damageResultLog = damageResultLog & " + (" & cAmmoMagicDamage & " = " & magicExtraDamage & ")"
!mmm     end if 
!rem     // Complete damage variables
!mmm     set damageResultLog = damageResult & " = " & damageResultLog
!mmm     set damageResult = highlight(damageResult, default, damageResultLog)
!mmm   end if
!mmm
!rem   // Save key attack data for defense script
!mmm   if attackResult >= 20 and damageResult > 0
!mmm     if not m3mgdExchangeStoreAttack("ranged") 
!mmm       do whisperback("Problem bei der Datenablage fÃ¼r das Abwehrskript; Angriffsdaten bitte manuell ergÃ¤nzen.")
!mmm     end if
!mmm   end if
!rem
!rem   // Break down extra magic damage to player and GM
!mmm   if cAmmoMagic and attackResult >= 20 and damageResult > 0
!mmm     set modifierLog = modifierLog & " {{Zauberschaden=" & damageResult & " }} "
!mmm   end if
!mmm
!rem   // Register practice point, if applicable
!mmm   if iscritical(cAttackRoll) and foeEndurance > 0
!mmm     if m3mgdProcessCombatPP(cOwnID, cWeaponLabel, foeID)
!mmm       set modifierLog = modifierLog & " {{Praxispunkt=[x](https://media.giphy.com/media/hKafco7mFwBioBxqFT/giphy.gif)}} "
!mmm     end if
!mmm   else if iscritical(cAttackRoll)
!mmm     set modifierLog = modifierLog & " {{Kein Praxispunkt=Gegner war schon erschÃ¶pft (AP=0)}} "
!mmm   end if
!mmm
!rem   // Pull together a sensible guess at a default way of addressing the weapon for output
!mmm   if cWeaponName eq ""
!mmm     set outputWeaponLabel = cWeaponLabel
!mmm   else
!mmm     set outputWeaponLabel = cWeaponType[0] & " *" & cWeaponName & "*"
!mmm   end if
!mmm
!mmm
!mmm   if cVerbose
!mmm     combine chat
!rem       // Check if script is run for an NPC (whisper data to GM only)
!mmm       if cGMSilentMode
!mmm         chat: /w GM 
!mmm       end if
!mmm       if not cAmmoMagic
!mmm         chat [AttackOpening]:              Angriff mit dem $[weaponLabel]{outputWeaponLabel} ($[weaponType]{cWeaponType[0]} "$[weaponName]{cWeaponName}"): Einer/s meiner/s $[ammoLabel]{ammoLabel} fliegt in Richtung $[targetName]{foeName} in $[range]{rangeDescription} und
!mmm       else
!mmm         chat [AttackOpeningMagicDamage]:   Mein Angriff mit $[weaponLabel]{outputWeaponLabel} ($[weaponType]{cWeaponType[0]} "$[weaponName]{cWeaponName}", magischer Zusatzschaden aktiviert) auf $[targetName]{foeName} 
!mmm       end if
!mmm       if criticalAttack
!mmm         chat [AttackIsCriticalSuccess]:    ist ein **kritischer Erfolg** ($[attackResult]{attackResult+0}). 
!mmm       else if isfumble(cAttackRoll)
!mmm         chat [AttackIsCriticalFailure]:    ist ein **grausamer Patzer**. 
!mmm       else if attackResult >= 22
!mmm         chat [AttackSucceedsWell]:         scheint ein ordentlicher Treffer zu werden ($[attackResult]{attackResult+0}), wenn $[targetName]{foeName} die Abwehr nicht gelingt.
!mmm       else if attackResult >= 20
!mmm         chat [AttackSucceedsBarely]:       trifft gerade so ($[attackResult]{attackResult+0}), wenn $[targetName]{foeName} die Abwehr nicht gelingt.
!mmm       else if attackResult >= 18
!mmm         chat [AttackFailsBarely]:          verfehlt sein Ziel knapp ($[attackResult]{attackResult+0}). $[targetName]{foeName} muss nicht ausweichen, aber **steht dahinter noch jemand?**
!mmm       else
!mmm         chat [AttackFailsClearly]:         verfehlt sein Ziel ($[attackResult]{attackResult+0}). $[targetName]{foeName} muss nicht einmal ausweichen.
!mmm       end if
!mmm       if attackResult >= 20 and sharpExtraDamage
!mmm         chat [AttackSuccessSharpshooting]: Der wird wehtun: die vielen, vielen Stunden mit der Zielscheibe haben sich mal wieder ausgezahlt.
!mmm       end if
!mmm       if attackResult >= 20 and cAmmoMagic
!mmm         chat [AttackSuccessClosingMagic]:  Au weia, da erwischt $[targetName]{foeName} auch Zauberschaden, wenn die Abwehr nicht gelingt.
!mmm       else if attackResult >= 20 and (not cAmmoMagic)
!mmm         chat [AttackSuccessClosing]:       $[targetName]{foeName} kann nun abwehren.
!mmm       else
!mmm         chat [AttackFailureClosing]:       $[targetName]{foeName} war zu keinem Zeitpunkt in Gefahr.
!mmm       end if
!mmm     end combine
!mmm   end if
!rem
!rem   // Add data table for easy reference
!mmm   combine chat
!rem     // Check if script is run for an NPC (whisper data to GM only)
!mmm     if cGMSilentMode
!mmm       chat: /w GM 
!mmm     end if
!mmm     chat:      ${"&"}{template:default} {{name=${cOwnID.name}: Fernkampfangriff mit ${cWeaponType[0]} * ${cWeaponName} *}} 
!mmm     chat:      {{Ziel=**${foeName}** ${m3mgdShapeMoji(foeID, cHealthAttr, cEnduranceAttr)}}}
!mmm     if iscritical(cAttackRoll)
!mmm       chat:    {{Kritischer Erfolg=${attackResult}&nbsp;(**Wurf** ${cAttackRoll})}}
!mmm     else if attackResult >= 20
!mmm       chat:    {{Erfolg=${attackResult} (**Wurf** ${cAttackRoll})}}
!mmm     else 
!mmm       chat:    {{Fehlschlag=${attackResult} (**Wurf** ${cAttackRoll})}}
!mmm     end if
!mmm     chat:      {{Boni/Mali=**${modifiers}**}}
!mmm     chat:      {{Entfernung=${distance} ${distanceUnits} (${round(distance/(distsnap()*distscale()))} Planquadrate, ${rangeBracket})}}
!mmm     if attackFromBehind
!mmm       chat:    {{Angriff von hinten=Bei Hinterhalt **kein Abwehrwurf** (${foeViewAngle}Â°)}}
!mmm     end if 
!mmm     if attackResult >= 20 and semiAutoModCode % 23 == 0
!mmm       chat:    {{Basisschaden=${damageResultBasic}}}
!mmm       chat:    {{ScharfschieÃŸen=${sharpExtraResult}}}
!mmm       chat:    {{Vollschaden=${damageResult}}}
!mmm     else if attackResult >= 20
!mmm       chat:    {{Schaden=${damageResult}}}
!mmm     end if
!mmm     if ammoCount ne "âˆž"
!mmm       chat:    {{${ammoLabel}=${ammoCount}}}
!mmm     end if
!mmm   end combine
!mmm
!mmm   chat: /w GM ${"&"}{template:default} ${modifierLog}
!mmm   if not cGMSilentMode
!mmm     chat: /w "${cOwnID.character_name}" ${"&"}{template:default} ${modifierLog}
!mmm   end if
!mmm
!mmm   if attackResult >= 20
!mmm     do m3mgdWeaponSelectorChatMenu(foeID, "defense", cWeaponType)
!mmm   end if
!mmm
!mmm end script