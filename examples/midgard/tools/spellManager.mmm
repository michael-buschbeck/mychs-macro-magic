!rem // spellManager
!rem //
!mmm script
!mmm
!rem // m3mgdGenerateUID(characterID)
!rem // 
!rem //   Generate random ID for Roll20 tables and make sure it's unique.
!rem // 
!mmm function m3mgdGenerateUID(characterID)
!mmm   set spellUID = roll("100d256")**2
!mmm   if characterID.("repeating_zauber_" & spellUID & "_Zauber")
!mmm     set spellUID = m3mgdGenerateUID(characterID)
!mmm   else 
!mmm     return spellUID
!mmm   end if
!mmm end function
!rem
!rem // m3mgdWriteSpellToSheet(characterID, spellRecord)
!rem // 
!rem //   Generate random ID for Roll20 tables and make sure it's unique.
!rem // 
!mmm function m3mgdWriteSpellToSheet(characterID, spellRecord)
!mmm   
!mmm   set effectDesc = ""
!mmm   if spellRecord.Material
!mmm     set effectDesc = "Benötigt " & spellRecord.Material & ". "
!mmm   end if
!mmm   set effectDesc = effectDesc & "Kategorie: " & spellRecord.Kategorie & ", Stufe " & spellRecord.Stufe & ". Agens: " & spellRecord.Agens & ", Reagens: " & spellRecord.Reagens
!mmm   set spellRecord = { spellRecord, ZauWirkung: effectDesc }
!mmm
!mmm   set spellUID = m3mgdGenerateUID(characterID)
!mmm
!mmm   set verified = 0
!mmm   for property in "Zauber", "AP", "RW", "Ziel", "WD", "WB", "ZD", "Art", "Prozess", "ZauWirkung"
!mmm     if setattr(characterID, "repeating_zauber_" & spellUID & "_" & property, spellRecord.(property)) eq spellRecord.(property)
!mmm       set verified = verified + 1
!mmm     else 
!mmm       debug chat: setattr() failed for ${characterID.character_name} (${characterID}) to set attribute ${"repeating_zauber_" & spellUID & "_" & property} to "${spellRecord.(property)}"
!mmm     end if
!mmm   end for
!mmm
!mmm   return (verified == 10)
!mmm
!mmm end function
!mmm
!mmm   set customizable cmd = default
!mmm   set customizable tokenID = default
!mmm   set customizable spellID = default
!mmm   set customizable spellCategory = default
!mmm   set customizable spellLevel = default
!mmm
!mmm   set spellIndex = deserialize("MacroSheet".m3mgdSpellBookIndex)
!mmm   set spellBook = deserialize("MacroSheet".m3mgdSpellBook)
!mmm
!mmm   if cmd eq "addSpellToSheet" and tokenID ne "" and not isdefault(spellID)
!mmm
!rem     // Add spell to character's spell sheet
!mmm     set spell = spellBook... where ...key eq spellID
!mmm     if not spell
!mmm       debug chat: Spell not found.
!mmm       exit script
!mmm     end if
!mmm
!mmm     if m3mgdWriteSpellToSheet(tokenID.character_id, spell.value)
!mmm       do whisperback("spellManager: Zauber '" & spell.value.Zauber & "' ins Zauberblatt von '" & tokenID.character_name & "' ergänzt.")
!mmm     else 
!mmm       do whisperback("spellManager: Irgendwas ist schiefgegangen beim Versuch, den Zauber '" & spell.value.Zauber & "' ins Zauberblatt von '" & tokenID.character_name & "' zu schreiben.")
!mmm     end if 
!mmm
!mmm   else if cmd eq "listSpells" and not isdefault(spellCategory)
!mmm
!rem     // Print relevant part of spellbook to chat
!mmm     combine chat
!mmm       set heading = "Die Große Midgard-Zauberschule :: " & spellCategory
!rem //      if spellCategory eq "Zauberspruch"
!rem //        set heading = heading & " (Stufe " & spellLevel & ")"
!rem //      end if 
!mmm       do chat(chatTightBoxHeader(heading))
!mmm       for spell in spellBook...
!rem //        if (spell.value.Kategorie eq spellCategory and spellCategory ne "Zauberspruch") or (spellCategory eq "Zauberspruch" and spell.value.Kategorie eq spellCategory and spell.value.Stufe == spellLevel)
!mmm         if spell.value.Kategorie eq spellCategory
!mmm           set payload = literal("!mmm customize") & "&#13;"
!mmm           set payload = payload & literal("!mmm set cmd=\"addSpellToSheet\"") & "&#13;"
!mmm           set payload = payload & literal("!mmm set tokenID=selected.token_id") & "&#13;"
!mmm           set payload = payload & literal("!mmm set spellID=\"" & spell.key & "\"") & "&#13;"
!mmm           set payload = payload & literal("!mmm end customize") & "&#13;" & "&#x25;{MacroSheet|spellManager}" & "&#13;"
!mmm           do chat(chatTightBoxFlowButton(spell.value.Zauber, "", payload))
!mmm         end if
!mmm       end for
!mmm       set payload = literal("!mmm customize") & "&#13;"
!mmm       set payload = payload & literal("!mmm set cmd=default") & "&#13;"
!mmm       set payload = payload & literal("!mmm end customize") & "&#13;" & "&#x25;{MacroSheet|spellManager}" & "&#13;"
!mmm       do chat(chatTightBoxButtonRow("<< zurück zum Index", "", payload))
!mmm     end combine
!mmm
!mmm   else if isdefault(cmd)
!mmm
!rem     // Print spellbook index to chat
!mmm     combine chat
!mmm       do chat(chatTightBoxHeader("Die Große Midgard-Zauberschule"))
!mmm       for spellCategory in spellIndex...
!rem //      if spellCategory.key eq "Zauberspruch"
!rem //        do chat(chatTightBoxSubHeader(spellCategory))
!rem //        for spellLevel in (1,2,3,4,5,6,7,8,9,10,11,12)
!rem //          set payload = literal("!mmm customize") & "&#13;"
!rem //          set payload = payload & literal("!mmm set cmd=\"listSpells\"") & "&#13;"
!rem //          set payload = payload & literal("!mmm set spellCategory=\"" & spellCategory.key & "\"") & "&#13;"
!rem //          set payload = payload & literal("!mmm set spellLevel=\"" & spellLevel & "\"") & "&#13;"
!rem //          set payload = payload & literal("!mmm end customize") & "&#13;" & "&#x25;{MacroSheet|spellManager}" & "&#13;"
!rem //          do chat(chatTightBoxFlowButton(spellLevel, "", payload))
!rem //        end for
!rem //      else 
!mmm           set payload = literal("!mmm customize") & "&#13;"
!mmm           set payload = payload & literal("!mmm set cmd=\"listSpells\"") & "&#13;"
!mmm           set payload = payload & literal("!mmm set spellCategory=\"" & spellCategory.key & "\"") & "&#13;"
!mmm           set payload = payload & literal("!mmm end customize") & "&#13;" & "&#x25;{MacroSheet|spellManager}" & "&#13;"
!mmm           do chat(chatTightBoxButtonRow(spellCategory, "", payload))
!rem //      end if
!mmm       end for
!mmm     end combine
!mmm
!mmm   end if
!mmm
!mmm end script