!rem // writeWeaponScripts
!rem // Automates (in part) writing of config scripts for common weapons
!mmm script
!rem
!mmm   set weaponTypeList = "Einhandschwerter", "Zweihandschwerter", "Stichwaffen", "Sto√üwaffen", "Stockwaffen", "Wurfmesser", "Bogen", "Langbogen", "Kompositbogen", "Schwere Armbrust", "Leichte Armbrust", "Schleuderwaffen", "Unbewaffnet"
!rem
!rem   // Build list of all selected tokens' characters
!mmm   
!mmm   set tokenList = selected
!mmm   if not tokenList
!mmm     do whisperback("No tokens selected. Aborting.")
!mmm     exit script
!mmm   end if
!mmm   
!mmm   for token in tokenList
!mmm     if isdenied(token.character_id)
!mmm       do whisperback("No character id available for token " & token & ": " & getreason(token.character_id))
!mmm       exit script
!mmm     end if
!mmm     
!mmm     set dupe = false
!mmm     for char in charList
!mmm       if token.character_id eq char
!mmm         set dupe = true
!mmm       end if
!mmm     end for
!mmm     
!mmm     if not dupe
!mmm       set charList = charList, token.character_id
!mmm     end if
!mmm     
!mmm   end for
!mmm   
!rem   // Build list of *unique* weapons used by those characters
!mmm   
!mmm   for char in charList
!mmm     
!mmm     for weaponLabelAttr in findattr(char, "Angriff", "Waffe")
!mmm       
!mmm       set dupe = false
!mmm       for weaponLabel in weaponLabelList
!mmm         if weaponLabel eq char.(weaponLabelAttr)
!mmm           set dupe = true
!mmm         end if
!mmm       end for
!mmm       
!mmm       if not dupe
!mmm         set weaponLabelList = weaponLabelList, char.(weaponLabelAttr)
!mmm       end if
!mmm       
!mmm     end for
!mmm     
!mmm     debug chat: ${char.character_name}: ${weaponLabelList}
!mmm     
!mmm   end for
!mmm   
!rem   // For each unique weapon, determine weapon type and build config script
!mmm   
!mmm   set lineBreak = "<" & "br>"
!mmm   combine chat using lineBreak
!mmm     chat: MMM code following:
!mmm     chat: ====================
!mmm
!mmm     for weaponLabel in weaponLabelList
!mmm     
!mmm       set weaponType = "MANUALTYPE"
!mmm       for weaponTypeOption in weaponTypeList
!mmm         if weaponTypeOption eq weaponLabel
!mmm           set weaponType = weaponTypeOption
!mmm         end if
!mmm       end for
!rem       // set scriptCall = "{MacroSheet|meleeAttack}"
!mmm       set scriptCall = "SCRIPTCALL"
!mmm       chat: ${"!"}mmm customize
!mmm       chat: ${"!"}mmm   set cWeaponLabel = "${weaponLabel}"
!mmm       chat: ${"!"}mmm   set cWeaponType = "${weaponType}"
!mmm       chat: ${"!"}mmm end customize
!mmm       chat: ${scriptCall}
!mmm       chat: - - - - - - - - - -
!mmm
!mmm     end for
!mmm   
!mmm   end combine
!mmm   
!mmm end script