!rem // showPersistentEffects v1.1 2024-05-24 phr 
!rem //
!rem // Chats a list of active persistent effects for the selected token, or all tokens if none are selected.
!rem 
!mmm script
!mmm function removeEffectPayload(tokenID, removeEffectIndex)
!mmm   set payload = literal("!mmm customize") & "&#13;"
!mmm   set payload = payload & literal("!mmm set cmd=\"removeEffect\"") & "&#13;"
!mmm   set payload = payload & literal("!mmm set tokenID=\"" & tokenID & "\"") & "&#13;"
!mmm   set payload = payload & literal("!mmm set removeEffectIndex=\"" & removeEffectIndex & "\"") & "&#13;"
!mmm   set payload = payload & literal("!mmm set deleteConfirm=?" & "{Für " & tokenID.token_name & " wirklich Effekt Nr." & (removeEffectIndex + 1) & " löschen?|Nein,false|Ja,true}") & "&#13;"
!mmm   set payload = payload & literal("!mmm end customize") & "&#13;" & "&#x25;{MacroSheet|removePersistentEffect}" & "&#13;"
!mmm   return payload
!mmm end function
!mmm 
!rem // MAIN
!rem
!mmm set pEregistry = deserialize(m3mgdExchange.m3mgdActivePersistentEffects)
!mmm set cleanRegistry = { pEregistry... where ...value ne undef }
!mmm if not cleanRegistry
!mmm   do chat(chatTightBoxRow("Keine persistenten Effekte im aktuellen Spiel."))
!mmm   exit script
!mmm end if
!mmm 
!mmm if selected
!mmm   set refLabel = selected.token_name
!mmm else
!mmm   set refLabel = "alle"
!mmm end if
!mmm set output = chatTightBoxHeader("Persistente Effekte für " & refLabel)
!mmm
!mmm for affectedTokenRecord in cleanRegistry...
!rem   // If there is a selected token, only produce output for this token alone. If there is no selected token, produce output for every token with an active persistent effect.
!mmm   if (not selected or (selected and affectedTokenRecord.key eq selected)) and affectedTokenRecord.value
!mmm     set output = output & chatTightBoxSubHeader(affectedTokenRecord.key.token_name)
!mmm       set activeCounter = 0
!mmm       for statusRule in m3mgdGetActivePersistentEffects(affectedTokenRecord.key)
!mmm         set activeCounter = activeCounter + 1
!mmm         set line = "❌ " & "#" & activeCounter & " (+" & statusRule.effect.expiry.count & " " & statusRule.effect.expiry.type & "s): **" & statusRule.label & "**"
!mmm         set tooltip = "Effekt löschen? " & statusRule.desc
!mmm         if statusRule.effect
!mmm           set line = line & " - " & stringify(m3mgdPersistentEffectLabels(statusRule.effect), "", "; ")
!mmm         end if
!mmm         set output = output & chatTightBoxButtonRow(line, tooltip, removeEffectPayload(affectedTokenRecord.key, activeCounter-1))
!mmm       end for
!mmm   end if
!mmm end for
!mmm
!mmm if activeCounter == 0
!mmm   set output = output & chatTightBoxRow("keine")
!mmm end if
!mmm
!mmm do chat(output)
!mmm end script