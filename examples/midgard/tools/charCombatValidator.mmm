!mmm script
!mmm   
!rem
!mmm function m3mgdCreateDropDownWeaponSelectorAttr(characterID, weaponsGroup)  
!mmm   
!mmm   if isdefault(characterID)
!mmm     set characterID = sender.character_id
!mmm   end if
!mmm   
!mmm   if isdefault(weaponsGroup)
!mmm     set weaponsGroup = "melee"
!mmm   end if
!mmm   
!mmm   if weaponsGroup eq "defense"
!mmm     set weaponsList = m3mgdListDefenseWeapons(characterID)
!mmm     set selectorPrompt = "Abwehrwaffe"
!mmm   else if weaponsGroup eq "melee"
!mmm     set weaponsList = m3mgdListMeleeAttackWeapons(characterID)
!mmm     set selectorPrompt = "Nahkampfwaffe"
!mmm   else if weaponsGroup eq "ranged"
!mmm     set weaponsList = m3mgdListRangedAttackWeapons(characterID)
!mmm     set selectorPrompt = "Fernkampfwaffe"
!mmm   end if
!mmm   
!mmm   set r20SelectorCode = ""
!mmm   for weaponLabelAttr in weaponsList
!mmm     if r20SelectorCode ne ""
!mmm       set r20SelectorCode = r20SelectorCode & "|"
!mmm     end if
!mmm     set r20SelectorCode = r20SelectorCode & characterID.(weaponLabelAttr)
!mmm   end for
!mmm   set r20SelectorCode = "?" & "{" & selectorPrompt & "|" & r20SelectorCode & "}"
!mmm   
!mmm   set selectorAttr = "m3mgd_" & weaponsGroup & "_weapon_selector"
!mmm   do setattr(characterID, selectorAttr, r20SelectorCode)
!mmm   return selectorAttr
!mmm end function
!mmm
!mmm
!mmm   if selected
!mmm     set charID = selected.character_id
!mmm   else 
!mmm     set charID = sender.character_id
!mmm   end if
!mmm   
!mmm   set configScriptsRequired = 0
!mmm
!mmm   combine chat using "<" & "br>"
!mmm
!mmm     chat: ============
!mmm     chat: Charakterbogen: **${charID.character_name}**
!mmm     chat: ------------
!mmm     chat: Verteidigungswaffen
!mmm   
!mmm     for weaponLabelAttr in m3mgdListDefenseWeapons(charID)
!mmm       
!mmm       set weaponLabel = charID.(weaponLabelAttr)
!mmm       set weaponType = m3mgdDefenseWeaponType(weaponLabel)
!mmm     
!mmm       if not isunknown(weaponType)
!mmm         if (weaponLabel eq "Abwehr ohne Schild") or (weaponLabel eq "ohne Schild") or (m3mgdParryLargeShieldTypes where ... eq weaponLabel)
!mmm           set utilityStatement = "gegen alle Angriffe"
!mmm         else if m3mgdParryStandardTypes where ... eq weaponLabel
!mmm           set utilityStatement = "gegen " & m3mgdParryStandardEffectiveAgainst
!mmm         else if m3mgdParrySmallShieldTypes where ... eq weaponLabel
!mmm           set utilityStatement = "gegen " & m3mgdParrySmallShieldEffectiveAgainst
!mmm         else
!mmm           set utilityStatement = "gegen keinerlei Angriffe (**unbekannter Waffentyp:** in initGameGlobals ergänzen!)"
!mmm         end if
!mmm         chat: *${weaponLabel}* als Standardwaffe erkannt, anwendbar ${utilityStatement}.
!mmm       else 
!mmm         chat: *${weaponLabel}* **braucht ein Konfigskript.**
!mmm         set configScriptsRequired = configScriptsRequired + 1
!mmm       end if
!mmm     
!mmm     end for
!mmm
!mmm     chat: ------------
!mmm     chat: Angriffswaffen
!mmm
!mmm     for weaponLabelAttr in findattr(charID, "Angriff", "Waffe")
!mmm       
!mmm       set weaponLabel = charID.(weaponLabelAttr)
!mmm       set weaponProperties = unknown
!mmm       set rangedWeaponAmmoLog = ""
!mmm       
!mmm       if charID.(findattr(charID, "Angriff", "Waffe", weaponLabel, "FkWaffe")) == 1
!mmm         set weaponProperties = m3mgdRangedWeaponProperties(weaponLabel)
!mmm         if not isunknown(weaponProperties) and count(weaponProperties) == 5
!mmm           set rangedWeaponAmmoAttr = weaponProperties[4]
!mmm           if isunknown(charID.(rangedWeaponAmmoAttr))
!mmm             do setattr(charID, rangedWeaponAmmoAttr, 0)
!mmm             set rangedWeaponAmmoLog = "Munition: **Standard-Attribut '" & rangedWeaponAmmoAttr & "' mit Wert '0' angelegt.**"
!mmm           else
!mmm             set rangedWeaponAmmoLog = "Munition: Standard-Attribut '" & rangedWeaponAmmoAttr & "' gefunden (Wert: '" & charID.(rangedWeaponAmmoAttr) & "')."
!mmm           end if
!mmm         else 
!mmm           set rangedWeaponAmmoLog = "Munition: kein Standard-Attribut bekannt."
!mmm         end if
!mmm       else 
!mmm         set weaponProperties = m3mgdMeleeWeaponType(weaponLabel)
!mmm       end if
!mmm     
!mmm       if not isunknown(weaponProperties)
!mmm         chat: *${weaponLabel}* als Standardwaffe erkannt. ${rangedWeaponAmmoLog}
!mmm       else 
!mmm         set configCallAttr = "m3mgd_configCall_" & weaponLabel
!mmm         if isunknown(charID.(configCallAttr))
!mmm           set payload = "!mmm script&#13;"
!mmm           set payload = payload & "!mmm set configCall&#x3D;&#x22;?" & "{Ability für " & literal(weaponLabel) & "&#x7C;}&#x22;&#13;"
!mmm           set payload = payload & "!mmm do setattr(&#x22;" & charID.character_id & "&#x22;, &#x22;" & literal(configCallAttr) & "&#x22;, configCall&#x29;&#13;"
!mmm           set payload = payload & "!mmm end script&#13;"
!mmm           chat: *${weaponLabel}* **braucht ein Konfigskript.** [**Verknüpfen**](${payload}) ${rangedWeaponAmmoLog}
!mmm         else
!mmm           chat: *${weaponLabel}* **braucht ein Konfigskript, und hat wohl eins.** [**Testen**](${"~"}${charID.token_name}|${charID.(configCallAttr)})  ${rangedWeaponAmmoLog}
!mmm         end if
!mmm         set configScriptsRequired = configScriptsRequired + 1
!mmm       end if
!mmm     
!mmm     end for
!mmm
!mmm     chat: ------------
!mmm     chat: **${charID.character_name}** braucht **${configScriptsRequired} Konfigskripte.**
!mmm     chat: ============
!mmm
!mmm   end combine
!mmm
!mmm end script