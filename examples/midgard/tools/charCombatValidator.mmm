!mmm script
!mmm   
!rem
!rem // m3mgdDefenseWeaponType(weaponLabel)
!rem // 
!rem //   If weaponLabel is exactly one of a list of standard attack or defense arms, returns the correct weaponType for it. 
!rem //   Otherwise returns unknown. 
!rem // 
!mmm function m3mgdDefenseWeaponType(weaponLabel)
!mmm 
!mmm   set weaponType = unknown
!mmm   
!mmm   if ("Buckler", "großer Schild", "kleiner Schild") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Schild"
!mmm   
!mmm   else if ("Kampfgabeln", "Parierdolch") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Parierwaffe"
!mmm   
!mmm   else if weaponLabel eq "Abwehr ohne Schild"
!mmm   
!mmm     set weaponType = ""
!mmm   
!mmm   end if
!mmm   
!mmm   return weaponType
!mmm 
!mmm end function
!rem
!rem // m3mgdMeleeWeaponType(weaponLabel)
!rem // 
!rem //   If weaponLabel is exactly one of a list of standard attack or defense arms, returns the correct weaponType for it. 
!rem //   Otherwise returns unknown. 
!rem // 
!mmm function m3mgdMeleeWeaponType(weaponLabel)
!mmm 
!mmm   set weaponType = unknown
!mmm   
!mmm   if ("Handaxt", "Keule", "Kriegshammer", "Schlachtbeil", "Streitaxt", "Streitkolben") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Einhandschlagwaffe"
!mmm   
!mmm   else if ("Anderthalbhänder", "Krummschwert", "Langschwert") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Einhandschwert"
!mmm   
!mmm   else if ("Florett", "Fuchtel", "Rapier") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Fechtwaffe"
!mmm   
!mmm   else if ("Kriegsflegel", "Morgenstern", "NunChaku") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Kettenwaffe"
!mmm   
!mmm   else if ("Gleffe", "Hellebarde", "leichter Speer", "Stielhammer", "Stoßspeer") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Spießwaffe"
!mmm   
!mmm   else if ("Dolch", "Kurzschwert", "Ochsenzunge") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Stichwaffe"
!mmm   
!mmm   else if ("Bo-Stab", "Kampfstab") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Stockwaffe"
!mmm   
!mmm   else if ("Magierstab", "Magierstecken") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Zauberstab"
!mmm   
!mmm   else if ("Barbarenstreitaxt", "Gleffe", "Hellebarde", "Ogerhammer", "Schlachtbeil", "Stabkeule", "Stielhammer") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Zweihandschlagwaffe"
!mmm   
!mmm   else if ("Anderthalbhänder", "Bihänder") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "Zweihandschwert"
!mmm   
!mmm   else if ("waffenloser Kampf", "Faustkampf", "Ringen (ohne Rüstung)", "Ringen (mit Rüstung)") where ... eq weaponLabel
!mmm   
!mmm     set weaponType = "waffenlos"
!mmm   
!mmm   end if
!mmm   
!mmm   return weaponType
!mmm 
!mmm end function
!rem
!rem // m3mgdRangedWeaponProperties(weaponLabel)
!rem // 
!rem //   If weaponLabel is exactly one of a list of standard attack or defense arms, returns a list of weaponProperties. 
!rem //   Otherwise returns unknown. 
!rem //   weaponProperties := (weaponType, lowRangeUpperBound, midRangeUpperBound, farRangeUpperBound, ammoLabel)
!rem // 
!mmm function m3mgdRangedWeaponProperties(weaponLabel)
!mmm 
!mmm   set weaponProperties = unknown
!mmm   
!mmm   if weaponLabel eq "Wurfaxt" or weaponLabel eq "Wurfhammer"
!mmm   
!mmm     set weaponProperties = ("Stielwurfwaffe", 10, 20, 30, weaponLabel)
!mmm   
!mmm   else if weaponLabel eq "Wurfeisen" or weaponLabel eq "Wurfkeule"
!mmm   
!mmm     set weaponProperties = ("Stielwurfwaffe", 10, 20, 50, weaponLabel)
!mmm   
!mmm   else if weaponLabel eq "Wurfmesser"
!mmm   
!mmm     set weaponProperties = ("Wurfklinge", 10, 15, 20, weaponLabel)
!mmm   
!mmm   else if weaponLabel eq "Wurfpfeil"
!mmm   
!mmm     set weaponProperties = ("Wurfklinge", 10, 20, 30, weaponLabel)
!mmm   
!mmm   else if weaponLabel eq "Wurfscheibe"
!mmm   
!mmm     set weaponProperties = ("Wurfscheibe", 10, 20, 30, weaponLabel)
!mmm   
!mmm   else if weaponLabel eq "Wurfstern"
!mmm   
!mmm     set weaponProperties = ("Wurfscheibe", 5, 10, 15, weaponLabel)
!mmm   
!mmm   else if weaponLabel eq "Speerschleuder"
!mmm   
!mmm     set weaponProperties = ("Wurfspieß", 20, 40, 70, weaponLabel)
!mmm   
!mmm   else if weaponLabel eq "Wurfspeer"
!mmm   
!mmm     set weaponProperties = ("Wurfspieß", 10, 30, 50, weaponLabel)
!mmm   
!mmm   else if weaponLabel eq "Wurfspieß"
!mmm   
!mmm     set weaponProperties = ("Wurfspieß", 10, 20, 30, weaponLabel)
!mmm   
!mmm   else if weaponLabel eq "leichte Armbrust"
!mmm   
!mmm     set weaponProperties = (weaponLabel, 30, 120, 220, "Bolzen")
!mmm   
!mmm   else if weaponLabel eq "schwere Armbrust"
!mmm   
!mmm     set weaponProperties = (weaponLabel, 30, 120, 240, "Bolzen")
!mmm   
!mmm   else if weaponLabel eq "Handarmbrust"
!mmm   
!mmm     set weaponProperties = (weaponLabel, 20, 40, 70, "Bolzen")
!mmm   
!mmm   else if weaponLabel eq "langes Blasrohr"
!mmm   
!mmm     set weaponProperties = ("Blasrohr", 10, 15, 20, "Pfeile")
!mmm   
!mmm   else if weaponLabel eq "kurzes Blasrohr"
!mmm   
!mmm     set weaponProperties = ("Blasrohr", 5, 7, 10, "Pfeile")
!mmm   
!mmm   else if weaponLabel eq "Bogen"
!mmm   
!mmm     set weaponProperties = (weaponLabel, 30, 100, 180, "Pfeile")
!mmm   
!mmm   else if weaponLabel eq "Kurzbogen"
!mmm   
!mmm     set weaponProperties = (weaponLabel, 30, 80, 120, "Pfeile")
!mmm   
!mmm   else if weaponLabel eq "Kompositbogen"
!mmm   
!mmm     set weaponProperties = (weaponLabel, 30, 120, 200, "Pfeile")
!mmm   
!mmm   else if weaponLabel eq "Langbogen"
!mmm   
!mmm     set weaponProperties = (weaponLabel, 30, 120, 200, "Pfeile")
!mmm   
!mmm   else if weaponLabel eq "Schleuder"
!mmm   
!mmm     set weaponProperties = ("Schleuder", 30, 60, 90, "Kugeln")
!mmm   
!mmm   else if weaponLabel eq "Stockschleuder"
!mmm   
!mmm     set weaponProperties = ("Schleuder", 50, 100, 180, "Kugeln")
!mmm   
!mmm   end if
!mmm   
!mmm   return weaponProperties
!mmm 
!mmm end function
!rem
!mmm function m3mgdBuildWeaponSelector(characterID, weaponsGroup)  
!mmm   
!mmm   if isdefault(characterID)
!mmm     set characterID = sender.character_id
!mmm   end if
!mmm   
!mmm   if isdefault(weaponsGroup)
!mmm     set weaponsGroup = "melee"
!mmm   end if
!mmm   
!mmm   if weaponsGroup eq "defense"
!mmm     set weaponsList = m3mgdListDefenseWeapons(characterID)
!mmm     set selectorPrompt = "Abwehrwaffe"
!mmm   else if weaponsGroup eq "melee"
!mmm     set weaponsList = m3mgdListMeleeAttackWeapons(characterID)
!mmm     set selectorPrompt = "Nahkampfwaffe"
!mmm   else if weaponsGroup eq "ranged"
!mmm     set weaponsList = m3mgdListRangedAttackWeapons(characterID)
!mmm     set selectorPrompt = "Fernkampfwaffe"
!mmm   end if
!mmm   
!mmm   set r20SelectorCode = ""
!mmm   for weaponLabelAttr in weaponsList
!mmm     if r20SelectorCode ne ""
!mmm       set r20SelectorCode = r20SelectorCode & "|"
!mmm     end if
!mmm     set r20SelectorCode = r20SelectorCode & characterID.(weaponLabelAttr)
!mmm   end for
!mmm   set r20SelectorCode = "?" & "{" & selectorPrompt & "|" & r20SelectorCode & "}"
!mmm   
!mmm   set selectorAttr = "m3mgd_" & weaponsGroup & "_weapon_selector"
!mmm   do setattr(characterID, selectorAttr, r20SelectorCode)
!mmm   return selectorAttr
!mmm end function
!rem
!mmm   if selected
!mmm     set charID = selected.character_id
!mmm   else 
!mmm     set charID = sender.character_id
!mmm   end if
!mmm   
!mmm   set configScriptsRequired = 0
!mmm
!mmm   combine chat using "<" & "br>"
!mmm
!mmm     chat: ============
!mmm     chat: Charakterbogen: **${charID.character_name}**
!mmm     chat: ------------
!mmm     chat: Verteidigungswaffen
!mmm   
!mmm     for weaponLabelAttr in m3mgdListDefenseWeapons(charID)
!mmm       
!mmm       set weaponLabel = charID.(weaponLabelAttr)
!mmm       set weaponProperties = m3mgdDefenseWeaponType(weaponLabel)
!mmm     
!mmm       if not isunknown(weaponProperties)
!mmm         chat: *${weaponLabel}* als Standardwaffe erkannt.
!mmm       else 
!mmm         chat: *${weaponLabel}* **braucht ein Konfigskript.**
!mmm       end if
!mmm     
!mmm     end for
!mmm
!mmm     chat: ------------
!mmm     chat: Angriffswaffen
!mmm
!mmm     for weaponLabelAttr in findattr(charID, "Angriff", "Waffe")
!mmm       
!mmm       set weaponLabel = charID.(weaponLabelAttr)
!mmm       
!mmm       if charID.(findattr(charID, "Angriff", "Waffe", weaponLabel, "FkWaffe")) == 1
!mmm         set weaponProperties = m3mgdRangedWeaponProperties(weaponLabel)
!mmm       else 
!mmm         set weaponProperties = m3mgdMeleeWeaponType(weaponLabel)
!mmm       end if
!mmm     
!mmm       if not isunknown(weaponProperties)
!mmm         chat: *${weaponLabel}* als Standardwaffe erkannt.
!mmm       else 
!mmm         chat: *${weaponLabel}* **braucht ein Konfigskript.**
!mmm         set configScriptsRequired = configScriptsRequired + 1
!mmm       end if
!mmm     
!mmm     end for
!mmm
!mmm     chat: ------------
!mmm     chat: Dropdown Verteidigungswaffen (Attribut '${m3mgdBuildWeaponSelector(charID, "defense")}') aktualisiert.
!mmm     chat: Dropdown Nahkampfwaffen (Attribut '${m3mgdBuildWeaponSelector(charID, "melee")}') aktualisiert.
!mmm     chat: Dropdown Fernkampfwaffen (Attribut '${m3mgdBuildWeaponSelector(charID, "ranged")}') aktualisiert.
!mmm
!mmm     chat: ------------
!mmm     chat: **${charID.character_name}** braucht **${configScriptsRequired} Konfigskripte.**
!mmm     chat: ============
!mmm
!mmm   end combine
!mmm
!mmm end script