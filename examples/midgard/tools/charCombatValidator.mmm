!mmm script
!mmm   
!rem
!mmm function m3mgdBuildWeaponSelector(characterID, weaponsGroup)  
!mmm   
!mmm   if isdefault(characterID)
!mmm     set characterID = sender.character_id
!mmm   end if
!mmm   
!mmm   if isdefault(weaponsGroup)
!mmm     set weaponsGroup = "melee"
!mmm   end if
!mmm   
!mmm   if weaponsGroup eq "defense"
!mmm     set weaponsList = m3mgdListDefenseWeapons(characterID)
!mmm     set selectorPrompt = "Abwehrwaffe"
!mmm   else if weaponsGroup eq "melee"
!mmm     set weaponsList = m3mgdListMeleeAttackWeapons(characterID)
!mmm     set selectorPrompt = "Nahkampfwaffe"
!mmm   else if weaponsGroup eq "ranged"
!mmm     set weaponsList = m3mgdListRangedAttackWeapons(characterID)
!mmm     set selectorPrompt = "Fernkampfwaffe"
!mmm   end if
!mmm   
!mmm   set r20SelectorCode = ""
!mmm   for weaponLabelAttr in weaponsList
!mmm     if r20SelectorCode ne ""
!mmm       set r20SelectorCode = r20SelectorCode & "|"
!mmm     end if
!mmm     set r20SelectorCode = r20SelectorCode & characterID.(weaponLabelAttr)
!mmm   end for
!mmm   set r20SelectorCode = "?" & "{" & selectorPrompt & "|" & r20SelectorCode & "}"
!mmm   
!mmm   set selectorAttr = "m3mgd_" & weaponsGroup & "_weapon_selector"
!mmm   do setattr(characterID, selectorAttr, r20SelectorCode)
!mmm   return selectorAttr
!mmm end function
!rem
!mmm function m3mgdBuildWeaponSelectorChatMenu(characterID, weaponsGroup)  
!mmm   
!mmm   if isdefault(characterID)
!mmm     set characterID = sender.character_id
!mmm   end if
!mmm   
!mmm   if isdefault(weaponsGroup)
!mmm     set weaponsGroup = "melee"
!mmm   end if
!mmm   
!mmm   if weaponsGroup eq "defense"
!mmm     set weaponsList = m3mgdListDefenseWeapons(characterID)
!mmm     set selectorPrompt = "Abwehrwaffe"
!mmm   else if weaponsGroup eq "melee"
!mmm     set weaponsList = m3mgdListMeleeAttackWeapons(characterID)
!mmm     set selectorPrompt = "Nahkampfwaffe"
!mmm   else if weaponsGroup eq "ranged"
!mmm     set weaponsList = m3mgdListRangedAttackWeapons(characterID)
!mmm     set selectorPrompt = "Fernkampfwaffe"
!mmm   end if
!mmm   
!mmm   set chatMenu = ""
!mmm   set itemCounter = 0
!mmm   for weaponLabelAttr in weaponsList
!mmm     set weaponLabel = characterID.(weaponLabelAttr)
!mmm     if weaponsGroup eq "defense"
!mmm       set weaponType = m3mgdDefenseWeaponType(weaponLabel)
!mmm       if characterID.PC
!mmm         set scriptCommand = "&#x25;{MacroSheet|defense}"
!mmm       else
!mmm         set scriptCommand = "&#x25;{GM_NPC_Makros|NPC_Abwehr}"
!mmm       end if
!mmm     else if weaponsGroup eq "melee"
!mmm       set weaponType = m3mgdMeleeWeaponType(weaponLabel)
!mmm       if characterID.PC
!mmm         set scriptCommand = "&#x25;{MacroSheet|meleeAttack}"
!mmm       else
!mmm         set scriptCommand = "&#x25;{GM_NPC_Makros|NPC_Nahkampfangriff}"
!mmm       end if
!mmm     else if weaponsGroup eq "ranged"
!mmm       set weaponProps = m3mgdRangedWeaponProperties(weaponLabel)
!mmm       set weaponType = weaponProps[0]
!mmm       if characterID.PC
!mmm         set scriptCommand = "&#x25;{MacroSheet|rangedAttack}"
!mmm       else
!mmm         set scriptCommand = "&#x25;{GM_NPC_Makros|NPC_Fernkampfangriff}"
!mmm       end if
!mmm     end if
!mmm     set payload = "!mmm customize&#13;!mmm set cWeaponLabel&#x3D;&#x22;" & "&#x40;" & "{" & characterID.name & "|" & weaponLabelAttr & "}" & "&#x22;&#13;!mmm set cWeaponType&#x3D;&#x22;" & weaponType & "&#x22;&#13;!mmm end customize&#13;" & scriptCommand
!mmm     if itemCounter % 2 == 0
!mmm       set chatMenu = chatMenu & "{{[" & weaponLabel & "](" & payload & ")="
!mmm     else
!mmm       set chatMenu = chatMenu & "[**" & weaponLabel & "**](" & payload & ")}}"
!mmm     end if
!mmm     set itemCounter = itemCounter + 1
!mmm   end for
!mmm   if itemCounter % 2 != 0
!mmm     set chatMenu = chatMenu & "}}"
!mmm   end if
!mmm   set chatMenu = "&" & "{template:default} {{name=" & selectorPrompt & "}} " & chatMenu
!mmm   
!mmm   chat: /w "${characterID.name}" ${chatMenu}
!mmm   return true
!mmm end function
!mmm
!mmm
!mmm   if selected
!mmm     set charID = selected.character_id
!mmm   else 
!mmm     set charID = sender.character_id
!mmm   end if
!mmm   
!mmm   set configScriptsRequired = 0
!mmm
!mmm   combine chat using "<" & "br>"
!mmm
!mmm     chat: ============
!mmm     chat: Charakterbogen: **${charID.character_name}**
!mmm     chat: ------------
!mmm     chat: Verteidigungswaffen
!mmm   
!mmm     for weaponLabelAttr in m3mgdListDefenseWeapons(charID)
!mmm       
!mmm       set weaponLabel = charID.(weaponLabelAttr)
!mmm       set weaponProperties = m3mgdDefenseWeaponType(weaponLabel)
!mmm     
!mmm       if not isunknown(weaponProperties)
!mmm         chat: *${weaponLabel}* als Standardwaffe erkannt.
!mmm       else 
!mmm         chat: *${weaponLabel}* **braucht ein Konfigskript.**
!mmm         set configScriptsRequired = configScriptsRequired + 1
!mmm       end if
!mmm     
!mmm     end for
!mmm
!mmm     chat: ------------
!mmm     chat: Angriffswaffen
!mmm
!mmm     for weaponLabelAttr in findattr(charID, "Angriff", "Waffe")
!mmm       
!mmm       set weaponLabel = charID.(weaponLabelAttr)
!mmm       
!mmm       if charID.(findattr(charID, "Angriff", "Waffe", weaponLabel, "FkWaffe")) == 1
!mmm         set weaponProperties = m3mgdRangedWeaponProperties(weaponLabel)
!mmm       else 
!mmm         set weaponProperties = m3mgdMeleeWeaponType(weaponLabel)
!mmm       end if
!mmm     
!mmm       if not isunknown(weaponProperties)
!mmm         chat: *${weaponLabel}* als Standardwaffe erkannt.
!mmm       else 
!mmm         chat: *${weaponLabel}* **braucht ein Konfigskript.**
!mmm         set configScriptsRequired = configScriptsRequired + 1
!mmm       end if
!mmm     
!mmm     end for
!mmm
!mmm     chat: ------------
!mmm     do m3mgdBuildWeaponSelectorChatMenu(charID, "defense")
!mmm     do m3mgdBuildWeaponSelectorChatMenu(charID, "melee")
!mmm     do m3mgdBuildWeaponSelectorChatMenu(charID, "ranged")
!mmm
!mmm     chat: ------------
!mmm     chat: **${charID.character_name}** braucht **${configScriptsRequired} Konfigskripte.**
!mmm     chat: ============
!mmm
!mmm   end combine
!mmm
!mmm end script