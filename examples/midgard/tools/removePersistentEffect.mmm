!rem // removePersistentEffect v0.1 2024-02-04 phr
!rem //
!rem // Removes an active persistent effect identified by tokenID and removeEffectIndex.
!rem 
!mmm script
!mmm   set customizable cmd = default
!mmm   set customizable tokenID = default
!mmm   set customizable removeEffectIndex = default
!mmm   set customizable deleteConfirm = false
!mmm   if cmd eq "removeEffect" and deleteConfirm
!mmm     set effectsDB = deserialize(m3mgdExchange.m3mgdActivePersistentEffects)
!mmm     set indexCounter = 0
!mmm     for statusRule in effectsDB.(tokenID)
!mmm       if indexCounter != removeEffectIndex
!mmm         set updatedActiveEffects = updatedActiveEffects, statusRule
!mmm       else
!mmm         if statusRule.effect.cappedAttribute
!mmm           do setattr(tokenID, statusRule.effect.cappedAttribute, tokenID.(statusRule.effect.cappedAttribute).max)
!mmm         end if
!mmm         if activeEffect.effect.marker
!mmm           do setattr(tokenID, statusRule.effect.marker, false)
!mmm         end if
!mmm         if tokenID.status_blue > 1
!mmm           do setattr(tokenID, "status_blue", tokenID.status_blue - 1)
!mmm         else
!mmm           do setattr(tokenID, "status_blue", false)
!mmm         end if 
!mmm       end if
!mmm       set indexCounter = indexCounter + 1
!mmm     end for
!mmm     
!mmm     do setattr(m3mgdExchange, "m3mgdActivePersistentEffects", serialize({ effectsDB, (tokenID): updatedActiveEffects }))
!mmm
!mmm     chat: Removed one active effect on ${highlight(tokenID.token_name, "info", tokenID)}.
!mmm   end if
!mmm
!mmm end script